# ä»»åŠ¡åˆ‡æ¢

## ä¸€ã€å†…æ ¸ç¨‹åºä»£ç 

```armasm{.line-numbers}
       ;ä»£ç æ¸…å•15-1
       ;æ–‡ä»¶åï¼šc15_core.asm
       ;æ–‡ä»¶è¯´æ˜ï¼šä¿æŠ¤æ¨¡å¼å¾®å‹æ ¸å¿ƒç¨‹åº 

       ;ä»¥ä¸‹å¸¸é‡å®šä¹‰éƒ¨åˆ†ã€‚å†…æ ¸çš„å¤§éƒ¨åˆ†å†…å®¹éƒ½åº”å½“å›ºå®š
       ;å†…æ ¸ä»£ç æ®µé€‰æ‹©å­ 
       core_code_seg_sel     equ  0x38
       ;å†…æ ¸æ•°æ®æ®µé€‰æ‹©å­
       core_data_seg_sel     equ  0x30
       ;ç³»ç»Ÿå…¬å…±ä¾‹ç¨‹ä»£ç æ®µçš„é€‰æ‹©å­ 
       sys_routine_seg_sel   equ  0x28
       ;è§†é¢‘æ˜¾ç¤ºç¼“å†²åŒºçš„æ®µé€‰æ‹©å­ 
       video_ram_seg_sel     equ  0x20
       ;å†…æ ¸å †æ ˆæ®µé€‰æ‹©å­
       core_stack_seg_sel    equ  0x18
       ;æ•´ä¸ª 0-4GB å†…å­˜çš„æ®µçš„é€‰æ‹©å­
       mem_0_4_gb_seg_sel    equ  0x08

       ;ä»¥ä¸‹æ˜¯ç³»ç»Ÿæ ¸å¿ƒçš„å¤´éƒ¨ï¼Œç”¨äºåŠ è½½æ ¸å¿ƒç¨‹åº 
       core_length      dd core_end       ;æ ¸å¿ƒç¨‹åºæ€»é•¿åº¦#00
       sys_routine_seg  dd section.sys_routine.start    ;ç³»ç»Ÿå…¬ç”¨ä¾‹ç¨‹æ®µä½ç½®#04
       core_data_seg    dd section.core_data.start      ;æ ¸å¿ƒæ•°æ®æ®µä½ç½®#08
       core_code_seg    dd section.core_code.start      ;æ ¸å¿ƒä»£ç æ®µä½ç½®#0c
       core_entry       dd start                        ;æ ¸å¿ƒä»£ç æ®µå…¥å£ç‚¹#10
                        dw core_code_seg_sel

       [bits 32]
;==============================ğŸ¥«ç³»ç»Ÿå…¬å…±ä¾‹ç¨‹ä»£ç æ®µğŸ¥«===============================
SECTION sys_routine vstart=0

       ;å­—ç¬¦ä¸²æ˜¾ç¤ºä¾‹ç¨‹
       ;æ˜¾ç¤º 0 ç»ˆæ­¢çš„å­—ç¬¦ä¸²å¹¶ç§»åŠ¨å…‰æ ‡ 
       ;è¾“å…¥ï¼šDS:EBX=ä¸²åœ°å€
put_string:                                 
       push ecx
  .getc:
       mov cl,[ebx]
       or cl,cl
       jz .exit
       call put_char
       inc ebx
       jmp .getc

  .exit:
       pop ecx
       retf                              

;åœ¨å½“å‰å…‰æ ‡å¤„æ˜¾ç¤ºä¸€ä¸ªå­—ç¬¦,å¹¶æ¨è¿›
;å…‰æ ‡ã€‚ä»…ç”¨äºæ®µå†…è°ƒç”¨ 
;è¾“å…¥ï¼šCL=å­—ç¬¦ASCIIç 
put_char:                                    
       pushad

       ;ä»¥ä¸‹å–å½“å‰å…‰æ ‡ä½ç½®
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;é«˜å­—
       mov ah,al

       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;ä½å­—
       mov bx,ax                          ;BX=ä»£è¡¨å…‰æ ‡ä½ç½®çš„16ä½æ•°

       cmp cl,0x0d                        ;å›è½¦ç¬¦ï¼Ÿ
       jnz .put_0a
       mov ax,bx
       mov bl,80
       div bl
       mul bl
       mov bx,ax
       jmp .set_cursor

.put_0a:
       cmp cl,0x0a                        ;æ¢è¡Œç¬¦ï¼Ÿ
       jnz .put_other
       add bx,80
       jmp .roll_screen

.put_other:                               ;æ­£å¸¸æ˜¾ç¤ºå­—ç¬¦
       push es
       mov eax,video_ram_seg_sel          ;0xb8000æ®µçš„é€‰æ‹©å­
       mov es,eax
       shl bx,1
       mov [es:bx],cl
       pop es

       ;ä»¥ä¸‹å°†å…‰æ ‡ä½ç½®æ¨è¿›ä¸€ä¸ªå­—ç¬¦
       shr bx,1
       inc bx

.roll_screen:
       cmp bx,2000                        ;å…‰æ ‡è¶…å‡ºå±å¹•ï¼Ÿæ»šå±
       jl .set_cursor

       push ds
       push es
       mov eax,video_ram_seg_sel
       mov ds,eax
       mov es,eax
       cld
       mov esi,0xa0                       ;å°å¿ƒï¼32ä½æ¨¡å¼ä¸‹movsb/w/d 
       mov edi,0x00                       ;ä½¿ç”¨çš„æ˜¯esi/edi/ecx 
       mov ecx,1920
       rep movsd
       mov bx,3840                        ;æ¸…é™¤å±å¹•æœ€åº•ä¸€è¡Œ
       mov ecx,80                         ;32ä½ç¨‹åºåº”è¯¥ä½¿ç”¨ECX
.cls:
       mov word[es:bx],0x0720
       add bx,2
       loop .cls

       pop es
       pop ds

       mov bx,1920

.set_cursor:
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       mov al,bh
       out dx,al
       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       mov al,bl
       out dx,al

       popad
       
       ret                                

;ä»ç¡¬ç›˜è¯»å–ä¸€ä¸ªé€»è¾‘æ‰‡åŒº
;eax=é€»è¾‘æ‰‡åŒºå·
;ds:ebx=ç›®æ ‡ç¼“å†²åŒºåœ°å€
;è¿”å›ï¼šebx=ebx+512
read_hard_disk_0:                           
       push eax 
       push ecx
       push edx

       push eax
       
       mov dx,0x1f2
       mov al,1
       out dx,al                          ;è¯»å–çš„æ‰‡åŒºæ•°

       inc dx                             ;0x1f3
       pop eax
       out dx,al                          ;LBAåœ°å€7~0

       inc dx                             ;0x1f4
       mov cl,8
       shr eax,cl
       out dx,al                          ;LBAåœ°å€15~8

       inc dx                             ;0x1f5
       shr eax,cl
       out dx,al                          ;LBAåœ°å€23~16

       inc dx                             ;0x1f6
       shr eax,cl
       or al,0xe0                         ;ç¬¬ä¸€ç¡¬ç›˜  LBAåœ°å€27~24
       out dx,al

       inc dx                             ;0x1f7
       mov al,0x20                        ;è¯»å‘½ä»¤
       out dx,al

.waits:
       in al,dx
       and al,0x88
       cmp al,0x08
       jnz .waits                         ;ä¸å¿™ï¼Œä¸”ç¡¬ç›˜å·²å‡†å¤‡å¥½æ•°æ®ä¼ è¾“ 

       mov ecx,256                        ;æ€»å…±è¦è¯»å–çš„å­—æ•°
       mov dx,0x1f0
.readw:
       in ax,dx
       mov [ebx],ax
       add ebx,2
       loop .readw

       pop edx
       pop ecx
       pop eax

       retf                               ;æ®µé—´è¿”å› 
      
;åˆ†é…å†…å­˜
;è¾“å…¥ï¼šecx=å¸Œæœ›åˆ†é…çš„å­—èŠ‚æ•°
;è¾“å‡ºï¼šecx=èµ·å§‹çº¿æ€§åœ°å€ 
allocate_memory:                            

       push ds
       push eax
       push ebx

       mov eax,core_data_seg_sel
       mov ds,eax

       mov eax,[ram_alloc]
       ;ä¸‹ä¸€æ¬¡åˆ†é…æ—¶çš„èµ·å§‹åœ°å€
       add eax,ecx                      

       ;è¿™é‡Œåº”å½“æœ‰æ£€æµ‹å¯ç”¨å†…å­˜æ•°é‡çš„æŒ‡ä»¤
       ;è¿”å›åˆ†é…çš„èµ·å§‹åœ°å€
       mov ecx,[ram_alloc]                

       mov ebx,eax
       and ebx,0xfffffffc
       ;å¼ºåˆ¶å¯¹é½
       add ebx,4                          
       ;ä¸‹æ¬¡åˆ†é…çš„èµ·å§‹åœ°å€æœ€å¥½æ˜¯ 4 å­—èŠ‚å¯¹é½ 
       test eax,0x00000003                
       ;å¦‚æœæ²¡æœ‰å¯¹é½ï¼Œåˆ™å¼ºåˆ¶å¯¹é½
       ;cmovcc æŒ‡ä»¤å¯ä»¥é¿å…æ§åˆ¶è½¬ç§» 
       cmovnz eax,ebx                     
       ;ä¸‹æ¬¡ä»è¯¥åœ°å€åˆ†é…å†…å­˜ 
       mov [ram_alloc],eax                
       
       pop ebx
       pop eax
       pop ds

       retf

;åœ¨ GDT å†…å®‰è£…ä¸€ä¸ªæ–°çš„æè¿°ç¬¦
;è¾“å…¥ï¼šedx:eax=æè¿°ç¬¦ 
;è¾“å‡ºï¼šcx=æè¿°ç¬¦çš„é€‰æ‹©å­
set_up_gdt_descriptor:                      

       push eax
       push ebx
       push edx

       push ds
       push es

       ;åˆ‡æ¢åˆ°æ ¸å¿ƒæ•°æ®æ®µ     
       mov ebx,core_data_seg_sel          
       mov ds,ebx
       ;ä»¥ä¾¿å¼€å§‹å¤„ç† GDT
       sgdt [pgdt]                        

       mov ebx,mem_0_4_gb_seg_sel
       mov es,ebx
       ;GDT ç•Œé™
       movzx ebx,word [pgdt]              
       ;GDT æ€»å­—èŠ‚æ•°ï¼Œä¹Ÿæ˜¯ä¸‹ä¸€ä¸ªæè¿°ç¬¦åç§»
       inc bx                             
       ;ä¸‹ä¸€ä¸ªæè¿°ç¬¦çš„çº¿æ€§åœ°å€
       add ebx,[pgdt+2]                   

       mov [es:ebx],eax
       mov [es:ebx+4],edx
       ;å¢åŠ ä¸€ä¸ªæè¿°ç¬¦çš„å¤§å°
       add word [pgdt],8                  
       ;å¯¹ GDT çš„æ›´æ”¹ç”Ÿæ•ˆ
       lgdt [pgdt]                        
       ;å¾—åˆ° GDT ç•Œé™å€¼
       mov ax,[pgdt]                      
       xor dx,dx
       mov bx,8
       ;é™¤ä»¥ 8ï¼Œå»æ‰ä½™æ•°
       div bx                             
       mov cx,ax
       ;å°†ç´¢å¼•å·ç§»åˆ°æ­£ç¡®ä½ç½®
       shl cx,3                           

       pop es
       pop ds

       pop edx
       pop ebx
       pop eax

       retf

;æ„é€ å­˜å‚¨å™¨å’Œç³»ç»Ÿçš„æ®µæè¿°ç¬¦
;è¾“å…¥ï¼šeax=çº¿æ€§åŸºåœ°å€
;      ebx=æ®µç•Œé™
;      ecx=å±æ€§ã€‚å„å±æ€§ä½éƒ½åœ¨åŸå§‹ä½ç½®ï¼Œæ— å…³çš„ä½æ¸…é›¶ 
;è¿”å›ï¼šedx:eax=æè¿°ç¬¦
make_seg_descriptor:    
                   
       mov edx,eax
       shl eax,16
       ;æè¿°ç¬¦å‰ 32 ä½ (EAX) æ„é€ å®Œæ¯•
       or ax,bx                           
       ;æ¸…é™¤åŸºåœ°å€ä¸­æ— å…³çš„ä½
       and edx,0xffff0000                 
       rol edx,8
       ;è£…é…åŸºå€çš„ 31~24 å’Œ 23~16 (80486+)
       bswap edx                          

       xor bx,bx
       ;è£…é…æ®µç•Œé™çš„é«˜ 4 ä½
       or edx,ebx                         
       ;è£…é…å±æ€§
       or edx,ecx                         

       retf

;æ„é€ é—¨çš„æè¿°ç¬¦ï¼ˆè°ƒç”¨é—¨ç­‰ï¼‰
;è¾“å…¥ï¼šeax=é—¨ä»£ç åœ¨æ®µå†…åç§»åœ°å€
;       bx=é—¨ä»£ç æ‰€åœ¨æ®µçš„é€‰æ‹©å­ 
;       cx=æ®µç±»å‹åŠå±æ€§ç­‰ï¼ˆå„å±æ€§ä½éƒ½åœ¨åŸå§‹ä½ç½®ï¼‰
;è¿”å›ï¼šedx:eax=å®Œæ•´çš„æè¿°ç¬¦
make_gate_descriptor:                       

       push ebx
       push ecx

       mov edx,eax
       ;å¾—åˆ°åç§»åœ°å€é«˜ 16 ä½
       and edx,0xffff0000                 
       ;ç»„è£…å±æ€§éƒ¨åˆ†åˆ° edx 
       or dx,cx             

       ;å¾—åˆ°åç§»åœ°å€ä½ 16 ä½
       and eax,0x0000ffff                  
       shl ebx,16                         
       ;ç»„è£…æ®µé€‰æ‹©å­éƒ¨åˆ† 
       or eax,ebx                         

       pop ecx
       pop ebx

       retf                                                               

;ç»ˆæ­¢å½“å‰ä»»åŠ¡ï¼Œæ³¨æ„ï¼Œæ‰§è¡Œæ­¤ä¾‹ç¨‹æ—¶ï¼Œå½“å‰ä»»åŠ¡ä»åœ¨è¿è¡Œä¸­ã€‚æ­¤ä¾‹ç¨‹å…¶å®ä¹Ÿæ˜¯å½“å‰ä»»åŠ¡çš„ä¸€éƒ¨åˆ† 
terminate_current_task:                     
       
       ;pushfd ç”¨äºå°† EFLAGS å¯„å­˜å™¨çš„å†…å®¹å‹å…¥å †æ ˆï¼ˆ32 ä½æ¨¡å¼ä¸‹ï¼‰
       pushfd
       ;è·å¾— EFLAGS å¯„å­˜å™¨å†…å®¹
       mov edx,[esp]
       ;æ¢å¤å †æ ˆæŒ‡é’ˆï¼Œä½¿å †æ ˆå¹³è¡¡ï¼Œä¿æŒå‹å…¥ EFLAGS å¯„å­˜å™¨å‰çš„çŠ¶æ€
       add esp,4

       mov eax,core_data_seg_sel
       mov ds,eax

       ;æµ‹è¯• NT ä½
       test dx,0100_0000_0000_0000B       
       ;å½“å‰ä»»åŠ¡æ˜¯åµŒå¥—çš„ï¼Œåˆ° .b1 æ‰§è¡Œ iretd
       jnz .b1
       ;å½“å‰ä»»åŠ¡ä¸æ˜¯åµŒå¥—çš„ï¼Œç›´æ¥ä½¿ç”¨ jmp æŒ‡ä»¤åˆ‡æ¢åˆ°å†…æ ¸ç¨‹åºç®¡ç†å™¨ä»»åŠ¡
       mov ebx,core_msg1                   
       call sys_routine_seg_sel:put_string
       ;ç¨‹åºç®¡ç†å™¨ä»»åŠ¡
       ;å› ä¸ºæ­¤æ—¶ç¨‹åºç®¡ç†å™¨å’Œå½“å‰ä»»åŠ¡æ²¡æœ‰å½¢æˆåµŒå¥—å…³ç³»ï¼Œæ‰€ä»¥åœ¨è¿”å›æ—¶ä¹Ÿå¿…é¡»è¦ç”¨ jmp
       jmp far [prgman_tss]

.b1: 
       mov ebx,core_msg0
       call sys_routine_seg_sel:put_string
       iretd
      
sys_routine_end:

;============================ğŸ§†ç³»ç»Ÿæ ¸å¿ƒçš„æ•°æ®æ®µğŸ§†================================
SECTION core_data vstart=0 
       
       ;ç”¨äºè®¾ç½®å’Œä¿®æ”¹ GDT
       pgdt             dw  0              
                        dd  0

       ;ä¸‹æ¬¡åˆ†é…å†…å­˜æ—¶çš„èµ·å§‹åœ°å€
       ram_alloc        dd  0x00100000    

       ;ç¬¦å·åœ°å€æ£€ç´¢è¡¨
       salt:
       salt_1           db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        dw  sys_routine_seg_sel

       salt_2           db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  sys_routine_seg_sel

       salt_3           db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  sys_routine_seg_sel

       salt_4           db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  terminate_current_task
                        dw  sys_routine_seg_sel

       salt_item_len    equ $-salt_4
       salt_items       equ ($-salt)/salt_item_len

       message_1        db  '  If you seen this message,that means we '
                        db  'are now in protect mode,and the system '
                        db  'core is loaded,and the video display '
                        db  'routine works perfectly.',0x0d,0x0a,0

       message_2        db  '  System wide CALL-GATE mounted.',0x0d,0x0a,0
       
       bin_hex          db '0123456789ABCDEF'
       ;å†…æ ¸ç”¨çš„ç¼“å†²åŒº
       core_buf         times 2048 db 0

       cpu_brnd0        db 0x0d,0x0a,'  ',0
       cpu_brand        times 52 db 0
       cpu_brnd1        db 0x0d,0x0a,0x0d,0x0a,0

       ;ä»»åŠ¡æ§åˆ¶å—é“¾
       tcb_chain        dd  0

       ;ç¨‹åºç®¡ç†å™¨çš„ä»»åŠ¡ä¿¡æ¯ 
       ;ç¨‹åºç®¡ç†å™¨çš„ TSS åŸºåœ°å€å’Œ TSS æè¿°ç¬¦é€‰æ‹©å­
       prgman_tss       dd  0             
                        dw  0

       prgman_msg1      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: Hello! I am Program Manager,'
                        db  'run at CPL=0.Now,create user task and switch '
                        db  'to it by the CALL instruction...',0x0d,0x0a,0
              
       prgman_msg2      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: I am glad to regain control.'
                        db  'Now,create another user task and switch to '
                        db  'it by the JMP instruction...',0x0d,0x0a,0
              
       prgman_msg3      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: I am gain control again,'
                        db  'HALT...',0

       core_msg0        db  0x0d,0x0a
                        db  '[SYSTEM CORE]: Uh...This task initiated with '
                        db  'CALL instruction or an exeception/ interrupt,'
                        db  'should use IRETD instruction to switch back...'
                        db  0x0d,0x0a,0

       core_msg1        db  0x0d,0x0a
                        db  '[SYSTEM CORE]: Uh...This task initiated with '
                        db  'JMP instruction,  should switch to Program '
                        db  'Manager directly by the JMP instruction...'
                        db  0x0d,0x0a,0

core_data_end:
               
;=============================ğŸ§‡å†…æ ¸æ ¸å¿ƒä»£ç æ®µğŸ§‡=================================
SECTION core_code vstart=0

;åœ¨ LDT å†…å®‰è£…ä¸€ä¸ªæ–°çš„æè¿°ç¬¦
;è¾“å…¥ï¼šedx:eax=æè¿°ç¬¦
;          ebx=TCBåŸºåœ°å€
;è¾“å‡ºï¼šcx=æè¿°ç¬¦çš„é€‰æ‹©å­
fill_descriptor_in_ldt:                     

       push eax
       push edx
       push edi
       push ds

       mov ecx,mem_0_4_gb_seg_sel
       mov ds,ecx
       ;è·å¾— LDT åŸºåœ°å€
       mov edi,[ebx+0x0c]                 
       
       xor ecx,ecx
       ;è·å¾— LDT ç•Œé™
       mov cx,[ebx+0x0a]                  
       ;LDT çš„æ€»å­—èŠ‚æ•°ï¼Œå³æ–°æè¿°ç¬¦åç§»åœ°å€
       inc cx                             
       
       ;å®‰è£…æè¿°ç¬¦
       mov [edi+ecx+0x00],eax
       mov [edi+ecx+0x04],edx             

       add cx,8                           
       ;å¾—åˆ°æ–°çš„ LDT ç•Œé™å€¼
       dec cx                              
       ;æ›´æ–° LDT ç•Œé™å€¼åˆ° TCB
       mov [ebx+0x0a],cx                  

       mov ax,cx
       xor dx,dx
       mov cx,8
       div cx
       
       mov cx,ax
       ;å·¦ç§» 3 ä½ï¼Œå¹¶ä¸”ä½¿ TI ä½=1ï¼ŒæŒ‡å‘ LDTï¼Œæœ€åä½¿ RPL=00
       shl cx,3                           
       or cx,0000_0000_0000_0100B          

       pop ds
       pop edi
       pop edx
       pop eax

       ret
          
;åŠ è½½å¹¶é‡å®šä½ç”¨æˆ·ç¨‹åº
;è¾“å…¥: PUSH é€»è¾‘æ‰‡åŒºå·
;      PUSH ä»»åŠ¡æ§åˆ¶å—åŸºåœ°å€
;è¾“å‡ºï¼šæ— 
load_relocate_program:                      

       pushad

       push ds
       push es
       
       ;1.ä¸ºç”¨æˆ·ç¨‹åºåˆ›å»º LDT æ‰€éœ€è¦çš„å†…å­˜ç©ºé—´ï¼›
       ;2.å°†ç”¨æˆ·ç¨‹åºå…¨éƒ¨åŠ è½½åˆ°å†…å­˜ä¸­ï¼Œå¹¶ä¸”ä¸ºç”¨æˆ·ç¨‹åºä¸­çš„æ•°æ®æ®µã€ä»£ç æ®µã€æ ˆæ®µåˆ›å»ºæè¿°ç¬¦å®‰è£…åˆ° LDT ä¸­ï¼›
       ;3.é‡å®šä½ U-SALTï¼Œå°† U-SALT ä¸­çš„ä¾‹ç¨‹åæ›¿æ¢ä¸ºå¯¹åº”è°ƒç”¨é—¨çš„é€‰æ‹©å­å’Œåç§»é‡ï¼›
       ;4.ä¸ºç”¨æˆ·ç¨‹åºåˆ›å»º 0,1,2 ç‰¹æƒçº§æ ˆï¼Œåˆ†åˆ«ä¸ºè¿™ä¸‰ä¸ªç‰¹æƒçº§åˆ«çš„æ ˆåˆ›å»ºæè¿°ç¬¦ï¼Œå¹¶ä¸”å°†æè¿°ç¬¦å®‰è£…åˆ° LDT ä¸­ï¼›
       ;5.åœ¨ GDT ä¸­å®‰è£… LDT ç³»ç»Ÿæ®µæè¿°ç¬¦;
       ;6.ä¸ºç”¨æˆ·ç¨‹åºåˆ›å»º TSS æ‰€éœ€è¦çš„å†…å­˜ç©ºé—´ï¼Œå¹¶ä¸”ç™»è®° TSS è¡¨æ ¼çš„åŸºæœ¬å†…å®¹ï¼›
       ;7.ä¸º TSS åˆ›å»ºæè¿°ç¬¦å¹¶å®‰è£…åˆ° GDT ä¸­

       ;ä¸ºè®¿é—®é€šè¿‡å †æ ˆä¼ é€’çš„å‚æ•°åšå‡†å¤‡
       mov ebp,esp                        

       mov ecx,mem_0_4_gb_seg_sel
       mov es,ecx

       ;ä»å †æ ˆä¸­å–å¾— TCB çš„åŸºåœ°å€
       mov esi,[ebp+11*4]                 

       ;ä»¥ä¸‹ç”³è¯·åˆ›å»º LDT æ‰€éœ€è¦çš„å†…å­˜
       ;å…è®¸å®‰è£… 20 ä¸ª LDT æè¿°ç¬¦
       mov ecx,160                        
       call sys_routine_seg_sel:allocate_memory
       ;ç™»è®° LDT åŸºåœ°å€åˆ° TCB ä¸­
       mov [es:esi+0x0c],ecx              
       ;ç™»è®° LDT åˆå§‹çš„ç•Œé™åˆ° TCB ä¸­
       mov word [es:esi+0x0a],0xffff       

       ;ä»¥ä¸‹å¼€å§‹åŠ è½½ç”¨æˆ·ç¨‹åº 
       mov eax,core_data_seg_sel
       ;åˆ‡æ¢ DS åˆ°å†…æ ¸æ•°æ®æ®µ
       mov ds,eax                         

       ;ä»å †æ ˆä¸­å–å‡ºç”¨æˆ·ç¨‹åºèµ·å§‹æ‰‡åŒºå·
       mov eax,[ebp+12*4]                 
       ;è¯»å–ç¨‹åºå¤´éƒ¨æ•°æ® 
       mov ebx,core_buf                        
       call sys_routine_seg_sel:read_hard_disk_0

       ;ä»¥ä¸‹åˆ¤æ–­æ•´ä¸ªç¨‹åºæœ‰å¤šå¤§
       ;ç¨‹åºå°ºå¯¸
       mov eax,[core_buf]                 
       mov ebx,eax
       ;ä½¿ä¹‹ 512 å­—èŠ‚å¯¹é½ï¼ˆèƒ½è¢« 512 æ•´é™¤çš„æ•°ä½ 9 ä½éƒ½ä¸º 0ï¼‰
       and ebx,0xfffffe00                 
       add ebx,512                        
       ;ç¨‹åºçš„å¤§å°æ­£å¥½æ˜¯ 512 çš„å€æ•°å—? 
       test eax,0x000001ff                
       ;ä¸æ˜¯ã€‚ä½¿ç”¨å‡‘æ•´çš„ç»“æœ 
       cmovnz eax,ebx                     

       ;å®é™…éœ€è¦ç”³è¯·çš„å†…å­˜æ•°é‡
       mov ecx,eax                        
       call sys_routine_seg_sel:allocate_memory
       ;ç™»è®°ç¨‹åºåŠ è½½åŸºåœ°å€åˆ° TCB ä¸­
       mov [es:esi+0x06],ecx    

       ;ebx -> ç”³è¯·åˆ°çš„å†…å­˜é¦–åœ°å€
       mov ebx,ecx                        
       xor edx,edx
       mov ecx,512
       div ecx
       ;æ€»æ‰‡åŒºæ•°
       mov ecx,eax       

       ;åˆ‡æ¢ ds åˆ° 0-4GB çš„æ®µ
       mov eax,mem_0_4_gb_seg_sel         
       mov ds,eax
       ;èµ·å§‹æ‰‡åŒºå·
       mov eax,[ebp+12*4]                  
.b1:
       call sys_routine_seg_sel:read_hard_disk_0
       inc eax
       ;å¾ªç¯è¯»ï¼Œç›´åˆ°è¯»å®Œæ•´ä¸ªç”¨æˆ·ç¨‹åº
       loop .b1                           
       ;è·å¾—ç¨‹åºåŠ è½½åŸºåœ°å€
       mov edi,[es:esi+0x06]              

       ;å»ºç«‹ç¨‹åºå¤´éƒ¨æ®µæè¿°ç¬¦
       ;ç¨‹åºå¤´éƒ¨èµ·å§‹çº¿æ€§åœ°å€
       mov eax,edi                        
       ;æ®µé•¿åº¦
       mov ebx,[edi+0x04]                 
       ;æ®µç•Œé™
       dec ebx                            
       ;å­—èŠ‚ç²’åº¦çš„æ•°æ®æ®µæè¿°ç¬¦ï¼Œç‰¹æƒçº§ 3
       mov ecx,0x0040f200                  
       call sys_routine_seg_sel:make_seg_descriptor

       ;å®‰è£…å¤´éƒ¨æ®µæè¿°ç¬¦åˆ° LDT ä¸­ 
       ;TCB çš„åŸºåœ°å€
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;è®¾ç½®é€‰æ‹©å­çš„ç‰¹æƒçº§ä¸º 3
       or cx,0000_0000_0000_0011B         
       ;ç™»è®°ç¨‹åºå¤´éƒ¨æ®µé€‰æ‹©å­åˆ° TCB å’Œå¤´éƒ¨å†…
       mov [es:esi+0x44],cx                
       mov [edi+0x04],cx 

       ;å»ºç«‹ç¨‹åºä»£ç æ®µæè¿°ç¬¦
       mov eax,edi
       ;ä»£ç èµ·å§‹çº¿æ€§åœ°å€
       add eax,[edi+0x14]                 
       ;æ®µé•¿åº¦
       mov ebx,[edi+0x18]                 
       ;æ®µç•Œé™
       dec ebx                            
       ;å­—èŠ‚ç²’åº¦çš„ä»£ç æ®µæè¿°ç¬¦ï¼Œç‰¹æƒçº§ 3
       mov ecx,0x0040f800                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB çš„åŸºåœ°å€
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;è®¾ç½®é€‰æ‹©å­çš„ç‰¹æƒçº§ä¸º 3
       or cx,0000_0000_0000_0011B         
       ;ç™»è®°ä»£ç æ®µé€‰æ‹©å­åˆ°å¤´éƒ¨
       mov [edi+0x14],cx                  

       ;å»ºç«‹ç¨‹åºæ•°æ®æ®µæè¿°ç¬¦
       mov eax,edi
       ;æ•°æ®æ®µèµ·å§‹çº¿æ€§åœ°å€
       add eax,[edi+0x1c]                 
       ;æ®µé•¿åº¦
       mov ebx,[edi+0x20]                 
       ;æ®µç•Œé™
       dec ebx
       ;å­—èŠ‚ç²’åº¦çš„æ•°æ®æ®µæè¿°ç¬¦ï¼Œç‰¹æƒçº§ 3 
       mov ecx,0x0040f200                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB çš„åŸºåœ°å€
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;è®¾ç½®é€‰æ‹©å­çš„ç‰¹æƒçº§ä¸º 3
       or cx,0000_0000_0000_0011B         
       ;ç™»è®°æ•°æ®æ®µé€‰æ‹©å­åˆ°å¤´éƒ¨
       mov [edi+0x1c],cx                  

       ;å»ºç«‹ç¨‹åºå †æ ˆæ®µæè¿°ç¬¦
       ;4KB çš„å€ç‡
       mov ecx,[edi+0x0c]                 
       mov ebx,0x000fffff
       ;å¾—åˆ°æ®µç•Œé™
       sub ebx,ecx                        
       mov eax,4096                        
       mul ecx                         
       ;å‡†å¤‡ä¸ºå †æ ˆåˆ†é…å†…å­˜
       mov ecx,eax                         
       call sys_routine_seg_sel:allocate_memory
       ;å¾—åˆ°å †æ ˆçš„é«˜ç«¯ç‰©ç†åœ°å€
       add eax,ecx                        
       ;å­—èŠ‚ç²’åº¦çš„å †æ ˆæ®µæè¿°ç¬¦ï¼Œç‰¹æƒçº§ 3 
       mov ecx,0x00c0f600                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB çš„åŸºåœ°å€
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;è®¾ç½®é€‰æ‹©å­çš„ç‰¹æƒçº§ä¸º 3
       or cx,0000_0000_0000_0011B         
       ;ç™»è®°å †æ ˆæ®µé€‰æ‹©å­åˆ°å¤´éƒ¨
       mov [edi+0x08],cx                  

       ;é‡å®šä½ SALT 
       mov eax,mem_0_4_gb_seg_sel        
       mov es,eax                         

       mov eax,core_data_seg_sel
       mov ds,eax

       cld
       ;U-SALT æ¡ç›®æ•°(é€šè¿‡è®¿é—® 4GB æ®µå–å¾—)
       mov ecx,[es:edi+0x24]              
       ;U-SALT åœ¨ 4GB æ®µå†…çš„åç§» 
       add edi,0x28                        
.b2: 
       push ecx
       push edi

       mov ecx,salt_items
       mov esi,salt
.b3:
       push edi
       push esi
       push ecx
       ;æ£€ç´¢è¡¨ä¸­ï¼Œæ¯æ¡ç›®çš„æ¯”è¾ƒæ¬¡æ•°
       mov ecx,64                         
       ;æ¯æ¬¡æ¯”è¾ƒ 4 å­—èŠ‚ 
       repe cmpsd                          
       jnz .b4
       ;è‹¥åŒ¹é…ï¼Œåˆ™ esi æ°å¥½æŒ‡å‘å…¶åçš„åœ°å€
       mov eax,[esi]                      
       ;å°†å­—ç¬¦ä¸²æ”¹å†™æˆåç§»åœ°å€
       mov [es:edi-256],eax                
       mov ax,[esi+4]
       ;ä»¥ç”¨æˆ·ç¨‹åºè‡ªå·±çš„ç‰¹æƒçº§ä½¿ç”¨è°ƒç”¨é—¨ï¼Œæ•… RPL=3
       or ax,0000000000000011B           
       ;å›å¡«è°ƒç”¨é—¨é€‰æ‹©å­ 
       mov [es:edi-252],ax                
.b4:

       pop ecx
       pop esi
       add esi,salt_item_len
       ;ä»å¤´æ¯”è¾ƒ
       pop edi                             
       loop .b3

       pop edi
       add edi,256
       pop ecx
       loop .b2
       ;ä»å †æ ˆä¸­å–å¾— TCB çš„åŸºåœ°å€
       mov esi,[ebp+11*4]                 

       ;åˆ›å»º 0 ç‰¹æƒçº§å †æ ˆ
       mov ecx,4096
       mov eax,ecx                       
       mov [es:esi+0x1a],ecx
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆå°ºå¯¸åˆ° TCB 
       shr dword [es:esi+0x1a],12
       call sys_routine_seg_sel:allocate_memory
       ;å †æ ˆå¿…é¡»ä½¿ç”¨é«˜ç«¯åœ°å€ä¸ºåŸºåœ°å€
       add eax,ecx                        
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆåŸºåœ°å€åˆ° TCB
       mov [es:esi+0x1e],eax               
       mov ebx,0xffffe                   
       mov ecx,0x00c09600
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB çš„åŸºåœ°å€
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;è®¾ç½®é€‰æ‹©å­çš„ç‰¹æƒçº§ä¸º 0
       ;or cx,0000_0000_0000_0000         
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆé€‰æ‹©å­åˆ° TCB 
       mov [es:esi+0x22],cx               
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆåˆå§‹ ESP åˆ° TCB
       mov dword [es:esi+0x24],0          

       ;åˆ›å»º 1 ç‰¹æƒçº§å †æ ˆ
       mov ecx,4096
       mov eax,ecx                        
       mov [es:esi+0x28],ecx
       shr [es:esi+0x28],12               
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx                        
       mov [es:esi+0x2c],eax
       mov ebx,0xffffe                   
       mov ecx,0x00c0b600                
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       or cx,0000_0000_0000_0001
       mov [es:esi+0x30],cx
       mov dword [es:esi+0x32],0

       ;åˆ›å»º 2 ç‰¹æƒçº§å †æ ˆ
       mov ecx,4096
       mov eax,ecx                        
       mov [es:esi+0x36],ecx
       shr [es:esi+0x36],12               
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx                        
       mov [es:esi+0x3a],ecx              
       mov ebx,0xffffe                    
       mov ecx,0x00c0d600                 
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi                 
       call fill_descriptor_in_ldt
       or cx,0000_0000_0000_0010
       mov [es:esi+0x3e],cx
       mov dword [es:esi+0x40],0

       ;åœ¨ GDT ä¸­ç™»è®° LDT æè¿°ç¬¦
       ;LDT çš„èµ·å§‹çº¿æ€§åœ°å€
       mov eax,[es:esi+0x0c]              
       ;LDT æ®µç•Œé™
       movzx ebx,word [es:esi+0x0a]       
       ;LDT æè¿°ç¬¦ï¼Œç‰¹æƒçº§ 0
       mov ecx,0x00408200                 
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;ç™»è®° LDT é€‰æ‹©å­åˆ° TCB ä¸­
       mov [es:esi+0x10],cx

       ;åˆ›å»ºç”¨æˆ·ç¨‹åºçš„ TSS
       ;TSS çš„åŸºæœ¬å°ºå¯¸
       mov ecx,104
       mov [es:esi+0x12],cx
       ;ç™»è®° TSS ç•Œé™å€¼åˆ° TCB
       dec word [es:esi+0x12]
       call sys_routine_seg_sel:allocate_memory
       ;ç™»è®° TSS åŸºåœ°å€åˆ° TCB
       mov [es:esi+0x14],ecx

       ;ç™»è®°åŸºæœ¬çš„ TSS è¡¨æ ¼å†…å®¹
       ;åå‘é“¾=0
       mov word [es:ecx+0],0              
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆåˆå§‹ ESP åˆ° TSS ä¸­ ESP0 å¤„
       mov edx,[es:esi+0x24]              
       mov [es:ecx+4],edx
       ;ç™»è®° 0 ç‰¹æƒçº§å †æ ˆæ®µé€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:esi+0x22]               
       mov [es:ecx+8],dx
       ;ç™»è®° 1 ç‰¹æƒçº§å †æ ˆåˆå§‹ ESP åˆ° TSS ä¸­
       mov edx,[es:esi+0x32]              
       mov [es:ecx+12],edx
       ;ç™»è®° 1 ç‰¹æƒçº§å †æ ˆæ®µé€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:esi+0x30]               
       mov [es:ecx+16],dx
       ;ç™»è®° 2 ç‰¹æƒçº§å †æ ˆåˆå§‹ ESP åˆ° TSS ä¸­
       mov edx,[es:esi+0x40]
       mov [es:ecx+20],edx
       ;ç™»è®° 2 ç‰¹æƒçº§å †æ ˆæ®µé€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:esi+0x3e]
       mov [es:ecx+24],dx
       ;ç™»è®°ä»»åŠ¡çš„ LDT é€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:esi+0x10]
       mov [es:ecx+96],dx
       ;ç™»è®°ä»»åŠ¡çš„ I/O ä½å›¾åç§»åˆ° TSS ä¸­ï¼Œä¹Ÿå°±æ˜¯ TSS çš„ç•Œé™å€¼
       mov dx,[es:esi+0x12]
       mov [es:ecx+102],dx
       ;T=0
       mov word [es:ecx+100],0
       ;ç™»è®° CR3(PDBR)
       mov dword [es:ecx+28],0            

       ;è®¿é—®ç”¨æˆ·ç¨‹åºå¤´éƒ¨ï¼Œè·å–æ•°æ®å¡«å…… TSSï¼Œå½“ä»¥ååˆ‡æ¢åˆ°ç”¨æˆ·ç¨‹åºä¹‹åï¼Œç”¨æˆ·ç¨‹åºçš„æ®µå¯„å­˜å™¨è‡ªåŠ¨è¢«åˆå§‹åŒ–ä¸ºä»¥ä¸‹å€¼
       ;ä»å †æ ˆä¸­å–å¾— TCB çš„åŸºåœ°å€ 
       mov ebx,[ebp+11*4]
       ;ç”¨æˆ·ç¨‹åºåŠ è½½çš„åŸºåœ°å€
       mov edi,[es:ebx+0x06] 
       ;ç™»è®°ç¨‹åºå…¥å£ç‚¹ï¼ˆEIPï¼‰åˆ° TSS
       mov edx,[es:edi+0x10]
       mov [es:ecx+32],edx
       ;ç™»è®°ç¨‹åºä»£ç æ®µï¼ˆCSï¼‰é€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:edi+0x14]
       mov [es:ecx+76],dx
       ;ç™»è®°ç¨‹åºå †æ ˆæ®µï¼ˆSSï¼‰é€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:edi+0x08]
       mov [es:ecx+80],dx
       ;ç™»è®°ç¨‹åºå¤´éƒ¨æ®µï¼ˆDSï¼‰é€‰æ‹©å­åˆ° TSS ä¸­
       mov dx,[es:edi+0x04]
       mov word [es:ecx+84],dx
       ;TSS ä¸­çš„ ES=0
       mov word [es:ecx+72],0
       ;TSS ä¸­çš„ FS=0
       mov word [es:ecx+88],0
       ;TSS ä¸­çš„ GS=0
       mov word [es:ecx+92],0

       pushfd
       pop edx
       ;å°† EFLAGS ä¸­çš„å†…å®¹ä¿å­˜åˆ° TSS ä¸­ï¼Œè¿™é‡Œä¿å­˜çš„ EFLAGS å¯„å­˜å™¨çš„å€¼æ˜¯å†…æ ¸ç¨‹åºç®¡ç†å™¨ä»»åŠ¡çš„å€¼ï¼ŒåŒæ—¶ä¹Ÿä¼šç»™ç”¨æˆ·ç¨‹åºä½¿ç”¨ï¼Œåˆå§‹ NT ä½ä¸º 0
       mov dword [es:ecx+36],edx

       ;åœ¨ GDT ä¸­ç™»è®° TSS æè¿°ç¬¦
       ;TSS çš„èµ·å§‹çº¿æ€§åœ°å€
       mov eax,[es:esi+0x14]
       ;æ®µé•¿åº¦ï¼ˆç•Œé™ï¼‰
       movzx ebx,word [es:esi+0x12]
       ;TSS æè¿°ç¬¦ï¼Œç‰¹æƒçº§ 0
       mov ecx,0x00408900
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;ç™»è®° TSS é€‰æ‹©å­åˆ° TCB
       mov [es:esi+0x18],cx               

       ;æ¢å¤åˆ°è°ƒç”¨æ­¤è¿‡ç¨‹å‰çš„ es æ®µ
       pop es                             
       ;æ¢å¤åˆ°è°ƒç”¨æ­¤è¿‡ç¨‹å‰çš„ ds æ®µ 
       pop ds                             

       popad
       ;ä¸¢å¼ƒè°ƒç”¨æœ¬è¿‡ç¨‹å‰å‹å…¥çš„å‚æ•°
       ret 8                               
      
;åœ¨ TCB é“¾ä¸Šè¿½åŠ ä»»åŠ¡æ§åˆ¶å—
;è¾“å…¥ï¼šECX=TCB çº¿æ€§åŸºåœ°å€
append_to_tcb_link:                         

       push eax
       push edx
       push ds
       push es
       
       ;ä»¤ ds æŒ‡å‘å†…æ ¸æ•°æ®æ®µ
       mov eax,core_data_seg_sel           
       mov ds,eax
       ;ä»¤ es æŒ‡å‘ 0..4GB æ®µ
       mov eax,mem_0_4_gb_seg_sel         
       mov es,eax
       ;å½“å‰ TCB æŒ‡é’ˆåŸŸæ¸…é›¶ï¼Œä»¥æŒ‡ç¤ºè¿™æ˜¯æœ€åä¸€ä¸ª TCB
       mov dword [es: ecx+0x00],0         

       ;TCB è¡¨å¤´æŒ‡é’ˆ  
       mov eax,[tcb_chain]              
       or eax,eax
       jz .notcb 
       
.searc:
       mov edx,eax
       mov eax,[es: edx+0x00]
       or eax,eax               
       jnz .searc
       
       mov [es: edx+0x00],ecx
       jmp .retpc
       
.notcb:       
       ;è‹¥ä¸ºç©ºè¡¨ï¼Œç›´æ¥ä»¤è¡¨å¤´æŒ‡é’ˆæŒ‡å‘ TCB
       mov [tcb_chain],ecx                
       
.retpc:
       pop es
       pop ds
       pop edx
       pop eax
       
       ret
         
start:
       ;ä»¤ ds æŒ‡å‘æ ¸å¿ƒæ•°æ®æ®µ
       mov ecx,core_data_seg_sel           
       mov ds,ecx
       ;ä»¤ es æŒ‡å‘ 4GB æ•°æ®æ®µ
       mov ecx,mem_0_4_gb_seg_sel          
       mov es,ecx

       mov ebx,message_1                    
       call sys_routine_seg_sel:put_string
                                   
       ;æ˜¾ç¤ºå¤„ç†å™¨å“ç‰Œä¿¡æ¯ 
       mov eax,0x80000002
       cpuid
       mov [cpu_brand + 0x00],eax
       mov [cpu_brand + 0x04],ebx
       mov [cpu_brand + 0x08],ecx
       mov [cpu_brand + 0x0c],edx

       mov eax,0x80000003
       cpuid
       mov [cpu_brand + 0x10],eax
       mov [cpu_brand + 0x14],ebx
       mov [cpu_brand + 0x18],ecx
       mov [cpu_brand + 0x1c],edx

       mov eax,0x80000004
       cpuid
       mov [cpu_brand + 0x20],eax
       mov [cpu_brand + 0x24],ebx
       mov [cpu_brand + 0x28],ecx
       mov [cpu_brand + 0x2c],edx

       ;æ˜¾ç¤ºå¤„ç†å™¨å“ç‰Œä¿¡æ¯
       mov ebx,cpu_brnd0                   
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brand
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brnd1
       call sys_routine_seg_sel:put_string

       ;ä»¥ä¸‹å¼€å§‹å®‰è£…ä¸ºæ•´ä¸ªç³»ç»ŸæœåŠ¡çš„è°ƒç”¨é—¨ã€‚ç‰¹æƒçº§ä¹‹é—´çš„æ§åˆ¶è½¬ç§»å¿…é¡»ä½¿ç”¨é—¨
       ;C-SALT è¡¨çš„èµ·å§‹ä½ç½®
       mov edi,salt                       
       ;C-SALT è¡¨çš„æ¡ç›®æ•°é‡ 
       mov ecx,salt_items                  
.b3:
       push ecx   
       ;è¯¥æ¡ç›®å…¥å£ç‚¹çš„ 32 ä½åç§»åœ°å€
       mov eax,[edi+256]                  
       ;è¯¥æ¡ç›®å…¥å£ç‚¹çš„æ®µé€‰æ‹©å­ 
       mov bx,[edi+260]                   
       ;ç‰¹æƒçº§ 3 çš„è°ƒç”¨é—¨ (3 ä»¥ä¸Šçš„ç‰¹æƒçº§æ‰å…è®¸è®¿é—®)ï¼Œ0 ä¸ªå‚æ•°(å› ä¸ºç”¨å¯„å­˜å™¨ä¼ é€’å‚æ•°ï¼Œè€Œæ²¡æœ‰ç”¨æ ˆ)  
       mov cx,1_11_0_1100_000_00000B      
       call sys_routine_seg_sel:make_gate_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;å°†è¿”å›çš„é—¨æè¿°ç¬¦é€‰æ‹©å­å›å¡«
       mov [edi+260],cx                   
       ;æŒ‡å‘ä¸‹ä¸€ä¸ª C-SALT æ¡ç›®
       add edi,salt_item_len               
       pop ecx
       loop .b3

       ;å¯¹é—¨è¿›è¡Œæµ‹è¯• 
       mov ebx,message_2
       ;é€šè¿‡é—¨æ˜¾ç¤ºä¿¡æ¯(åç§»é‡å°†è¢«å¿½ç•¥) 
       call far [salt_1+256]

       ;åˆ›å»ºå·¥ä½œåœ¨ 0 ç‰¹æƒçº§çš„å†…æ ¸ä»»åŠ¡-ç¨‹åºç®¡ç†å™¨ï¼Œæ­¤å†…æ ¸ä»»åŠ¡ä¸»è¦åŠŸèƒ½ä¸ºåˆ›å»ºå…¶ä»–ä»»åŠ¡å¹¶ä¸”ç®¡ç†ä»–ä»¬
       ;ä¸ºç¨‹åºç®¡ç†å™¨çš„ TSS åˆ†é…å†…å­˜ç©ºé—´
       mov ecx,104
       call sys_routine_seg_sel:allocate_memory
       ;ä¿å­˜ç¨‹åºç®¡ç†å™¨çš„ TSS åŸºåœ°å€
       mov [prgman_tss+0x00],ecx

       ;åœ¨ç¨‹åºç®¡ç†å™¨çš„ TSS ä¸­è®¾ç½®å¿…è¦çš„é¡¹ç›® 
       ;ç¨‹åºç®¡ç†å™¨ï¼ˆå†…æ ¸ç¨‹åºï¼‰æ²¡æœ‰ LDTï¼Œå…¶æ‰€æœ‰çš„æ®µæè¿°ç¬¦å‡ç”± MBR åˆ›å»ºå¹¶å®‰è£…åœ¨ GDT ä¸­ï¼Œå¹¶ä¸”ç”±äºå…¶è¿è¡Œåœ¨ 0 ç‰¹æƒçº§ï¼Œæ‰€ä»¥ä¹Ÿä¸éœ€è¦åˆ›å»ºé¢å¤–å…¶ä»–ç‰¹æƒçº§çš„æ ˆ
       ;å¾€ TSS çš„ LDT æ®µé€‰æ‹©å­ä¸­å†™å…¥ 0
       mov word [es:ecx+96],0
       ;æ²¡æœ‰ I/O ä½å›¾ï¼Œè¿™é‡Œå†™å…¥ TSS çš„æ®µç•Œé™ã€‚0 ç‰¹æƒçº§äº‹å®ä¸Šä¸éœ€è¦ I/O ä½å›¾ï¼Œå› ä¸ºå…¶å¯ä»¥è®¿é—®æ‰€æœ‰çš„ç¡¬ä»¶ç«¯å£
       mov word [es:ecx+102],103
       ;åå‘é“¾=0ï¼ˆå‰ä¸€ä¸ªä»»åŠ¡çš„ TSS æŒ‡é’ˆï¼‰
       mov word [es:ecx+0],0
       ;ç™»è®° CR3(PDBR)
       mov dword [es:ecx+28],0
       ;T=0
       mov word [es:ecx+100],0
       
       ;åˆ›å»º TSS æè¿°ç¬¦ï¼Œå¹¶å®‰è£…åˆ° GDT ä¸­ï¼ˆTSS æè¿°ç¬¦åªèƒ½å®‰è£…åœ¨ GDT ä¸­ï¼‰
       ;TSS çš„èµ·å§‹çº¿æ€§åœ°å€
       mov eax,ecx
       ;æ®µé•¿åº¦ï¼ˆç•Œé™ï¼‰
       mov ebx,103
       ;TSS æè¿°ç¬¦ï¼Œç‰¹æƒçº§ 0ï¼ŒG=0ï¼ˆå­—èŠ‚ä¸ºç²’åº¦ï¼‰ï¼ŒD/B=1 æŒ‡ç¤º 32 ä½çš„åç§»åœ°å€æˆ–æ“ä½œæ•°
       ;P=1ï¼ŒS=0ï¼ˆè¡¨ç¤ºè¿™æ˜¯ä¸€ä¸ªç³»ç»Ÿæ®µï¼‰ï¼ŒDPL=0ï¼ˆ0 ç‰¹æƒçº§ï¼‰ï¼ŒTYPE=1001ï¼ˆB=0ï¼Œéå¿™çŠ¶æ€ï¼‰
       mov ecx,0x00408900
       ;eax=çº¿æ€§åŸºåœ°å€ï¼Œebx=æ®µç•Œé™ï¼Œecx=å±æ€§ï¼Œæ„é€ çš„æ®µæè¿°ç¬¦åœ¨ edx:eax ä¸­
       call sys_routine_seg_sel:make_seg_descriptor
       ;å°† edx:eax ä¸­ä¿å­˜çš„æ®µæè¿°ç¬¦å®‰è£…åˆ° GDT ä¸­ï¼Œå¹¶ä¸”è¿”å› ecxï¼Œå…¶ä¸­ä¿å­˜ TSS æè¿°ç¬¦é€‰æ‹©å­
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;ä¿å­˜ç¨‹åºç®¡ç†å™¨çš„ TSS æè¿°ç¬¦é€‰æ‹©å­
       mov [prgman_tss+0x04],cx

       ;ä»»åŠ¡å¯„å­˜å™¨ TR ä¸­çš„å†…å®¹æ˜¯ä»»åŠ¡å­˜åœ¨çš„æ ‡å¿—ï¼Œè¯¥å†…å®¹ä¹Ÿå†³å®šäº†å½“å‰ä»»åŠ¡æ˜¯è°ã€‚
       ;ä¸‹é¢çš„æŒ‡ä»¤ä¸ºå½“å‰æ­£åœ¨æ‰§è¡Œçš„ 0 ç‰¹æƒçº§ä»»åŠ¡"ç¨‹åºç®¡ç†å™¨"åè¡¥æ‰‹ç»­ï¼ˆTSSï¼‰ï¼Œå°† cx æŒ‡å‘çš„ TSS æè¿°ç¬¦é€‰æ‹©å­åŠ è½½åˆ° TR å¯„å­˜å™¨ä¸­ï¼ŒåŒæ—¶å°† TSS æè¿°ç¬¦ä¸­çš„ B ä½ç½®ä¸º 1ï¼ˆNT=0ï¼‰
       ltr cx

       ;ç°åœ¨å¯è®¤ä¸º "ç¨‹åºç®¡ç†å™¨" ä»»åŠ¡æ­£æ‰§è¡Œä¸­
       mov ebx,prgman_msg1
       call sys_routine_seg_sel:put_string

       ;ä¸ºç”¨æˆ·ç¨‹åºçš„ TCB æ§åˆ¶å—åˆ†é…å†…å­˜
       mov ecx,0x46
       call sys_routine_seg_sel:allocate_memory
       ;å°†æ­¤ TCB æ·»åŠ åˆ° TCB é“¾ä¸­
       call append_to_tcb_link             

       ;ç”¨æˆ·ç¨‹åºä½äºé€»è¾‘ 50 æ‰‡åŒº
       push dword 50                      
       ;å‹å…¥ä»»åŠ¡æ§åˆ¶å—èµ·å§‹çº¿æ€§åœ°å€
       push ecx                            

       ;å’Œä¸Šä¸€ç« ä¸åŒï¼Œä»»åŠ¡åˆ‡æ¢æ—¶è¦æ¢å¤ TSS å†…å®¹ï¼Œæ‰€ä»¥ load_relocate_program åœ¨åˆ›å»ºä»»åŠ¡æ—¶ TSS è¦å¡«å†™å®Œæ•´ï¼ŒåŒ…æ‹¬ 0-2 ç‰¹æƒçº§æ ˆçš„é€‰æ‹©å­å’Œ ESP å€¼ï¼Œå½“å‰æ ˆçš„é€‰æ‹©å­å’Œ ESP å€¼ç­‰ï¼ŒåŒæ—¶è¿˜æœ‰å„ä¸ªæ®µå¯„å­˜å™¨çš„å€¼å’Œ EFLAGS å¯„å­˜å™¨çš„å€¼
       call load_relocate_program
       ;ä½¿ç”¨ call æŒ‡ä»¤æ‰§è¡Œä»»åŠ¡åˆ‡æ¢
       ;åœ¨ä»»åŠ¡æ§åˆ¶å— TCB ä¸­ï¼Œä½åœ°å€ 0x14 å¤„æ˜¯ TSS åŸºåœ°å€ï¼ˆ32 ä½ï¼‰ï¼Œé«˜åœ°å€ 0x18 å¤„æ˜¯ TSS é€‰æ‹©å­ï¼ˆ16 ä½ï¼‰ï¼Œä½åœ°å€çš„ 32 ä½åŸºåœ°å€ä¼šè¢«çœ‹åšæ˜¯åç§»é‡è€Œå¿½ç•¥
       call far [es:ecx+0x14]
                                   
       ;é‡æ–°åŠ è½½å¹¶åˆ‡æ¢ä»»åŠ¡ 
       mov ebx,prgman_msg2
       call sys_routine_seg_sel:put_string

       mov ecx,0x46
       call sys_routine_seg_sel:allocate_memory
       ;å°†æ­¤ TCB æ·»åŠ åˆ° TCB é“¾ä¸­
       call append_to_tcb_link            
       ;ç”¨æˆ·ç¨‹åºä½äºé€»è¾‘ 50 æ‰‡åŒº
       push dword 50                      
       ;å‹å…¥ä»»åŠ¡æ§åˆ¶å—èµ·å§‹çº¿æ€§åœ°å€
       push ecx                           

       call load_relocate_program
       ;ä½¿ç”¨ jmp æŒ‡ä»¤æ‰§è¡Œä»»åŠ¡åˆ‡æ¢
       jmp far [es:ecx+0x14]              

       mov ebx,prgman_msg3
       call sys_routine_seg_sel:put_string

       hlt
            
core_code_end:

SECTION core_trail

core_end:
```

## äºŒã€ç”¨æˆ·ç¨‹åºä»£ç 

```armasm{.line-numbers}
       ;ä»£ç æ¸…å•15-2
       ;æ–‡ä»¶åï¼šc15.asm
       ;æ–‡ä»¶è¯´æ˜ï¼šç”¨æˆ·ç¨‹åº 
       ;åˆ›å»ºæ—¥æœŸï¼š2011-11-15 19:11   

;============================ğŸ”ç”¨æˆ·ç¨‹åºå¤´éƒ¨æ®µğŸ”===============================
SECTION header vstart=0
       ;ç¨‹åºæ€»é•¿åº¦ #0x00
       program_length   dd program_end
       ;ç¨‹åºå¤´éƒ¨çš„é•¿åº¦ #0x04
       head_len         dd header_end
       ;ç”¨äºæ¥æ”¶å †æ ˆæ®µé€‰æ‹©å­ #0x08
       stack_seg        dd 0                    
       ;ç¨‹åºå»ºè®®çš„å †æ ˆå¤§å° #0x0cï¼Œä»¥ 4KB ä¸ºå•ä½
       stack_len        dd 1                    

       ;ç¨‹åºå…¥å£ #0x10  
       prgentry         dd start                
       ;ä»£ç æ®µä½ç½® #0x14
       code_seg         dd section.code.start   
       ;ä»£ç æ®µé•¿åº¦ #0x18
       code_len         dd code_end             
       ;æ•°æ®æ®µä½ç½® #0x1c
       data_seg         dd section.data.start   
       ;æ•°æ®æ®µé•¿åº¦ #0x20
       data_len         dd data_end             

       ;ç¬¦å·åœ°å€æ£€ç´¢è¡¨
       salt_items       dd (header_end-salt)/256 ;#0x24
       ;#0x28
       salt:                                     
       PrintString      db  '@PrintString'
                        times 256-($-PrintString) db 0
       TerminateProgram db  '@TerminateProgram'
                        times 256-($-TerminateProgram) db 0
       ReadDiskData     db  '@ReadDiskData'
                        times 256-($-ReadDiskData) db 0
                 
header_end:
  
;============================ğŸ¥©ç”¨æˆ·ç¨‹åºæ•°æ®æ®µğŸ¥©================================

SECTION data vstart=0                
       message_1        db  0x0d,0x0a
                        db  '[USER TASK]: Hi! nice to meet you,'
                        db  'I am run at CPL=',0
       message_2        db  0
                        db  '.Now,I must exit...',0x0d,0x0a,0
data_end:

      [bits 32]

;==========================ğŸ«”ç”¨æˆ·ç¨‹åºä»£ç æ®µğŸ«”===================================

SECTION code vstart=0
start:
       ;ä»»åŠ¡å¯åŠ¨æ—¶ï¼Œds æŒ‡å‘å¤´éƒ¨æ®µï¼Œä¹Ÿä¸éœ€è¦è®¾ç½®å †æ ˆ
       ;è¿™æ˜¯å› ä¸ºé€šè¿‡ call for [TSS é€‰æ‹©å­] è¿›è¡Œä»»åŠ¡åˆ‡æ¢åˆ°ç”¨æˆ·ç¨‹åºæ—¶ï¼Œä¼šä» TSS ä¸­å–å‡ºç”¨æˆ·ç¨‹åºçš„å¯„å­˜å™¨å€¼æ¢å¤ç”¨æˆ·ç¨‹åºæ‰§è¡Œç¯å¢ƒ
       mov eax,ds
       mov fs,eax

       mov eax,[data_seg]
       mov ds,eax

       mov ebx,message_1
       call far [fs:PrintString]
       
       mov ax,cs
       ;è®¡ç®—å½“å‰ç‰¹æƒçº§ CPLï¼ˆå³ cs ä»£ç æ®µçš„ RPL ä½ï¼‰ï¼Œç„¶åè½¬æ¢æˆ ASCII ç åå†™å›åˆ°æ•°æ®æ®µ
       and al,0000_0011B
       or al,0x0030
       mov [message_2],al
       
       mov ebx,message_2
       call far [fs:PrintString]
       ;é€€å‡ºï¼Œå¹¶å°†æ§åˆ¶æƒè¿”å›åˆ°æ ¸å¿ƒ
       call far [fs:TerminateProgram]
    
code_end:

SECTION trail
program_end:
```