# 任务切换

## 一、内核程序代码

```armasm{.line-numbers}
       ;代码清单15-1
       ;文件名：c15_core.asm
       ;文件说明：保护模式微型核心程序 

       ;以下常量定义部分。内核的大部分内容都应当固定
       ;内核代码段选择子 
       core_code_seg_sel     equ  0x38
       ;内核数据段选择子
       core_data_seg_sel     equ  0x30
       ;系统公共例程代码段的选择子 
       sys_routine_seg_sel   equ  0x28
       ;视频显示缓冲区的段选择子 
       video_ram_seg_sel     equ  0x20
       ;内核堆栈段选择子
       core_stack_seg_sel    equ  0x18
       ;整个 0-4GB 内存的段的选择子
       mem_0_4_gb_seg_sel    equ  0x08

       ;以下是系统核心的头部，用于加载核心程序 
       core_length      dd core_end       ;核心程序总长度#00
       sys_routine_seg  dd section.sys_routine.start    ;系统公用例程段位置#04
       core_data_seg    dd section.core_data.start      ;核心数据段位置#08
       core_code_seg    dd section.core_code.start      ;核心代码段位置#0c
       core_entry       dd start                        ;核心代码段入口点#10
                        dw core_code_seg_sel

       [bits 32]
;==============================🥫系统公共例程代码段🥫===============================
SECTION sys_routine vstart=0

       ;字符串显示例程
       ;显示 0 终止的字符串并移动光标 
       ;输入：DS:EBX=串地址
put_string:                                 
       push ecx
  .getc:
       mov cl,[ebx]
       or cl,cl
       jz .exit
       call put_char
       inc ebx
       jmp .getc

  .exit:
       pop ecx
       retf                              

;在当前光标处显示一个字符,并推进
;光标。仅用于段内调用 
;输入：CL=字符ASCII码
put_char:                                    
       pushad

       ;以下取当前光标位置
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;高字
       mov ah,al

       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;低字
       mov bx,ax                          ;BX=代表光标位置的16位数

       cmp cl,0x0d                        ;回车符？
       jnz .put_0a
       mov ax,bx
       mov bl,80
       div bl
       mul bl
       mov bx,ax
       jmp .set_cursor

.put_0a:
       cmp cl,0x0a                        ;换行符？
       jnz .put_other
       add bx,80
       jmp .roll_screen

.put_other:                               ;正常显示字符
       push es
       mov eax,video_ram_seg_sel          ;0xb8000段的选择子
       mov es,eax
       shl bx,1
       mov [es:bx],cl
       pop es

       ;以下将光标位置推进一个字符
       shr bx,1
       inc bx

.roll_screen:
       cmp bx,2000                        ;光标超出屏幕？滚屏
       jl .set_cursor

       push ds
       push es
       mov eax,video_ram_seg_sel
       mov ds,eax
       mov es,eax
       cld
       mov esi,0xa0                       ;小心！32位模式下movsb/w/d 
       mov edi,0x00                       ;使用的是esi/edi/ecx 
       mov ecx,1920
       rep movsd
       mov bx,3840                        ;清除屏幕最底一行
       mov ecx,80                         ;32位程序应该使用ECX
.cls:
       mov word[es:bx],0x0720
       add bx,2
       loop .cls

       pop es
       pop ds

       mov bx,1920

.set_cursor:
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       mov al,bh
       out dx,al
       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       mov al,bl
       out dx,al

       popad
       
       ret                                

;从硬盘读取一个逻辑扇区
;eax=逻辑扇区号
;ds:ebx=目标缓冲区地址
;返回：ebx=ebx+512
read_hard_disk_0:                           
       push eax 
       push ecx
       push edx

       push eax
       
       mov dx,0x1f2
       mov al,1
       out dx,al                          ;读取的扇区数

       inc dx                             ;0x1f3
       pop eax
       out dx,al                          ;LBA地址7~0

       inc dx                             ;0x1f4
       mov cl,8
       shr eax,cl
       out dx,al                          ;LBA地址15~8

       inc dx                             ;0x1f5
       shr eax,cl
       out dx,al                          ;LBA地址23~16

       inc dx                             ;0x1f6
       shr eax,cl
       or al,0xe0                         ;第一硬盘  LBA地址27~24
       out dx,al

       inc dx                             ;0x1f7
       mov al,0x20                        ;读命令
       out dx,al

.waits:
       in al,dx
       and al,0x88
       cmp al,0x08
       jnz .waits                         ;不忙，且硬盘已准备好数据传输 

       mov ecx,256                        ;总共要读取的字数
       mov dx,0x1f0
.readw:
       in ax,dx
       mov [ebx],ax
       add ebx,2
       loop .readw

       pop edx
       pop ecx
       pop eax

       retf                               ;段间返回 
      
;分配内存
;输入：ecx=希望分配的字节数
;输出：ecx=起始线性地址 
allocate_memory:                            

       push ds
       push eax
       push ebx

       mov eax,core_data_seg_sel
       mov ds,eax

       mov eax,[ram_alloc]
       ;下一次分配时的起始地址
       add eax,ecx                      

       ;这里应当有检测可用内存数量的指令
       ;返回分配的起始地址
       mov ecx,[ram_alloc]                

       mov ebx,eax
       and ebx,0xfffffffc
       ;强制对齐
       add ebx,4                          
       ;下次分配的起始地址最好是 4 字节对齐 
       test eax,0x00000003                
       ;如果没有对齐，则强制对齐
       ;cmovcc 指令可以避免控制转移 
       cmovnz eax,ebx                     
       ;下次从该地址分配内存 
       mov [ram_alloc],eax                
       
       pop ebx
       pop eax
       pop ds

       retf

;在 GDT 内安装一个新的描述符
;输入：edx:eax=描述符 
;输出：cx=描述符的选择子
set_up_gdt_descriptor:                      

       push eax
       push ebx
       push edx

       push ds
       push es

       ;切换到核心数据段     
       mov ebx,core_data_seg_sel          
       mov ds,ebx
       ;以便开始处理 GDT
       sgdt [pgdt]                        

       mov ebx,mem_0_4_gb_seg_sel
       mov es,ebx
       ;GDT 界限
       movzx ebx,word [pgdt]              
       ;GDT 总字节数，也是下一个描述符偏移
       inc bx                             
       ;下一个描述符的线性地址
       add ebx,[pgdt+2]                   

       mov [es:ebx],eax
       mov [es:ebx+4],edx
       ;增加一个描述符的大小
       add word [pgdt],8                  
       ;对 GDT 的更改生效
       lgdt [pgdt]                        
       ;得到 GDT 界限值
       mov ax,[pgdt]                      
       xor dx,dx
       mov bx,8
       ;除以 8，去掉余数
       div bx                             
       mov cx,ax
       ;将索引号移到正确位置
       shl cx,3                           

       pop es
       pop ds

       pop edx
       pop ebx
       pop eax

       retf

;构造存储器和系统的段描述符
;输入：eax=线性基地址
;      ebx=段界限
;      ecx=属性。各属性位都在原始位置，无关的位清零 
;返回：edx:eax=描述符
make_seg_descriptor:    
                   
       mov edx,eax
       shl eax,16
       ;描述符前 32 位 (EAX) 构造完毕
       or ax,bx                           
       ;清除基地址中无关的位
       and edx,0xffff0000                 
       rol edx,8
       ;装配基址的 31~24 和 23~16 (80486+)
       bswap edx                          

       xor bx,bx
       ;装配段界限的高 4 位
       or edx,ebx                         
       ;装配属性
       or edx,ecx                         

       retf

;构造门的描述符（调用门等）
;输入：eax=门代码在段内偏移地址
;       bx=门代码所在段的选择子 
;       cx=段类型及属性等（各属性位都在原始位置）
;返回：edx:eax=完整的描述符
make_gate_descriptor:                       

       push ebx
       push ecx

       mov edx,eax
       ;得到偏移地址高 16 位
       and edx,0xffff0000                 
       ;组装属性部分到 edx 
       or dx,cx             

       ;得到偏移地址低 16 位
       and eax,0x0000ffff                  
       shl ebx,16                         
       ;组装段选择子部分 
       or eax,ebx                         

       pop ecx
       pop ebx

       retf                                                               

;终止当前任务，注意，执行此例程时，当前任务仍在运行中。此例程其实也是当前任务的一部分 
terminate_current_task:                     
       
       ;pushfd 用于将 EFLAGS 寄存器的内容压入堆栈（32 位模式下）
       pushfd
       ;获得 EFLAGS 寄存器内容
       mov edx,[esp]
       ;恢复堆栈指针，使堆栈平衡，保持压入 EFLAGS 寄存器前的状态
       add esp,4

       mov eax,core_data_seg_sel
       mov ds,eax

       ;测试 NT 位
       test dx,0100_0000_0000_0000B       
       ;当前任务是嵌套的，到 .b1 执行 iretd
       jnz .b1
       ;当前任务不是嵌套的，直接使用 jmp 指令切换到内核程序管理器任务
       mov ebx,core_msg1                   
       call sys_routine_seg_sel:put_string
       ;程序管理器任务
       ;因为此时程序管理器和当前任务没有形成嵌套关系，所以在返回时也必须要用 jmp
       jmp far [prgman_tss]

.b1: 
       mov ebx,core_msg0
       call sys_routine_seg_sel:put_string
       iretd
      
sys_routine_end:

;============================🧆系统核心的数据段🧆================================
SECTION core_data vstart=0 
       
       ;用于设置和修改 GDT
       pgdt             dw  0              
                        dd  0

       ;下次分配内存时的起始地址
       ram_alloc        dd  0x00100000    

       ;符号地址检索表
       salt:
       salt_1           db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        dw  sys_routine_seg_sel

       salt_2           db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  sys_routine_seg_sel

       salt_3           db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  sys_routine_seg_sel

       salt_4           db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  terminate_current_task
                        dw  sys_routine_seg_sel

       salt_item_len    equ $-salt_4
       salt_items       equ ($-salt)/salt_item_len

       message_1        db  '  If you seen this message,that means we '
                        db  'are now in protect mode,and the system '
                        db  'core is loaded,and the video display '
                        db  'routine works perfectly.',0x0d,0x0a,0

       message_2        db  '  System wide CALL-GATE mounted.',0x0d,0x0a,0
       
       bin_hex          db '0123456789ABCDEF'
       ;内核用的缓冲区
       core_buf         times 2048 db 0

       cpu_brnd0        db 0x0d,0x0a,'  ',0
       cpu_brand        times 52 db 0
       cpu_brnd1        db 0x0d,0x0a,0x0d,0x0a,0

       ;任务控制块链
       tcb_chain        dd  0

       ;程序管理器的任务信息 
       ;程序管理器的 TSS 基地址和 TSS 描述符选择子
       prgman_tss       dd  0             
                        dw  0

       prgman_msg1      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: Hello! I am Program Manager,'
                        db  'run at CPL=0.Now,create user task and switch '
                        db  'to it by the CALL instruction...',0x0d,0x0a,0
              
       prgman_msg2      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: I am glad to regain control.'
                        db  'Now,create another user task and switch to '
                        db  'it by the JMP instruction...',0x0d,0x0a,0
              
       prgman_msg3      db  0x0d,0x0a
                        db  '[PROGRAM MANAGER]: I am gain control again,'
                        db  'HALT...',0

       core_msg0        db  0x0d,0x0a
                        db  '[SYSTEM CORE]: Uh...This task initiated with '
                        db  'CALL instruction or an exeception/ interrupt,'
                        db  'should use IRETD instruction to switch back...'
                        db  0x0d,0x0a,0

       core_msg1        db  0x0d,0x0a
                        db  '[SYSTEM CORE]: Uh...This task initiated with '
                        db  'JMP instruction,  should switch to Program '
                        db  'Manager directly by the JMP instruction...'
                        db  0x0d,0x0a,0

core_data_end:
               
;=============================🧇内核核心代码段🧇=================================
SECTION core_code vstart=0

;在 LDT 内安装一个新的描述符
;输入：edx:eax=描述符
;          ebx=TCB基地址
;输出：cx=描述符的选择子
fill_descriptor_in_ldt:                     

       push eax
       push edx
       push edi
       push ds

       mov ecx,mem_0_4_gb_seg_sel
       mov ds,ecx
       ;获得 LDT 基地址
       mov edi,[ebx+0x0c]                 
       
       xor ecx,ecx
       ;获得 LDT 界限
       mov cx,[ebx+0x0a]                  
       ;LDT 的总字节数，即新描述符偏移地址
       inc cx                             
       
       ;安装描述符
       mov [edi+ecx+0x00],eax
       mov [edi+ecx+0x04],edx             

       add cx,8                           
       ;得到新的 LDT 界限值
       dec cx                              
       ;更新 LDT 界限值到 TCB
       mov [ebx+0x0a],cx                  

       mov ax,cx
       xor dx,dx
       mov cx,8
       div cx
       
       mov cx,ax
       ;左移 3 位，并且使 TI 位=1，指向 LDT，最后使 RPL=00
       shl cx,3                           
       or cx,0000_0000_0000_0100B          

       pop ds
       pop edi
       pop edx
       pop eax

       ret
          
;加载并重定位用户程序
;输入: PUSH 逻辑扇区号
;      PUSH 任务控制块基地址
;输出：无
load_relocate_program:                      

       pushad

       push ds
       push es
       
       ;1.为用户程序创建 LDT 所需要的内存空间；
       ;2.将用户程序全部加载到内存中，并且为用户程序中的数据段、代码段、栈段创建描述符安装到 LDT 中；
       ;3.重定位 U-SALT，将 U-SALT 中的例程名替换为对应调用门的选择子和偏移量；
       ;4.为用户程序创建 0,1,2 特权级栈，分别为这三个特权级别的栈创建描述符，并且将描述符安装到 LDT 中；
       ;5.在 GDT 中安装 LDT 系统段描述符;
       ;6.为用户程序创建 TSS 所需要的内存空间，并且登记 TSS 表格的基本内容；
       ;7.为 TSS 创建描述符并安装到 GDT 中

       ;为访问通过堆栈传递的参数做准备
       mov ebp,esp                        

       mov ecx,mem_0_4_gb_seg_sel
       mov es,ecx

       ;从堆栈中取得 TCB 的基地址
       mov esi,[ebp+11*4]                 

       ;以下申请创建 LDT 所需要的内存
       ;允许安装 20 个 LDT 描述符
       mov ecx,160                        
       call sys_routine_seg_sel:allocate_memory
       ;登记 LDT 基地址到 TCB 中
       mov [es:esi+0x0c],ecx              
       ;登记 LDT 初始的界限到 TCB 中
       mov word [es:esi+0x0a],0xffff       

       ;以下开始加载用户程序 
       mov eax,core_data_seg_sel
       ;切换 DS 到内核数据段
       mov ds,eax                         

       ;从堆栈中取出用户程序起始扇区号
       mov eax,[ebp+12*4]                 
       ;读取程序头部数据 
       mov ebx,core_buf                        
       call sys_routine_seg_sel:read_hard_disk_0

       ;以下判断整个程序有多大
       ;程序尺寸
       mov eax,[core_buf]                 
       mov ebx,eax
       ;使之 512 字节对齐（能被 512 整除的数低 9 位都为 0）
       and ebx,0xfffffe00                 
       add ebx,512                        
       ;程序的大小正好是 512 的倍数吗? 
       test eax,0x000001ff                
       ;不是。使用凑整的结果 
       cmovnz eax,ebx                     

       ;实际需要申请的内存数量
       mov ecx,eax                        
       call sys_routine_seg_sel:allocate_memory
       ;登记程序加载基地址到 TCB 中
       mov [es:esi+0x06],ecx    

       ;ebx -> 申请到的内存首地址
       mov ebx,ecx                        
       xor edx,edx
       mov ecx,512
       div ecx
       ;总扇区数
       mov ecx,eax       

       ;切换 ds 到 0-4GB 的段
       mov eax,mem_0_4_gb_seg_sel         
       mov ds,eax
       ;起始扇区号
       mov eax,[ebp+12*4]                  
.b1:
       call sys_routine_seg_sel:read_hard_disk_0
       inc eax
       ;循环读，直到读完整个用户程序
       loop .b1                           
       ;获得程序加载基地址
       mov edi,[es:esi+0x06]              

       ;建立程序头部段描述符
       ;程序头部起始线性地址
       mov eax,edi                        
       ;段长度
       mov ebx,[edi+0x04]                 
       ;段界限
       dec ebx                            
       ;字节粒度的数据段描述符，特权级 3
       mov ecx,0x0040f200                  
       call sys_routine_seg_sel:make_seg_descriptor

       ;安装头部段描述符到 LDT 中 
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B         
       ;登记程序头部段选择子到 TCB 和头部内
       mov [es:esi+0x44],cx                
       mov [edi+0x04],cx 

       ;建立程序代码段描述符
       mov eax,edi
       ;代码起始线性地址
       add eax,[edi+0x14]                 
       ;段长度
       mov ebx,[edi+0x18]                 
       ;段界限
       dec ebx                            
       ;字节粒度的代码段描述符，特权级 3
       mov ecx,0x0040f800                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B         
       ;登记代码段选择子到头部
       mov [edi+0x14],cx                  

       ;建立程序数据段描述符
       mov eax,edi
       ;数据段起始线性地址
       add eax,[edi+0x1c]                 
       ;段长度
       mov ebx,[edi+0x20]                 
       ;段界限
       dec ebx
       ;字节粒度的数据段描述符，特权级 3 
       mov ecx,0x0040f200                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B         
       ;登记数据段选择子到头部
       mov [edi+0x1c],cx                  

       ;建立程序堆栈段描述符
       ;4KB 的倍率
       mov ecx,[edi+0x0c]                 
       mov ebx,0x000fffff
       ;得到段界限
       sub ebx,ecx                        
       mov eax,4096                        
       mul ecx                         
       ;准备为堆栈分配内存
       mov ecx,eax                         
       call sys_routine_seg_sel:allocate_memory
       ;得到堆栈的高端物理地址
       add eax,ecx                        
       ;字节粒度的堆栈段描述符，特权级 3 
       mov ecx,0x00c0f600                 
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B         
       ;登记堆栈段选择子到头部
       mov [edi+0x08],cx                  

       ;重定位 SALT 
       mov eax,mem_0_4_gb_seg_sel        
       mov es,eax                         

       mov eax,core_data_seg_sel
       mov ds,eax

       cld
       ;U-SALT 条目数(通过访问 4GB 段取得)
       mov ecx,[es:edi+0x24]              
       ;U-SALT 在 4GB 段内的偏移 
       add edi,0x28                        
.b2: 
       push ecx
       push edi

       mov ecx,salt_items
       mov esi,salt
.b3:
       push edi
       push esi
       push ecx
       ;检索表中，每条目的比较次数
       mov ecx,64                         
       ;每次比较 4 字节 
       repe cmpsd                          
       jnz .b4
       ;若匹配，则 esi 恰好指向其后的地址
       mov eax,[esi]                      
       ;将字符串改写成偏移地址
       mov [es:edi-256],eax                
       mov ax,[esi+4]
       ;以用户程序自己的特权级使用调用门，故 RPL=3
       or ax,0000000000000011B           
       ;回填调用门选择子 
       mov [es:edi-252],ax                
.b4:

       pop ecx
       pop esi
       add esi,salt_item_len
       ;从头比较
       pop edi                             
       loop .b3

       pop edi
       add edi,256
       pop ecx
       loop .b2
       ;从堆栈中取得 TCB 的基地址
       mov esi,[ebp+11*4]                 

       ;创建 0 特权级堆栈
       mov ecx,4096
       mov eax,ecx                       
       mov [es:esi+0x1a],ecx
       ;登记 0 特权级堆栈尺寸到 TCB 
       shr dword [es:esi+0x1a],12
       call sys_routine_seg_sel:allocate_memory
       ;堆栈必须使用高端地址为基地址
       add eax,ecx                        
       ;登记 0 特权级堆栈基地址到 TCB
       mov [es:esi+0x1e],eax               
       mov ebx,0xffffe                   
       mov ecx,0x00c09600
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 0
       ;or cx,0000_0000_0000_0000         
       ;登记 0 特权级堆栈选择子到 TCB 
       mov [es:esi+0x22],cx               
       ;登记 0 特权级堆栈初始 ESP 到 TCB
       mov dword [es:esi+0x24],0          

       ;创建 1 特权级堆栈
       mov ecx,4096
       mov eax,ecx                        
       mov [es:esi+0x28],ecx
       shr [es:esi+0x28],12               
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx                        
       mov [es:esi+0x2c],eax
       mov ebx,0xffffe                   
       mov ecx,0x00c0b600                
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       or cx,0000_0000_0000_0001
       mov [es:esi+0x30],cx
       mov dword [es:esi+0x32],0

       ;创建 2 特权级堆栈
       mov ecx,4096
       mov eax,ecx                        
       mov [es:esi+0x36],ecx
       shr [es:esi+0x36],12               
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx                        
       mov [es:esi+0x3a],ecx              
       mov ebx,0xffffe                    
       mov ecx,0x00c0d600                 
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi                 
       call fill_descriptor_in_ldt
       or cx,0000_0000_0000_0010
       mov [es:esi+0x3e],cx
       mov dword [es:esi+0x40],0

       ;在 GDT 中登记 LDT 描述符
       ;LDT 的起始线性地址
       mov eax,[es:esi+0x0c]              
       ;LDT 段界限
       movzx ebx,word [es:esi+0x0a]       
       ;LDT 描述符，特权级 0
       mov ecx,0x00408200                 
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;登记 LDT 选择子到 TCB 中
       mov [es:esi+0x10],cx

       ;创建用户程序的 TSS
       ;TSS 的基本尺寸
       mov ecx,104
       mov [es:esi+0x12],cx
       ;登记 TSS 界限值到 TCB
       dec word [es:esi+0x12]
       call sys_routine_seg_sel:allocate_memory
       ;登记 TSS 基地址到 TCB
       mov [es:esi+0x14],ecx

       ;登记基本的 TSS 表格内容
       ;反向链=0
       mov word [es:ecx+0],0              
       ;登记 0 特权级堆栈初始 ESP 到 TSS 中 ESP0 处
       mov edx,[es:esi+0x24]              
       mov [es:ecx+4],edx
       ;登记 0 特权级堆栈段选择子到 TSS 中
       mov dx,[es:esi+0x22]               
       mov [es:ecx+8],dx
       ;登记 1 特权级堆栈初始 ESP 到 TSS 中
       mov edx,[es:esi+0x32]              
       mov [es:ecx+12],edx
       ;登记 1 特权级堆栈段选择子到 TSS 中
       mov dx,[es:esi+0x30]               
       mov [es:ecx+16],dx
       ;登记 2 特权级堆栈初始 ESP 到 TSS 中
       mov edx,[es:esi+0x40]
       mov [es:ecx+20],edx
       ;登记 2 特权级堆栈段选择子到 TSS 中
       mov dx,[es:esi+0x3e]
       mov [es:ecx+24],dx
       ;登记任务的 LDT 选择子到 TSS 中
       mov dx,[es:esi+0x10]
       mov [es:ecx+96],dx
       ;登记任务的 I/O 位图偏移到 TSS 中，也就是 TSS 的界限值
       mov dx,[es:esi+0x12]
       mov [es:ecx+102],dx
       ;T=0
       mov word [es:ecx+100],0
       ;登记 CR3(PDBR)
       mov dword [es:ecx+28],0            

       ;访问用户程序头部，获取数据填充 TSS，当以后切换到用户程序之后，用户程序的段寄存器自动被初始化为以下值
       ;从堆栈中取得 TCB 的基地址 
       mov ebx,[ebp+11*4]
       ;用户程序加载的基地址
       mov edi,[es:ebx+0x06] 
       ;登记程序入口点（EIP）到 TSS
       mov edx,[es:edi+0x10]
       mov [es:ecx+32],edx
       ;登记程序代码段（CS）选择子到 TSS 中
       mov dx,[es:edi+0x14]
       mov [es:ecx+76],dx
       ;登记程序堆栈段（SS）选择子到 TSS 中
       mov dx,[es:edi+0x08]
       mov [es:ecx+80],dx
       ;登记程序头部段（DS）选择子到 TSS 中
       mov dx,[es:edi+0x04]
       mov word [es:ecx+84],dx
       ;TSS 中的 ES=0
       mov word [es:ecx+72],0
       ;TSS 中的 FS=0
       mov word [es:ecx+88],0
       ;TSS 中的 GS=0
       mov word [es:ecx+92],0

       pushfd
       pop edx
       ;将 EFLAGS 中的内容保存到 TSS 中，这里保存的 EFLAGS 寄存器的值是内核程序管理器任务的值，同时也会给用户程序使用，初始 NT 位为 0
       mov dword [es:ecx+36],edx

       ;在 GDT 中登记 TSS 描述符
       ;TSS 的起始线性地址
       mov eax,[es:esi+0x14]
       ;段长度（界限）
       movzx ebx,word [es:esi+0x12]
       ;TSS 描述符，特权级 0
       mov ecx,0x00408900
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;登记 TSS 选择子到 TCB
       mov [es:esi+0x18],cx               

       ;恢复到调用此过程前的 es 段
       pop es                             
       ;恢复到调用此过程前的 ds 段 
       pop ds                             

       popad
       ;丢弃调用本过程前压入的参数
       ret 8                               
      
;在 TCB 链上追加任务控制块
;输入：ECX=TCB 线性基地址
append_to_tcb_link:                         

       push eax
       push edx
       push ds
       push es
       
       ;令 ds 指向内核数据段
       mov eax,core_data_seg_sel           
       mov ds,eax
       ;令 es 指向 0..4GB 段
       mov eax,mem_0_4_gb_seg_sel         
       mov es,eax
       ;当前 TCB 指针域清零，以指示这是最后一个 TCB
       mov dword [es: ecx+0x00],0         

       ;TCB 表头指针  
       mov eax,[tcb_chain]              
       or eax,eax
       jz .notcb 
       
.searc:
       mov edx,eax
       mov eax,[es: edx+0x00]
       or eax,eax               
       jnz .searc
       
       mov [es: edx+0x00],ecx
       jmp .retpc
       
.notcb:       
       ;若为空表，直接令表头指针指向 TCB
       mov [tcb_chain],ecx                
       
.retpc:
       pop es
       pop ds
       pop edx
       pop eax
       
       ret
         
start:
       ;令 ds 指向核心数据段
       mov ecx,core_data_seg_sel           
       mov ds,ecx
       ;令 es 指向 4GB 数据段
       mov ecx,mem_0_4_gb_seg_sel          
       mov es,ecx

       mov ebx,message_1                    
       call sys_routine_seg_sel:put_string
                                   
       ;显示处理器品牌信息 
       mov eax,0x80000002
       cpuid
       mov [cpu_brand + 0x00],eax
       mov [cpu_brand + 0x04],ebx
       mov [cpu_brand + 0x08],ecx
       mov [cpu_brand + 0x0c],edx

       mov eax,0x80000003
       cpuid
       mov [cpu_brand + 0x10],eax
       mov [cpu_brand + 0x14],ebx
       mov [cpu_brand + 0x18],ecx
       mov [cpu_brand + 0x1c],edx

       mov eax,0x80000004
       cpuid
       mov [cpu_brand + 0x20],eax
       mov [cpu_brand + 0x24],ebx
       mov [cpu_brand + 0x28],ecx
       mov [cpu_brand + 0x2c],edx

       ;显示处理器品牌信息
       mov ebx,cpu_brnd0                   
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brand
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brnd1
       call sys_routine_seg_sel:put_string

       ;以下开始安装为整个系统服务的调用门。特权级之间的控制转移必须使用门
       ;C-SALT 表的起始位置
       mov edi,salt                       
       ;C-SALT 表的条目数量 
       mov ecx,salt_items                  
.b3:
       push ecx   
       ;该条目入口点的 32 位偏移地址
       mov eax,[edi+256]                  
       ;该条目入口点的段选择子 
       mov bx,[edi+260]                   
       ;特权级 3 的调用门 (3 以上的特权级才允许访问)，0 个参数(因为用寄存器传递参数，而没有用栈)  
       mov cx,1_11_0_1100_000_00000B      
       call sys_routine_seg_sel:make_gate_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;将返回的门描述符选择子回填
       mov [edi+260],cx                   
       ;指向下一个 C-SALT 条目
       add edi,salt_item_len               
       pop ecx
       loop .b3

       ;对门进行测试 
       mov ebx,message_2
       ;通过门显示信息(偏移量将被忽略) 
       call far [salt_1+256]

       ;创建工作在 0 特权级的内核任务-程序管理器，此内核任务主要功能为创建其他任务并且管理他们
       ;为程序管理器的 TSS 分配内存空间
       mov ecx,104
       call sys_routine_seg_sel:allocate_memory
       ;保存程序管理器的 TSS 基地址
       mov [prgman_tss+0x00],ecx

       ;在程序管理器的 TSS 中设置必要的项目 
       ;程序管理器（内核程序）没有 LDT，其所有的段描述符均由 MBR 创建并安装在 GDT 中，并且由于其运行在 0 特权级，所以也不需要创建额外其他特权级的栈
       ;往 TSS 的 LDT 段选择子中写入 0
       mov word [es:ecx+96],0
       ;没有 I/O 位图，这里写入 TSS 的段界限。0 特权级事实上不需要 I/O 位图，因为其可以访问所有的硬件端口
       mov word [es:ecx+102],103
       ;反向链=0（前一个任务的 TSS 指针）
       mov word [es:ecx+0],0
       ;登记 CR3(PDBR)
       mov dword [es:ecx+28],0
       ;T=0
       mov word [es:ecx+100],0
       
       ;创建 TSS 描述符，并安装到 GDT 中（TSS 描述符只能安装在 GDT 中）
       ;TSS 的起始线性地址
       mov eax,ecx
       ;段长度（界限）
       mov ebx,103
       ;TSS 描述符，特权级 0，G=0（字节为粒度），D/B=1 指示 32 位的偏移地址或操作数
       ;P=1，S=0（表示这是一个系统段），DPL=0（0 特权级），TYPE=1001（B=0，非忙状态）
       mov ecx,0x00408900
       ;eax=线性基地址，ebx=段界限，ecx=属性，构造的段描述符在 edx:eax 中
       call sys_routine_seg_sel:make_seg_descriptor
       ;将 edx:eax 中保存的段描述符安装到 GDT 中，并且返回 ecx，其中保存 TSS 描述符选择子
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;保存程序管理器的 TSS 描述符选择子
       mov [prgman_tss+0x04],cx

       ;任务寄存器 TR 中的内容是任务存在的标志，该内容也决定了当前任务是谁。
       ;下面的指令为当前正在执行的 0 特权级任务"程序管理器"后补手续（TSS），将 cx 指向的 TSS 描述符选择子加载到 TR 寄存器中，同时将 TSS 描述符中的 B 位置为 1（NT=0）
       ltr cx

       ;现在可认为 "程序管理器" 任务正执行中
       mov ebx,prgman_msg1
       call sys_routine_seg_sel:put_string

       ;为用户程序的 TCB 控制块分配内存
       mov ecx,0x46
       call sys_routine_seg_sel:allocate_memory
       ;将此 TCB 添加到 TCB 链中
       call append_to_tcb_link             

       ;用户程序位于逻辑 50 扇区
       push dword 50                      
       ;压入任务控制块起始线性地址
       push ecx                            

       ;和上一章不同，任务切换时要恢复 TSS 内容，所以 load_relocate_program 在创建任务时 TSS 要填写完整，包括 0-2 特权级栈的选择子和 ESP 值，当前栈的选择子和 ESP 值等，同时还有各个段寄存器的值和 EFLAGS 寄存器的值
       call load_relocate_program
       ;使用 call 指令执行任务切换
       ;在任务控制块 TCB 中，低地址 0x14 处是 TSS 基地址（32 位），高地址 0x18 处是 TSS 选择子（16 位），低地址的 32 位基地址会被看做是偏移量而忽略
       call far [es:ecx+0x14]
                                   
       ;重新加载并切换任务 
       mov ebx,prgman_msg2
       call sys_routine_seg_sel:put_string

       mov ecx,0x46
       call sys_routine_seg_sel:allocate_memory
       ;将此 TCB 添加到 TCB 链中
       call append_to_tcb_link            
       ;用户程序位于逻辑 50 扇区
       push dword 50                      
       ;压入任务控制块起始线性地址
       push ecx                           

       call load_relocate_program
       ;使用 jmp 指令执行任务切换
       jmp far [es:ecx+0x14]              

       mov ebx,prgman_msg3
       call sys_routine_seg_sel:put_string

       hlt
            
core_code_end:

SECTION core_trail

core_end:
```

## 二、用户程序代码

```armasm{.line-numbers}
       ;代码清单15-2
       ;文件名：c15.asm
       ;文件说明：用户程序 
       ;创建日期：2011-11-15 19:11   

;============================🍔用户程序头部段🍔===============================
SECTION header vstart=0
       ;程序总长度 #0x00
       program_length   dd program_end
       ;程序头部的长度 #0x04
       head_len         dd header_end
       ;用于接收堆栈段选择子 #0x08
       stack_seg        dd 0                    
       ;程序建议的堆栈大小 #0x0c，以 4KB 为单位
       stack_len        dd 1                    

       ;程序入口 #0x10  
       prgentry         dd start                
       ;代码段位置 #0x14
       code_seg         dd section.code.start   
       ;代码段长度 #0x18
       code_len         dd code_end             
       ;数据段位置 #0x1c
       data_seg         dd section.data.start   
       ;数据段长度 #0x20
       data_len         dd data_end             

       ;符号地址检索表
       salt_items       dd (header_end-salt)/256 ;#0x24
       ;#0x28
       salt:                                     
       PrintString      db  '@PrintString'
                        times 256-($-PrintString) db 0
       TerminateProgram db  '@TerminateProgram'
                        times 256-($-TerminateProgram) db 0
       ReadDiskData     db  '@ReadDiskData'
                        times 256-($-ReadDiskData) db 0
                 
header_end:
  
;============================🥩用户程序数据段🥩================================

SECTION data vstart=0                
       message_1        db  0x0d,0x0a
                        db  '[USER TASK]: Hi! nice to meet you,'
                        db  'I am run at CPL=',0
       message_2        db  0
                        db  '.Now,I must exit...',0x0d,0x0a,0
data_end:

      [bits 32]

;==========================🫔用户程序代码段🫔===================================

SECTION code vstart=0
start:
       ;任务启动时，ds 指向头部段，也不需要设置堆栈
       ;这是因为通过 call for [TSS 选择子] 进行任务切换到用户程序时，会从 TSS 中取出用户程序的寄存器值恢复用户程序执行环境
       mov eax,ds
       mov fs,eax

       mov eax,[data_seg]
       mov ds,eax

       mov ebx,message_1
       call far [fs:PrintString]
       
       mov ax,cs
       ;计算当前特权级 CPL（即 cs 代码段的 RPL 位），然后转换成 ASCII 码后写回到数据段
       and al,0000_0011B
       or al,0x0030
       mov [message_2],al
       
       mov ebx,message_2
       call far [fs:PrintString]
       ;退出，并将控制权返回到核心
       call far [fs:TerminateProgram]
    
code_end:

SECTION trail
program_end:
```