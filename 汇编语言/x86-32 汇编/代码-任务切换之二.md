# 任务切换之二

本章的汇编代码和前一章大部分相同，主要增加了通过 TSS 实现任务切换的步骤。

```armasm{.line-numbers}
    ;创建工作在 0 特权级的内核任务-程序管理器，此内核任务主要功能为创建其他任务并且管理他们
    ;为程序管理器的 TSS 分配内存空间
    mov ecx,104
    call sys_routine_seg_sel:allocate_memory
    ;保存程序管理器的 TSS 基地址
    mov [prgman_tss+0x00],ecx

    ;在程序管理器的 TSS 中设置必要的项目 
    ;程序管理器（内核程序）没有 LDT，其所有的段描述符均由 MBR 创建并安装在 GDT 中，并且由于其运行在 0 特权级，所以也不需要创建额外其他特权级的栈
    ;往 TSS 的 LDT 段选择子中写入 0
    mov word [es:ecx+96],0
    ;没有 I/O 位图，这里写入 TSS 的段界限。0 特权级事实上不需要 I/O 位图，因为其可以访问所有的硬件端口
    mov word [es:ecx+102],103
    ;反向链=0（前一个任务的 TSS 指针）
    mov word [es:ecx+0],0
    ;登记 CR3(PDBR)
    mov dword [es:ecx+28],0
    ;T=0
    mov word [es:ecx+100],0

    ;创建 TSS 描述符，并安装到 GDT 中（TSS 描述符只能安装在 GDT 中）
    ;TSS 的起始线性地址
    mov eax,ecx
    ;段长度（界限）
    mov ebx,103
    ;TSS 描述符，特权级 0，G=0（字节为粒度），D/B=1 指示 32 位的偏移地址或操作数
    ;P=1，S=0（表示这是一个系统段），DPL=0（0 特权级），TYPE=1001（B=0，非忙状态）
    mov ecx,0x00408900
    ;eax=线性基地址，ebx=段界限，ecx=属性，构造的段描述符在 edx:eax 中
    call sys_routine_seg_sel:make_seg_descriptor
    ;将 edx:eax 中保存的段描述符安装到 GDT 中，并且返回 ecx，其中保存 TSS 描述符选择子
    call sys_routine_seg_sel:set_up_gdt_descriptor
    ;保存程序管理器的 TSS 描述符选择子
    mov [prgman_tss+0x04],cx

    ;任务寄存器 TR 中的内容是任务存在的标志，该内容也决定了当前任务是谁。
    ;下面的指令为当前正在执行的 0 特权级任务"程序管理器"后补手续（TSS），将 cx 指向的 TSS 描述符选择子加载到 TR 寄存器中，同时将 TSS 描述符中的 B 位置为 1（NT=0）
    ltr cx
```

比如之前内核代码中，内核通过假装从调用门返回的方式来实现从高特权级（内核 0 特权级）向低特权级（用户程序 3 特权级）程序的转移，这种方式比较别扭。在本章中，我们创建一个运行在 0 特权级的内核任务-程序管理器，由于每个任务都有自己独立的地址空间（即独立页目录和页表）和任务状态段 TSS，**<font color="red">所以任务之间是彼此隔离的，只需要用特权级规则控制对 TSS 的访问就行，软件不需要在任务切换时进行显式的特权级检查</font>**，因此 0 特权级的程序管理器任务可以直接转移到 3 特权级的用户程序任务。

程序管理器是计算机启动以来的第一个任务，上述代码为程序管理器任务创建任务状态段（TSS）并往其中保存必要的项目，比如程序管理器（内核程序）没有 LDT，其所有的段描述符均由 MBR 创建并安装在 GDT 中，并且由于其运行在 0 特权级，所以也不需要创建额外其他特权级的栈往 TSS 的 LDT 段选择子中写入 0；又因为它是第一个任务，没有嵌套于其他任务中，所以将 TSS 中的反向链（指向前一个任务）设置为 0。

接着为这个 TSS 创建段描述符，并且安装到 GDT 中，在创建段描述符时，**<font color="red">设置其 TYPE=1001，B（BUSY）标志位设置为 0，同时此任务的 NT=0</font>**。最后将此 TSS 段的基地址和段描述符的选择子分别保存到 **`[prgman_tss+0x00]`** 和 **`[prgman_tss+0x04]`**。

接下来执行 **`ltr cx`** 指令，LTR 指令的作用：Loads the source operand into the segment selector field of the task register. The source operand (a general-purpose register or a memory location) contains a segment selector that points to a task state segment (TSS). After the segment selector is loaded in the task register，the processor uses the segment selector to locate the segment descriptor for the TSS in the global descriptor table (GDT). It then loads the segment limit and base address for the TSS from the segment descriptor into the task register. **<font color="red">The task pointed to by the task register is marked busy (将对应 TSS 段描述符中的 B 置为 1)</font>**，but a switch to the task does not occur.因此此 LTR 指令将此 TSS 描述符的 B 标志位设置为 1，此时程序管理器任务的 B=1，NT=0。

在进行完上述操作后，就调用 **`load_relocate_program`** 例程，此例程和前一章中的例程基本一致，但是其具体工作和上一章相比没有太大变化，仅仅是对 TSS 的填写比较完整。注意这是任务切换的要求，从一个任务切换到另一个任务时，处理器要从新任务的 TSS 中恢复 (加载) 各个寄存器的内容。

```armasm{.line-numbers}
    ;创建用户程序的 TSS
    ;TSS 的基本尺寸
    mov ecx,104
    mov [es:esi+0x12],cx
    ;登记 TSS 界限值到 TCB
    dec word [es:esi+0x12]
    call sys_routine_seg_sel:allocate_memory
    ;登记 TSS 基地址到 TCB
    mov [es:esi+0x14],ecx

    ;登记基本的 TSS 表格内容
    ;反向链=0
    mov word [es:ecx+0],0              
    ;登记 0 特权级堆栈初始 ESP 到 TSS 中 ESP0 处
    mov edx,[es:esi+0x24]              
    mov [es:ecx+4],edx
    ;登记 0 特权级堆栈段选择子到 TSS 中
    mov dx,[es:esi+0x22]               
    mov [es:ecx+8],dx
    ;登记 1 特权级堆栈初始 ESP 到 TSS 中
    mov edx,[es:esi+0x32]              
    mov [es:ecx+12],edx
    ;登记 1 特权级堆栈段选择子到 TSS 中
    mov dx,[es:esi+0x30]               
    mov [es:ecx+16],dx
    ;登记 2 特权级堆栈初始 ESP 到 TSS 中
    mov edx,[es:esi+0x40]
    mov [es:ecx+20],edx
    ;登记 2 特权级堆栈段选择子到 TSS 中
    mov dx,[es:esi+0x3e]
    mov [es:ecx+24],dx
    ;登记任务的 LDT 选择子到 TSS 中
    mov dx,[es:esi+0x10]
    mov [es:ecx+96],dx
    ;登记任务的 I/O 位图偏移到 TSS 中，也就是 TSS 的界限值
    mov dx,[es:esi+0x12]
    mov [es:ecx+102],dx
    ;T=0
    mov word [es:ecx+100],0
    ;登记 CR3(PDBR)
    mov dword [es:ecx+28],0            

    ;访问用户程序头部，获取数据填充 TSS，当以后切换到用户程序之后，用户程序的段寄存器自动被初始化为以下值
    ;从堆栈中取得 TCB 的基地址 
    mov ebx,[ebp+11*4]
    ;用户程序加载的基地址
    mov edi,[es:ebx+0x06]
    ;登记程序入口点（EIP）到 TSS
    mov edx,[es:edi+0x10]
    mov [es:ecx+32],edx
    ;登记程序代码段（CS）选择子到 TSS 中
    mov dx,[es:edi+0x14]
    mov [es:ecx+76],dx
    ;登记程序堆栈段（SS）选择子到 TSS 中
    mov dx,[es:edi+0x08]
    mov [es:ecx+80],dx
    ;登记程序头部段（DS）选择子到 TSS 中
    mov dx,[es:edi+0x04]
    mov word [es:ecx+84],dx
    ;TSS 中的 ES=0
    mov word [es:ecx+72],0
    ;TSS 中的 FS=0
    mov word [es:ecx+88],0
    ;TSS 中的 GS=0
    mov word [es:ecx+92],0

    pushfd
    pop edx
    ;将 EFLAGS 中的内容保存到 TSS 中，这里保存的 EFLAGS 寄存器的值是内核程序管理器任务的值，同时也会给用户程序使用，初始 NT 位为 0
    mov dword [es:ecx+36],edx
```

load_relocate_program 例程中新增的代码如上所示，将 0~2 特权级栈的堆栈段选择子和栈顶指针 esp 保存到 TSS 中，以及将 CR3、T、I/O 位图偏移值保存到 TSS 中。然后就是将 CS、EIP、SS、DS、FS、GS、ES 的值保存到 TSS 中。最后把 EDX 中的内容写入 TSS 中 EFLAGS 域。**<font color="red">注意这是当前任务 (程序管理器) EFLAGS 寄存器的副本，新任务将使用这个副本作为初始的 EFLAGS</font>**。一般来说，此时 EFLAGS 寄存器的 IOPL 字段为 00，将来新任务开始执行时，会用这个副本作为处理器 EFLAGS 寄存器的当前值，并因此而没有足够的 I/O 特权（用户程序的 CPL=3>IOPL）。

>如果当前特权级 CPL 低于任务的 I/O 特权级 IOPL，也并不意味着所有的硬件端口都对当前任务关上了大门。事实上，处理器的意思是总体上不允许，但个别端口除外。至于个别端口是哪些端口，要找到当前任务的 TSS，并检索 I/O 许可位串。

```armasm{.line-numbers}
    call load_relocate_program
    ;使用 call 指令执行任务切换
    ;在任务控制块 TCB 中，低地址 0x14 处是 TSS 基地址（32 位），高地址 0x18 处是 TSS 选择子（16 位），低地址的 32 位基地址会被看做是偏移量而忽略
    call far [es:ecx+0x14]
```

在 load_relocate_program 例程执行完毕之后，就返回到主程序执行，如上所示。当执行上述 call far 指令时，**<font color="red">当执行任务切换时，处理器用得到的选择子访问 GDT，一旦它发现那是一个 TSS 描述符，就知道应该执行任务切换的操作</font>**。首先，因为当前正在执行的任务是由任务寄存器 TR 指示的，所以它要把当前程序管理器任务的每个寄存器的"快照"保存到由 TR 指向的 TSS 中。

然后，处理器用指令中给出的 TSS 选择子访问 GDT，取得新任务的 TSS 描述符，并从该 TSS 中恢复各个寄存器的内容，包括通用寄存器、标志寄存器 EFLAGS、段寄存器、指令指针寄存器 EIP、栈指针寄存器 ESP，以及局部描述符表寄存器 (LDTR) 等。最终，任务寄存器 TR 指向新任务的 TSS，而处理器旋即开始执行新的任务。

程序管理器是计算机启动以来的第一个任务，在任务切换前，其 TSS 描述符的 B 位是 1，EFLAGS 寄存器的 NT 位是 0。因为本次任务切换是用 CALL 指令发起的，因此任务切换后，其 TSS 描述符的 B 位仍旧是 1，EFLAGS 寄存器的 NT 位不变。**当任务切换完成，用户任务成为当前任务，其 TSS 描述符的 B 位置 1；EFLAGS 寄存器的 NT 位置 "1"**；TSS 的任务链接域被修改为前一个任务 (程序管理器任务) 的 TSS 描述符选择子。在切换到用户程序后，执行下述代码：

```armasm{.line-numbers}
SECTION code vstart=0
start:
    ;任务启动时，ds 指向头部段，也不需要设置堆栈
    ;这是因为通过 call for [TSS 选择子] 进行任务切换到用户程序时，会从 TSS 中取出用户程序的寄存器值恢复用户程序执行环境
    mov eax,ds
    mov fs,eax

    mov eax,[data_seg]
    mov ds,eax

    mov ebx,message_1
    call far [fs:PrintString]

    mov ax,cs
    ;计算当前特权级 CPL（即 cs 代码段的 RPL 位），然后转换成 ASCII 码后写回到数据段
    and al,0000_0011B
    or al,0x0030
    mov [message_2],al

    mov ebx,message_2
    call far [fs:PrintString]
    ;退出，并将控制权返回到核心
    call far [fs:TerminateProgram]
```

当前任务的做法稍有些特殊，在显示了信息之后，接着通过调用门转到内核全局空间执行。从该调用门的符号名 "TerminateProgram" 上看，意图是终止当前任务的执行，而不是临时转换到其他任务。其代码如下：

```armasm{.line-numbers}
;终止当前任务，注意，执行此例程时，当前任务仍在运行中。此例程其实也是当前任务的一部分 
terminate_current_task:                     
    ;pushfd 用于将 EFLAGS 寄存器的内容压入堆栈（32 位模式下）
    pushfd
    ;获得 EFLAGS 寄存器内容
    mov edx,[esp]
    ;恢复堆栈指针，使堆栈平衡，保持压入 EFLAGS 寄存器前的状态
    add esp,4

    mov eax,core_data_seg_sel
    mov ds,eax

    ;测试 EFLAGS 寄存器中的 NT 位
    test dx,0100_0000_0000_0000B
    ;当前任务是嵌套的，到 .b1 执行 iretd
    jnz .b1
    ;当前任务不是嵌套的，直接使用 jmp 指令切换到内核程序管理器任务
    mov ebx,core_msg1
    call sys_routine_seg_sel:put_string
    ;程序管理器任务
    ;因为此时程序管理器和当前任务没有形成嵌套关系，所以在返回时也必须要用 jmp
    jmp far [prgman_tss]

.b1: 
    mov ebx,core_msg0
    call sys_routine_seg_sel:put_string
    iretd
```

上述过程用来结束当前用户程序任务的执行，并转换到其他任务。我们现在仍处在用户任务中，要结束当前的用户任务，可以先切换到程序管理器任务，然后回收用户程序所占用的内存空间，并保证不再转换到该任务。**<font color="red">为了切换到程序管理器任务，需要根据当前任务 EFLAGS 寄存器的 NT 位决定是采用 iret 指令，还是 jmp 指令</font>**。因为当前任务是嵌套于程序管理器任务，NT=1，因此转移到标号 b1 处通过 iretd 切换到程序管理器任务执行。

注意，在此处，我们用的是 iretd，而不是 iret。实际上这是同一条指令，机器码都是 CF。在 16 位模式下，iret 指令的操作数默认是 16 位的，要按 32 位操作数执行须加指令前缀 0x66，即 66 CF。为了方便，编译器创造了 iretd。当在 16 位模式下使用 iretd 时，编译器就知道，应当加上指令前缀 0x66。在 32 位模式下，iret 和 iretd 是相同的，下面的示例展示了它们之间的区别:

```armasm{.line-numbers}
[bits 16]
iret        ;编译后的机器码为 CF
iretd       ;编译后的机器码为 66 CF

[bits 32]
iret        ;编译后的机器码为 CF
iretd       ;编译后的机器码为 CF
```

在转移到程序管理任务之后，再重新创建一个新任务 B，然后同样调用 load_relocate_program 程序，填写 B 任务 TSS 中必要的项目，然后调用 jmp far 转移到用户程序 B。
 
```armasm{.line-numbers}
    mov ecx,0x46
    call sys_routine_seg_sel:allocate_memory
    ;将此 TCB 添加到 TCB 链中
    call append_to_tcb_link
    ;用户程序位于逻辑 50 扇区
    push dword 50
    ;压入任务控制块起始线性地址
    push ecx
    call load_relocate_program
    ;使用 jmp 指令执行任务切换
    jmp far [es:ecx+0x14]
```

其次这次是用 JMP 指令发起的任务切换，新任务不会嵌套于旧任务中。任务切换之后，程序管理器任务 TSS 描述符的 B 位被清零，EFLAGS 寄存器的 NT 位不变（NT=0）; 新任务 TSS 描述符的 B 位置位（B=1），EFLAGS 寄存器的 NT 位不变，保持它从 TSS 加载时的状态（NT=0）; 任务链接域的内容不变。

同样，任务 B 也要执行 terminate_current_task 例程，因为 B 任务的 NT=0，因此使用 JMP FAR 指令返回程序管理器任务。注意，因为此时程序管理器和当前任务没有形成嵌套关系，所以在返回时也必须要用 jmp。因为先前在使用 JMP 从程序管理器切换到用户程序 B 时，B 中的任务链接域的内容不变，也就是空，因此无法使用 iretd 返回到程序管理器任务。而如果使用 CALL FAR 指令，则程序管理器任务会嵌套于当前任务 B 中。故只能使用 JMP FAR 指令。