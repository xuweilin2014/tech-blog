# 中断和异常的处理与抢占式多任务

```armasm{.line-numbers}
       ;代码清单17-1
       ;文件名：c17_mbr.asm
       ;文件说明：硬盘主引导扇区代码 
       ;创建日期：2012-07-13 11:20       
       
       ;主引导程序占用 1 个扇区，没有使用分页，所以不需要占用 1 个 4KB 页面的大小
       ;常数，内核加载的起始内存地址
       core_base_address equ 0x00040000
       ;常数，内核的起始逻辑扇区号
       core_start_sector equ 0x00000001

;================================🍫主引导程序段🍫=================================
;由于主引导程序段使用平坦地址模型，因此基地址为 0x00000000，而主引导程序被加载到内存的物理地址为 0x7C00。因此由于段基地址为 0x00000000，EIP 寄存器的初始内容为 0x00007C00
SECTION  mbr  vstart=0x00007c00      

       ;设置堆栈段和栈指针，栈的基地址为 0，栈顶指针 sp 指向 0x7C00
       mov ax,cs
       mov ss,ax
       mov sp,0x7c00

       ;计算 GDT 所在的逻辑段地址
       ;GDT 的 32 位物理基地址
       mov eax,[cs:pgdt+0x02]
       xor edx,edx
       mov ebx,16
       ;分解成 16 位逻辑地址，eax 保存 GDT 段地址，edx 保存 GDT 偏移地址
       ;edx(高 16 位) eax(低 16 位) 除以 ebx，最后的结果商在 eax 中，余数在 edx 中
       div ebx

       ;令 ds 指向该 GDT 段以进行操作
       mov ds,eax
       ;段内起始偏移地址
       mov ebx,edx

       ;跳过 0# 号描述符的槽位
       ;创建 1# 描述符，保护模式下的内核代码段描述符
       ;基地址为 0，界限 0xFFFFF，DPL=00，S=1（代码段或数据段），4KB 粒度，D=1 表示 32 位的偏移地址或操作数，只执行代码段，向上扩展
       mov dword [ebx+0x08],0x0000ffff
       mov dword [ebx+0x0c],0x00cf9800

       ;创建 2# 描述符，保护模式下的内核数据段和堆栈段描述符
       ;基地址为 0，界限 0xFFFFF，DPL=00，S=1，4KB 粒度，D=1 表示 32 位的偏移地址或操作数，可读可写数据段描述符，向上扩展
       mov dword [ebx+0x10],0x0000ffff
       mov dword [ebx+0x14],0x00cf9200

       ;初始化描述符表寄存器 GDTR
       ;描述符表的界限，GDT 描述符表中有 3 个全局描述符（0#、1#、2#），每个描述符 8 字节，因此界限为 23 字节
       mov word [cs:pgdt],23

       lgdt [cs:pgdt]
       ;南桥芯片内的端口，端口 0x92 的第 1 个 bit 位用于控制 A20，它和来自键盘控制器的 A20 控制线一起，通过或门连接到处理器 A20M# 引脚，输入 1 时，高电平，开启 A20 及以上地址
       in al,0x92
       or al,0000_0010B
       ;打开 A20
       out 0x92,al
       ;执行 cli 指令后，将 IF 标志位置 0，禁止 CPU 响应可屏蔽中断（INTR）
       cli

       ;CR0 是 32 位寄存器，第 1 位（位 0）是保护模式允许位（Protection Enable,PE），如果把该位置 1，则处理器进入保护模式
       mov eax,cr0
       or eax,1
       ;设置 PE 位
       mov cr0,eax

       ;以下进入保护模式，清流水线并串行化处理器
       ;16 位的描述符选择子:32 位偏移，这里没有创建 MBR 自己的段描述符，而是使用前面创建的 1# 内核代码段 0~4GB 描述符，也可以实现代码段跳转
       ;段描述符选择子中 0~3 位分别表示 RPL 和 TI，因此这里的 0x0008 段描述符选择子表示 GDTR 中第 1 个内核代码段描述符
       jmp dword 0x0008:flush

       [bits 32]               
  flush:              
       ;加载数据段 (4GB) 选择子
       mov eax,0x00010
       mov ds,eax
       mov es,eax
       mov fs,eax
       mov gs,eax
       ;加载堆栈段 (4GB) 选择子
       mov ss,eax
       ;堆栈指针，指向 0x7000，理论上从 0x00000000~0x00007000 之间的空间都属于堆栈段
       mov esp,0x7000
       
       ;以下加载内核程序到内存中
       ;内核在内存中的起始物理地址/线性地址 0x00040000
       mov edi,core_base_address
       ;内核在磁盘中的起始逻辑扇区号 1
       mov eax,core_start_sector
       mov ebx,edi
       ;以下读取程序的起始部分（一个扇区）到内存 ds:ebx 缓冲区中
       call read_hard_disk_0

       ;以下判断整个程序有多大
       ;内核程序尺寸
       mov eax,[edi]
       xor edx,edx
       ;512 字节每扇区
       mov ecx,512
       ;使用 edx:eax 除以 ecx，商会存放在 eax 中，余数会存放在 edx 中
       div ecx

       or edx,edx
       ;如果未除尽，且 eax = 0，则说明用户程序大小只有不到 1 个扇区的大小，直接跳转到 pge 
       ;如果未除尽，且 eax > 0，则说明用户程序真正占用的扇区数 = eax + 1，所以不用减少扇区数
       ;如果除尽了，用户程序真正占用的扇区数 = eax，由于已经读了一个扇区，所以扇区总数减 1
       jnz @1
       dec eax
   @1:
       ;考虑实际长度 ≤512 个字节的情况
       or eax,eax
       ;EAX=0 ?
       jz pge

       ;读取剩余的扇区
       mov ecx,eax
       mov eax,core_start_sector
       ;从下一个逻辑扇区接着读
       inc eax
   @2:
       call read_hard_disk_0
       inc eax
       ;循环读，直到读完整个内核
       loop @2

   pge:
       ;准备打开分页机制。从此，再也不用在段之间转来转去
       ;创建系统内核的页目录表 PDT，下面为页目录表 PDT 的物理地址
       mov ebx,0x00020000
       ;在页目录创建指向页目录表自己的目录项（页目录最后一个目录项）
       ;MBR 空间有限，后面尽量不使用立即数
       mov dword [ebx+4092],0x00020003 
       mov edx,0x00021003
       ;在页目录内创建与线性地址 0x00000000 对应的目录项
       ;写入目录项（页表的物理地址和属性），此目录项仅用于过渡。
       mov [ebx+0x000],edx

       ;在页目录内创建与线性地址 0x80000000 对应的目录项
       mov [ebx+0x800],edx

       ;创建与上面两个目录项相对应的页表，初始化页表项
       ;页表的物理地址
       mov ebx,0x00021000
       ;起始页的物理地址
       xor eax,eax 
       xor esi,esi
  .b1:
       mov edx,eax
       or edx,0x00000003
       ;登记页的物理地址
       mov [ebx+esi*4],edx
       ;下一个相邻页的物理地址
       add eax,0x1000
       inc esi
       ;仅低端 1MB 内存对应的页才是有效的
       cmp esi,256
       jl .b1
       
       ;下面将页目录 PDT 的基地址赋值给 CR3 寄存器，即页目录表基址寄存器 PDBR，PCD=PWT=0 
       mov eax,0x00020000
       mov cr3,eax

       ;将 GDT 的线性地址映射到从 0x80000000 开始的相同位置 
       sgdt [pgdt]
       mov ebx,[pgdt+2]
       ;GDTR 也用的是线性地址，因此将 GDT 的线性地址加上 0x80000000 即可将 GDT 映射到内存高端
       ;注意，这里只是修改了 GDTR 寄存器的值（加上了 0x80000000），并没有真的将其转移到高端内存处，因此 GDT 在真实物理内存的地址不变，只是"映射"到了地址空间的高端
       ;具体而言，GDTR 中 GDT 的线性基地址变为 0x80008000，当使用段选择子时，将基地址加上段选择子 * 8，得到段描述符的线性地址，最后通过【页部件】转化之后，就得到段描述符的物理地址，并将其加载到段寄存器描述符高速缓存中
       add dword [pgdt+2],0x80000000
       lgdt [pgdt]

       ;开启分页机制
       mov eax,cr0
       or eax,0x80000000
       mov cr0,eax

       ;将内核栈映射到高端，这是非常容易被忽略的一件事。应当把内核的所有东西都移到高端，否则，一定会和正在加载的用户任务局部空间里的内容冲突，而且很难想到问题会出在这里。 
       add esp,0x80000000
       ;这是一个 32 位段内近转移，远转移需要在段间进行，需要 16 位的目标代码选择子以及 32 位的偏移量，但是使用平坦模型之后，不存在段间转移。从线性地址 0x80040004 处取得一个 32 位的段内偏移量，传送到 EIP 寄存器
       jmp [0x80040004]
       
;从硬盘读取一个逻辑扇区
;EAX=逻辑扇区号
;DS:EBX=目标缓冲区地址
;返回：EBX=EBX+512 
read_hard_disk_0:                           

       push eax 
       push ecx
       push edx

       push eax
       
       mov dx,0x1f2
       mov al,1
       out dx,al                          ;读取的扇区数

       inc dx                             ;0x1f3
       pop eax
       out dx,al                          ;LBA地址7~0

       inc dx                             ;0x1f4
       mov cl,8
       shr eax,cl
       out dx,al                          ;LBA地址15~8

       inc dx                             ;0x1f5
       shr eax,cl
       out dx,al                          ;LBA地址23~16

       inc dx                             ;0x1f6
       shr eax,cl
       or al,0xe0                         ;第一硬盘  LBA地址27~24
       out dx,al

       inc dx                             ;0x1f7
       mov al,0x20                        ;读命令
       out dx,al

  .waits:
       in al,dx
       and al,0x88
       cmp al,0x08
       jnz .waits                         ;不忙，且硬盘已准备好数据传输 

       mov ecx,256                        ;总共要读取的字数
       mov dx,0x1f0

  .readw:
       in ax,dx
       mov [ebx],ax
       add ebx,2
       loop .readw

       pop edx
       pop ecx
       pop eax

       ret
                        ;GDT 的界限
       pgdt             dw 0
                        ;GDT 的物理/线性地址
                        dd 0x00008000
                     
       times 510-($-$$) db 0
                        db 0x55,0xaa
```


```armasm{.line-numbers}
       ;代码清单17-2
       ;文件名：c17_core.asm
       ;文件说明：保护模式微型核心程序 
       ;创建日期：2012-07-12 23:15

       ;内核程序所占用的扇区为 9 个扇区，2 个 4KB 页面
       ;以下定义常量
       flat_4gb_code_seg_sel  equ  0x0008      ;平坦模型下的 4GB 代码段选择子 
       flat_4gb_data_seg_sel  equ  0x0018      ;平坦模型下的 4GB 数据段选择子 
       idt_linear_address     equ  0x8001f000  ;中断描述符表的线性基地址 

       ;以下定义宏
       ;在内核空间中分配虚拟内存
       %macro alloc_core_linear 0 
              mov ebx,[core_tcb+0x06]
              add dword [core_tcb+0x06],0x1000
              call flat_4gb_code_seg_sel:alloc_inst_a_page
       %endmacro 
       ;在任务空间中分配虚拟内存
       %macro alloc_user_linear 0 
              mov ebx,[esi+0x06]
              add dword [esi+0x06],0x1000
              call flat_4gb_code_seg_sel:alloc_inst_a_page
       %endmacro
         
;===============================================================================
SECTION  core  vstart=0x80040000

       ;以下是系统核心的头部，用于加载核心程序
       ;核心程序总长度 #00 
       core_length      dd core_end
       ;核心代码段入口点 #04
       core_entry       dd start

       [bits 32]

;字符串显示例程（适用于平坦内存模型） 
;显示 0 终止的字符串并移动光标
;输入：EBX=字符串的线性地址
put_string:                            

       push ebx
       push ecx
       ;硬件操作期间，关中断
       cli

  .getc:
       mov cl,[ebx]
       ;检测串结束标志（0）
       or cl,cl
       ;显示完毕，返回 
       jz .exit
       call put_char
       inc ebx
       jmp .getc

  .exit:
       ;硬件操作完毕，开放中断
       sti
       pop ecx
       pop ebx
       ;段间返回
       retf

;在当前光标处显示一个字符,并推进
;光标。仅用于段内调用 
;输入：CL=字符 ASCII 码 
put_char:                                   
       pushad

       ;以下取当前光标位置
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             
       in al,dx
       mov ah,al

       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;低字
       mov bx,ax                          ;BX=代表光标位置的16位数
       and ebx,0x0000ffff                 ;准备使用32位寻址方式访问显存 
       
       cmp cl,0x0d                        ;回车符？
       jnz .put_0a                         
       
       mov ax,bx                          ;以下按回车符处理 
       mov bl,80
       div bl
       mul bl
       mov bx,ax
       jmp .set_cursor

  .put_0a:
       cmp cl,0x0a                        ;换行符？
       jnz .put_other
       add bx,80                          ;增加一行 
       jmp .roll_screen

  .put_other:                               ;正常显示字符
       shl bx,1
       mov [0x800b8000+ebx],cl            ;在光标位置处显示字符 

       ;以下将光标位置推进一个字符
       shr bx,1
       inc bx

  .roll_screen:
       cmp bx,2000                        ;光标超出屏幕？滚屏
       jl .set_cursor

       cld
       mov esi,0x800b80a0                 ;小心！32位模式下movsb/w/d 
       mov edi,0x800b8000                 ;使用的是esi/edi/ecx 
       mov ecx,1920
       rep movsd
       mov bx,3840                        ;清除屏幕最底一行
       mov ecx,80                         ;32位程序应该使用ECX
  .cls:
       mov word [0x800b8000+ebx],0x0720
       add bx,2
       loop .cls

       mov bx,1920

  .set_cursor:
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       mov al,bh
       out dx,al
       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       mov al,bl
       out dx,al
       
       popad
       
       ret                              


read_hard_disk_0:                           ;从硬盘读取一个逻辑扇区（平坦模型） 
                                            ;EAX=逻辑扇区号
                                            ;EBX=目标缓冲区线性地址
                                            ;返回：EBX=EBX+512
         cli
         
         push eax 
         push ecx
         push edx
      
         push eax
         
         mov dx,0x1f2
         mov al,1
         out dx,al                          ;读取的扇区数

         inc dx                             ;0x1f3
         pop eax
         out dx,al                          ;LBA地址7~0

         inc dx                             ;0x1f4
         mov cl,8
         shr eax,cl
         out dx,al                          ;LBA地址15~8

         inc dx                             ;0x1f5
         shr eax,cl
         out dx,al                          ;LBA地址23~16

         inc dx                             ;0x1f6
         shr eax,cl
         or al,0xe0                         ;第一硬盘  LBA地址27~24
         out dx,al

         inc dx                             ;0x1f7
         mov al,0x20                        ;读命令
         out dx,al

  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                         ;不忙，且硬盘已准备好数据传输 

         mov ecx,256                        ;总共要读取的字数
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [ebx],ax
         add ebx,2
         loop .readw

         pop edx
         pop ecx
         pop eax
      
         sti
      
         retf                               ;远返回 

;在 GDT 内安装一个新的描述符
;输入：edx:eax=描述符 
;输出：cx=描述符的选择子
set_up_gdt_descriptor:   

       push eax
       push ebx
       push edx

       ;取得 gdtr 的界限和线性地址
       sgdt [pgdt] 
       ;gdt 界限->ebx，movzx 指令将一个较小的数据类型（如字节或字）移动到较大的寄存器中，并且用零填充较高的位
       movzx ebx,word [pgdt]
       ;GDT 总字节数，也是下一个描述符偏移
       inc bx
       ;ebx->下一个描述符的线性地址
       add ebx,[pgdt+2]

       mov [ebx],eax
       mov [ebx+4],edx
       ;将 GDT 的界限值增加一个描述符的大小（8 字节）
       add word [pgdt],8
       ;对 GDT 的更改生效
       lgdt [pgdt]
       ;得到 GDT 界限值
       mov ax,[pgdt]
       xor dx,dx
       mov bx,8
       ;除以 8，去掉余数
       div bx
       mov cx,ax
       ;将索引号移到正确位置
       shl cx,3

       pop edx
       pop ebx
       pop eax

       retf

;构造存储器和系统的段描述符
;输入：EAX=线性基地址
;      EBX=段界限
;      ECX=属性。各属性位都在原始位置，无关的位清零 
;返回：EDX:EAX=描述符
make_seg_descriptor:                        

       mov edx,eax
       shl eax,16
       ;描述符前 32 位 EAX 构造完毕
       or ax,bx
       ;清除基地址中无关的位     
       and edx,0xffff0000
       rol edx,8
       ;装配基址的 31~24 和 23~16 (80486+)
       bswap edx

       xor bx,bx
       ;装配段界限的高 4 位
       or edx,ebx
       ;装配属性
       or edx,ecx
       retf

;构造门的描述符（调用门等）
;输入：eax=门代码在段内偏移地址
;      bx=门代码所在段的选择子 
;      cx=段类型及属性等（各属性位都在原始位置）
;返回：edx:eax=完整的描述符
make_gate_descriptor:                       

       push ebx
       push ecx

       mov edx,eax
       ;得到偏移地址高 16 位 
       and edx,0xffff0000
       ;组装属性部分到 edx
       or dx,cx
       ;得到偏移地址低 16 位
       and eax,0x0000ffff 
       shl ebx,16
       ;组装段选择子部分
       or eax,ebx

       pop ecx
       pop ebx
       ;可以使用 ret 指令，因为此例程不会给用户程序使用
       retf                                   

;分配一个4KB的页
;输入：无
;输出：EAX=页的物理地址
allocate_a_4k_page:                         

       push ebx
       push ecx
       push edx

       xor eax,eax
.b1:
       ;bts 指令测试位串中 eax 指定的比特位，首先用该比特位的值设置 EFLAGS 寄存器的 CF 标志，然后将该比特设置为 1
       bts [page_bit_map],eax
       ;CF=0 时跳转到 b2，CF=0 说明此页可以被分配使用
       jnc .b2
       ;测试下一个 bit 位
       inc eax
       cmp eax,page_map_len*8
       jl .b1

       mov ebx,message_3
       call flat_4gb_code_seg_sel:put_string
       ;没有可以分配的页，停机
       hlt

.b2:
       ;乘以 4096（0x1000）
       shl eax,12 

       pop edx
       pop ecx
       pop ebx

       ret
         
;分配一个页，并安装在当前活动的
;层级分页结构中
;输入：EBX=页的线性地址
alloc_inst_a_page:                          

       push eax
       push ebx
       push esi
       
       ;检查该线性地址的页表对应的页目录项在页目录中是否存在
       mov esi,ebx
       and esi,0xffc00000
       ;得到页目录项索引，并乘以 4
       shr esi,20
       ;页目录自身的线性地址 + 表内偏移
       or esi,0xfffff000

       ;P 位是否为 "1"。检查该线性地址是否已经有对应的页表
       test dword [esi],0x00000001 
       jnz .b1
       
       ;创建该线性地址所对应的页表
       ;分配一个页做为页表 
       call allocate_a_4k_page 
       or eax,0x00000007
       ;在页目录中登记该页表的【物理地址】
       mov [esi],eax

.b1:
       ;下面组装页在页表中的页表项对应线性地址，FFC(10 位)—页目录项索引(10 位)—页表项的偏移地址(12 位)
       ;开始访问该线性地址所对应的页表
       mov esi,ebx
       shr esi,10
       ;或者 0xfffff000，因高 10 位是零
       and esi,0x003ff000
       ;得到该页表的线性地址
       or esi,0xffc00000
       
       ;得到该线性地址在页表内的对应条目（页表项）
       and ebx,0x003ff000
       ;相当于右移 12 位，再乘以 4
       shr ebx,10
       ;页表项的线性地址
       or esi,ebx
       ;分配一个页，这才是要安装的页
       call allocate_a_4k_page
       or eax,0x00000007
       ;将该页的【物理地址】登记到页表中
       mov [esi],eax
       
       pop esi
       pop ebx
       pop eax
       
       retf  

;创建新页目录，并复制当前页目录内容
;输入：无
;输出：EAX=新页目录的物理地址
create_copy_cur_pdir:                       

       push esi
       push edi
       push ebx
       push ecx
       
       ;分配一个 4KB 的物理页作为用户程序页目录，并且将页的物理地址保存到 eax 中
       call allocate_a_4k_page
       mov ebx,eax
       ;P=1 表示页位于内存中，RW=1 表示页可读可写，US=1 表示允许所有特权级别程序访问
       or ebx,0x00000007
       ;为了方便访问新分配的用户程序页目录，将其物理地址保存到当前内核页目录的倒数第二项
       mov [0xfffffff8],ebx
       ;修改用户程序页目录的最后一个目录项的值为【用户程序页目录的基地址】
       mov [0xffffeffc],ebx

       invlpg [0xfffffff8]

       ;ESI->当前页目录的线性地址
       mov esi,0xfffff000
       ;EDI->新页目录的线性地址
       mov edi,0xffffe000
       ;ECX=要复制的目录项数
       mov ecx,1024
       cld
       repe movsd 
       
       pop ecx
       pop ebx
       pop edi
       pop esi
       
       retf

;通用的中断处理过程
general_interrupt_handler:
       
       ;当 8259A 响应某一级中断而为其服务时，中断服务寄存器 ISR 的相应位置 1，在中断服务结束时，ISR 中相应位应清 0，以便再次接收同级别的中断。而发送 EOI 是通过往端口 20h 或 A0h 写 OCW2 来实现的。
       push eax
       ;中断结束命令 EOI
       mov al,0x20
       ;向从片发送 
       out 0xa0,al
       ;向主片发送 
       out 0x20,al

       pop eax

       iretd

;通用的异常处理过程
general_exception_handler:
       ;通用异常处理过程，只做两件事：显示错误信息、停机
       mov ebx,excep_msg
       ;内核代码在平坦模型下调用 put_string 不需要使用远转移指令（段间转移），但是 put_string 作为内核公共例程，未来会包装成调用门给用户程序调用（put_string 返回使用的是 retf 指令），因此需要使用段间远转移
       call flat_4gb_code_seg_sel:put_string
       
       hlt

;实时时钟中断处理过程
rtm_0x70_interrupt_handle:

       pushad
       ;中断结束命令 EOI，发送 EOI 是通过往端口 20h 或 A0h 写 OCW2 来实现的
       ;发送中断结束命令将 ISR 寄存器中的相应位清零，只有这样，才能允许同样的中断再次被处理
       mov al,0x20
       ;向 8259A 从片发送
       out 0xa0,al
       ;向 8259A 主片发送
       out 0x20,al

       ;寄存器 C 的索引。且开放 NMI
       mov al,0x0c
       out 0x70,al
       ;读一下 RTC 的寄存器 C，否则只发生一次中断。此处闹钟和周期性中断的情况在初始化 CMOS RTC 时已经被禁止，只考虑更新结束中断的情况
       in al,0x71
       ;找当前任务（状态为忙的任务）在链表中的位置
       mov eax,tcb_chain
.b0:
       ;EAX=前一个 TCB 块的线性地址
       ;EBX=当前 TCB 块的线性地址
       mov ebx,[eax]
       or ebx,ebx
       ;如果链表为空，或已到末尾，则从中断返回
       jz .irtn
       ;是忙任务（当前任务）？
       cmp word [ebx+0x04],0xffff
       je .b1
       ;定位到下一个 TCB（的线性地址）
       mov eax,ebx
       jmp .b0

       ;将当前为忙的任务移到链尾
.b1:
       ;EAX=前一个 TCB 块的线性地址
       ;EBX=当前 TCB 块的线性地址
       ;ECX=下一个 TCB 块的线性地址
       mov ecx,[ebx]
       ;将当前任务的 TCB 块从链中拆除
       mov [eax],ecx

.b2:  
       ;此时，EDX=当前任务 TCB 块的线性地址
       ;EAX=前一个任务 TCB 块的线性地址
       mov edx,[eax]
       ;已到链表尾端？
       or edx,edx
       jz .b3
       mov eax,edx
       jmp .b2

.b3:
       ;将忙任务的 TCB 挂在链表尾端
       mov [eax],ebx
       ;将忙任务的 TCB 标记为链尾，在每个 TCB 控制块的 0x00 偏移处，是下一个 TCB 块的线性地址，如果此线性地址为 0，说明已经到了链表尾部
       mov dword [ebx],0x00000000

       ;从链首搜索第一个空闲任务
       mov eax,tcb_chain
.b4:
       mov eax,[eax]
       ;已到链尾（未发现空闲任务）
       or eax,eax
       ;未发现空闲任务，从中断返回，此时不进行任务切换操作
       jz .irtn
       ;是空闲任务？
       cmp word [eax+0x04],0x0000
       jnz .b4

       ;将空闲任务和当前任务的状态都取反
       ;设置空闲任务的状态为忙
       not word [eax+0x04]
       ;设置当前任务（忙）的状态为空闲
       not word [ebx+0x04]
       ;任务转换
       jmp far [eax+0x14]

.irtn:
       popad
       iretd

;终止当前任务
;注意，执行此例程时，当前任务仍在运行中。此例程其实也是当前任务的一部分 
terminate_current_task:                     
       ;找当前任务（状态为忙的任务）在链表中的位置
       mov eax,tcb_chain
.b0:                                      ;EAX=链表头或当前TCB线性地址
       mov ebx,[eax]                      ;EBX=下一个TCB线性地址
       cmp word [ebx+0x04],0xffff         ;是忙任务（当前任务）？
       je .b1
       mov eax,ebx                        ;定位到下一个TCB（的线性地址）
       jmp .b0
         
.b1:
       ;修改当前任务的状态为 "退出"
       mov word [ebx+0x04],0x3333
         
.b2:
       ;停机，等待程序管理器恢复运行时，将其回收
       hlt 
       jmp .b2 

       ;用于设置和修改 GDT
       pgdt             dw  0 
                        dd  0
       ;中断描述符表 IDT 的界限（2 字节）和线性基地址（4字节）
       pidt             dw  0
                        dd  0

       ;任务控制块链
       tcb_chain        dd  0 
       ;内核（程序管理器）的 TCB，一共 32*4=128 字节
       core_tcb         times 32 dd 0

       page_bit_map     db  0xff,0xff,0x00,0x00,0x00,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0x55,0x55,0x55,0x55,0x55,0x55,0x55,0x55
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
       page_map_len     equ $-page_bit_map

       ;符号地址检索表
       salt:
       salt_1           db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        dw  flat_4gb_code_seg_sel

       salt_2           db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  flat_4gb_code_seg_sel

       salt_3           db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  flat_4gb_code_seg_sel

       salt_4           db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  terminate_current_task
                        dw  flat_4gb_code_seg_sel

       salt_item_len    equ $-salt_4
       salt_items       equ ($-salt)/salt_item_len

       excep_msg        db  '********Exception encounted********',0
       message_0        db  '  Working in system core with protection '
                        db  'and paging are all enabled.System core is mapped '
                        db  'to address 0x80000000.',0x0d,0x0a,0
       message_1        db  '  System wide CALL-GATE mounted.',0x0d,0x0a,0
       message_3        db  '********No more pages********',0
       core_msg0        db  '  System core task running!',0x0d,0x0a,0
       bin_hex          db '0123456789ABCDEF'
       ;内核用的缓冲区
       core_buf         times 512 db 0
       cpu_brnd0        db 0x0d,0x0a,'  ',0
       cpu_brand        times 52 db 0
       cpu_brnd1        db 0x0d,0x0a,0x0d,0x0a,0

;在 LDT 内安装一个新的描述符
;输入：EDX:EAX=描述符
;          EBX=TCB 基地址
;输出：CX=描述符的选择子
fill_descriptor_in_ldt:                     

       push eax
       push edx
       push edi

       ;获得 LDT 基地址
       mov edi,[ebx+0x0c]
       xor ecx,ecx
       ;获得 LDT 界限
       mov cx,[ebx+0x0a]
       ;LDT 的总字节数，即新描述符偏移地址
       inc cx
       ;安装描述符
       mov [edi+ecx+0x00],eax
       mov [edi+ecx+0x04],edx

       ;得到新的 LDT 界限值
       add cx,8                           
       dec cx 
       ;更新 LDT 界限值到 TCB
       mov [ebx+0x0a],cx

       mov ax,cx
       xor dx,dx
       mov cx,8
       div cx
       
       mov cx,ax
       ;左移 3 位，并且使 TI 位=1，指向 LDT，最后使 RPL=00
       ;段选择子的 TI 位表示描述符的位置，TI=1 表示描述符在 LDT 中，TI=0 表示描述符在 GDT 中
       shl cx,3
       or cx,0000_0000_0000_0100B 

       pop edi
       pop edx
       pop eax

       ret
      
;加载并重定位用户程序
;输入: PUSH 逻辑扇区号
;      PUSH 任务控制块基地址
;输出：无 
load_relocate_program:                      

       pushad
       ;为访问通过堆栈传递的参数做准备
       mov ebp,esp

       ;清空当前页目录的前半部分（对应低 2GB 的局部地址空间）
       mov ebx,0xfffff000
       xor esi,esi
  .b1:
       mov dword [ebx+esi*4],0x00000000
       inc esi
       cmp esi,512
       jl .b1

       mov eax,cr3
       ;刷新 TLB，让 TLB 中的所有条目都失效，因为对于页表项的修改不会反映到 TLB 中，可能导致访问的物理地址错误
       mov cr3,eax 
       
       ;以下开始分配内存并加载用户程序
       ;从堆栈中取出用户程序起始扇区号
       mov eax,[ebp+40]
       ;读取用户程序头部数据到内核缓冲区 core_buf 中
       mov ebx,core_buf
       call flat_4gb_code_seg_sel:read_hard_disk_0

       ;以下判断整个程序有多大
       ;程序尺寸
       mov eax,[core_buf]
       mov ebx,eax
       ;使之 4KB 对齐
       and ebx,0xfffff000 
       add ebx,0x1000     
       ;程序的大小正好是 4KB 的倍数吗?
       ;对齐之后，程序大小 >= 4KB
       test eax,0x00000fff
       ;不是。使用凑整的结果
       cmovnz eax,ebx

       mov ecx,eax
       ;程序占用的总 4KB 页数
       shr ecx,12 
       ;用户程序的起始扇区号
       mov eax,[ebp+40]
       ;从堆栈中取得用户程序 TCB 块的线性地址
       mov esi,[ebp+36]
  .b2:
       ;宏：在用户任务地址空间上分配内存保存用户程序
       ;每次分配 1 个 4KB 页面，需要从磁盘读取 8 个 512 字节扇区。根据前面计算代码，即使程序的大小不足 4KB，也会使其对齐成为 4KB，因此用户程序最小也占 4KB。
       alloc_user_linear
       
       push ecx
       ;4096 = 512*8，所以需要读取 8 个
       mov ecx,8
  .b3:
       call flat_4gb_code_seg_sel:read_hard_disk_0
       inc eax
       loop .b3

       pop ecx
       loop .b2

       ;在内核地址空间内创建用户任务的 TSS，用户任务的 TSS/TCB 必须在全局空间上分配
       ;宏：在内核的地址空间上分配内存，ebx 为 TSS 的线性基地址，esi 为 TCB 的线性基地址
       alloc_core_linear
       ;在用户程序 TCB 中填写用户 TSS 的线性地址
       mov [esi+0x14],ebx
       ;在 TCB 中填写 TSS 的界限值
       mov word [esi+0x12],103
       
       ;在用户任务的局部地址空间内创建 LDT
       ;宏：在用户任务地址空间上分配内存 
       alloc_user_linear
       ;填写 LDT 线性地址到 TCB 中
       mov [esi+0x0c],ebx

       ;建立程序代码段描述符
       mov eax,0x00000000
       mov ebx,0x000fffff
       ;代码段描述符，特权级 3；G=1 段界限的粒度为 4KB；D=1 表示 32 位的偏移地址或操作数；S=1 表示数据段或代码段；TYPE=1000，只执行的代码段；
       mov ecx,0x00c0f800
       call flat_4gb_code_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi
       ;将代码段描述符安装到 LDT 中
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B
       
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 CS 域
       mov [ebx+76],cx 

       ;建立程序数据段描述符
       mov eax,0x00000000
       mov ebx,0x000fffff
       ;数据段描述符，特权级 3；G=1 段界限的粒度为 4KB；D=1 表示 32 位的偏移地址或操作数；S=1 表示数据段或代码段；TYPE=0010，可读写的数据段；                
       mov ecx,0x00c0f200
       call flat_4gb_code_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi                        
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 DS 域
       mov [ebx+84],cx 
       ;填写 TSS 的 ES 域
       mov [ebx+72],cx
       ;填写 TSS 的 FS 域
       mov [ebx+88],cx
       ;填写 TSS 的 GS 域
       mov [ebx+92],cx
       
       ;将数据段作为用户任务的 3 特权级固有堆栈
       ;3 特权级堆栈的基地址 ss 为此数据段的基地址，高端 esp 为 TCB 块中下一个可以分配的地址 
       ;宏：在用户任务地址空间上分配内存
       alloc_user_linear
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 SS 域
       mov [ebx+80],cx
       ;堆栈的高端线性地址
       mov edx,[esi+0x06]
       ;填写 TSS 的 ESP 域 
       mov [ebx+56],edx 

       ;在用户任务的局部地址空间内创建 0 特权级堆栈
       ;宏：在用户任务地址空间上分配内存
       alloc_user_linear
       mov eax,0x00000000
       mov ebx,0x000fffff
       ;堆栈段描述符，特权级 3；G=1 段界限的粒度为 4KB；D=1 表示 32 位的偏移地址或操作数；S=1 表示数据段或代码段；TYPE=0010，可读写的数据段/堆栈段；
       mov ecx,0x00c09200
       call flat_4gb_code_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 0
       or cx,0000_0000_0000_0000B
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 SS0 域
       mov [ebx+8],cx
       ;堆栈的高端线性地址
       mov edx,[esi+0x06]
       ;填写 TSS 的 ESP0 域
       mov [ebx+4],edx

       ;在用户任务的局部地址空间内创建 1 特权级堆栈
       ;宏：在用户任务地址空间上分配内存
       alloc_user_linear

       mov eax,0x00000000
       mov ebx,0x000fffff
       ;4KB 粒度的堆栈段描述符，特权级 1
       mov ecx,0x00c0b200
       call flat_4gb_code_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 1
       or cx,0000_0000_0000_0001B
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 SS1 域
       mov [ebx+16],cx
       ;堆栈的高端线性地址
       mov edx,[esi+0x06]
       ;填写 TSS 的 ESP1 域
       mov [ebx+12],edx 

       ;在用户任务的局部地址空间内创建 2 特权级堆栈
       ;宏：在用户任务地址空间上分配内存
       alloc_user_linear

       mov eax,0x00000000
       mov ebx,0x000fffff
       mov ecx,0x00c0d200
       ;4KB 粒度的堆栈段描述符，特权级 2
       call flat_4gb_code_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 2
       or cx,0000_0000_0000_0010B
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 SS2 域
       mov [ebx+24],cx
       ;堆栈的高端线性地址
       mov edx,[esi+0x06]
       ;填写 TSS 的 ESP2 域
       mov [ebx+20],edx 

       ;重定位 U-SALT
       cld
       ;U-SALT 条目数
       mov ecx,[0x0c] 
       ;U-SALT 在 4GB 空间内的偏移，edi
       mov edi,[0x08]
  .b4:
       push ecx
       push edi
       ;C-SALT 条目数
       mov ecx,salt_items
       ;C-SALT 在 4GB 空间内的偏移，esi
       mov esi,salt
  .b5:
       push edi
       push esi
       push ecx

       ;检索表中，每条目的比较次数
       mov ecx,64
       ;cmpsd 每次比较 4 字节，这条指令比较 esi 寄存器指向的源字符串（或内存位置）和 edi 寄存器指向的目标字符串（或内存位置）中的双字（32 位数据）
       ;repe 是一个前缀，重复执行直到 ecx 寄存器减至 0 或者零标志 ZF 被清除（不相等时）
       repe cmpsd
       jnz .b6
       ;若匹配，则 esi 恰好指向其后的 32 位偏移地址
       mov eax,[esi]
       ;将字符串改写成偏移地址
       mov [edi-256],eax 
       mov ax,[esi+4]
       ;以用户程序自己的特权级使用调用门
       or ax,0000000000000011B
       ;回填调用门选择子
       mov [edi-252],ax 
  .b6:
      
       pop ecx
       pop esi
       add esi,salt_item_len
       ;从头比较
       pop edi 
       loop .b5

       pop edi
       add edi,256
       pop ecx
       loop .b4

       ;在 GDT 中登记 LDT 描述符
       ;从堆栈中取得用户程序 TCB 的基地址
       mov esi,[ebp+36]  
       ;LDT 的起始线性地址
       mov eax,[esi+0x0c]
       ;LDT 段界限
       movzx ebx,word [esi+0x0a]
       ;LDT 描述符，特权级 0
       mov ecx,0x00408200
       call flat_4gb_code_seg_sel:make_seg_descriptor
       call flat_4gb_code_seg_sel:set_up_gdt_descriptor
       ;登记 LDT 选择子到 TCB 中
       mov [esi+0x10],cx
       
       ;从 TCB 中获取用户程序 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 LDT 域 
       mov [ebx+96],cx
       ;反向链=0
       mov word [ebx+0],0
       ;段长度（界限）
       mov dx,[esi+0x12]
       ;填写 TSS 的 I/O 位图偏移域
       mov [ebx+102],dx
       ;T=0
       mov word [ebx+100],0
       ;从任务的 4GB 地址空间获取入口点
       mov eax,[0x04] 
       ;填写 TSS 的 EIP 域
       mov [ebx+32],eax 

       pushfd
       pop edx
       ;填写 TSS 的 EFLAGS 域
       mov [ebx+36],edx

       ;在 GDT 中登记 TSS 描述符
       ;从 TCB 中获取 TSS 的起始线性地址
       mov eax,[esi+0x14]
       ;段长度（界限）
       movzx ebx,word [esi+0x12]
       ;TSS 描述符，特权级 0
       mov ecx,0x00408900
       call flat_4gb_code_seg_sel:make_seg_descriptor
       call flat_4gb_code_seg_sel:set_up_gdt_descriptor
       ;登记 TSS 选择子到 TCB
       mov [esi+0x18],cx

       ;创建用户任务的页目录
       ;注意！页的分配和使用是由页位图决定的，可以不占用线性地址空间 
       call flat_4gb_code_seg_sel:create_copy_cur_pdir
       ;从 TCB 中获取 TSS 的线性地址
       mov ebx,[esi+0x14]
       ;填写 TSS 的 CR3(PDBR) 域
       mov dword [ebx+28],eax

       popad
       ;丢弃调用本过程前压入的参数
       ret 8 
      
;在 TCB 链上追加任务控制块
;输入：ECX=TCB 线性基地址
append_to_tcb_link:
                            
       cli
       
       push eax
       push ebx

       ;EAX=链表头或当前 TCB 线性地址
       mov eax,tcb_chain
  .b0:              
       ;EBX=下一个 TCB 线性地址                        
       mov ebx,[eax]
       or ebx,ebx
       ;链表为空，或已到末尾
       jz .b1
       ;定位到下一个 TCB（的线性地址）
       mov eax,ebx
       jmp .b0

  .b1:
       mov [eax],ecx
       ;当前 TCB 指针域清零，以指示这是最后一个 TCB
       mov dword [ecx],0x00000000

       pop ebx
       pop eax
       
       sti
       
       ret
         
start:
       ;创建中断描述符表 IDT
       ;由于保护模式的中断处理和实模式不同，在 CPU 跳转到内核代码之前已经使用 cli 指令禁止 INTR 可屏蔽中断。在此之前，禁止调用 put_string 过程，以及任何含有 sti 指令的过程。IDT 中前 20 个向量是处理器异常使用的
       ;门代码在段内偏移地址
       mov eax,general_exception_handler
       ;门代码所在段的选择子
       mov bx,flat_4gb_code_seg_sel
       ;32 位中断门，0 特权级
       ;中断门和陷阱门的区别在于进入中断处理程序后，通过中断门进入的话，EFLAGS 中 IF 位被清零，禁止中断嵌套；通过陷阱门进入的话，IF 位保持不变，允许其他中断
       mov cx,0x8e00
       ;组装中断门描述符到 edx:eax 中
       call flat_4gb_code_seg_sel:make_gate_descriptor
       ;中断描述符表的线性地址
       mov ebx,idt_linear_address
       xor esi,esi
.idt0:
       mov [ebx+esi*8],eax
       mov [ebx+esi*8+4],edx
       inc esi
       ;安装前 20 个异常中断处理过程
       cmp esi,19
       jle .idt0

       ;其余为保留或硬件使用的中断向量（中断向量 20~255），将通用中断处理过程安装到 IDT 中
       ;门代码在段内偏移地址
       mov eax,general_interrupt_handler
       ;门代码所在段的选择子
       mov bx,flat_4gb_code_seg_sel
       ;32 位中断门，0 特权级
       mov cx,0x8e00
       call flat_4gb_code_seg_sel:make_gate_descriptor
       ;中断描述符表的线性地址，上面组装的中断门描述符在 edx:eax 中
       mov ebx,idt_linear_address
.idt1:
       mov [ebx+esi*8],eax
       mov [ebx+esi*8+4],edx
       inc esi
       ;安装普通的中断处理过程
       cmp esi,255
       jle .idt1

       ;设置实时时钟中断处理过程
       ;门代码在段内偏移地址
       mov eax,rtm_0x70_interrupt_handle
       ;门代码所在段的选择子
       mov bx,flat_4gb_code_seg_sel
       ;32 位中断门，0 特权级
       mov cx,0x8e00
       ;组装中断门描述符到 edx:eax 中
       call flat_4gb_code_seg_sel:make_gate_descriptor

       ;中断描述符表的线性地址
       mov ebx,idt_linear_address
       mov [ebx+0x70*8],eax
       mov [ebx+0x70*8+4],edx

       ;准备开放中断
       ;IDT 的界限
       mov word [pidt],256*8-1
       mov dword [pidt+2],idt_linear_address
       ;加载中断描述符表寄存器 IDTR
       ;lidt 指向一个包含了 48 位（6 字节）数据的内存区域，包含 idt 表的界限和基地址
       lidt [pidt]

       ;设置 8259A 中断控制器，首先设置主片 0x20/0x21
       mov al,0x11
       ;ICW1:边沿触发/多片级联方式
       out 0x20,al
       ;ICW2:起始中断向量，ICW2 的中断状态为 100B，再加上中断请求 IR2IR1IR0 编码，主片中中断向量的编码为 0x20-0x27
       mov al,0x20
       out 0x21,al
       ;ICW3:从片级联到主片的 IR2 引脚
       mov al,0x04
       out 0x21,al
       ;ICW4:非总线缓冲，全嵌套，正常 EOI 命令
       mov al,0x01
       out 0x21,al

       ;设置从片 0xa0/0xa1
       mov al,0x11
       ;ICW1:边沿触发/级联方式
       out 0xa0,al
       ;ICW2:起始中断向量，从片中断向量的编号为 0x70-0x77
       mov al,0x70
       out 0xa1,al
       ;ICW3:从片级联到 IR2
       mov al,0x02
       out 0xa1,al
       ;ICW4:非总线缓冲，全嵌套，正常 EOI
       mov al,0x01
       out 0xa1,al

       ;设置和时钟中断相关的硬件
       ;RTC 寄存器 B
       mov al,0x0b
       ;阻断 NMI，端口 0x70 寄存器的最高位（bit 7）为控制 NMI 中断的开关，当其为 0 时，表示允许 NMI 中断到达处理器
       or al,0x80
       ;使用索引端口设置要访问 RTC 的寄存器 B
       out 0x70,al
       ;设置寄存器 B，禁止周期性和闹钟中断，开放更新结束后中断，并且更新周期每秒都会正常发生，BCD 码，24 小时制
       mov al,0x12
       ;使用数据端口 0x71 向寄存器 B 写入数据
       out 0x71,al

       ;读 8259A 从片的 IMR/OCW1 寄存器
       in al,0xa1
       ;清除 bit 0 (此位连接 RTC)，将从片 8259A 中 IMR 寄存器和实时时钟中断对应的位清零，表示允许时钟中断
       and al,0xfe
       ;写回 IMR/OCW1 寄存器
       out 0xa1,al
       
       ;0x70 或 0x74 是索引端口，而 0x71 或 0x75 是数据端口
       ;前面设置了寄存器 B，表明允许哪些中断，现在读取寄存器 C 的值，寄存器 C 的值在每次读取后清零，如果没有清零，RTC 将不会产生中断信号
       mov al,0x0c
       out 0x70,al
       ;读 RTC 寄存器 C，复位未决的中断状态
       in al,0x71

       ;开放硬件中断
       sti

       mov ebx,message_0
       call flat_4gb_code_seg_sel:put_string

       ;显示处理器品牌信息 
       mov eax,0x80000002
       cpuid
       mov [cpu_brand + 0x00],eax
       mov [cpu_brand + 0x04],ebx
       mov [cpu_brand + 0x08],ecx
       mov [cpu_brand + 0x0c],edx

       mov eax,0x80000003
       cpuid
       mov [cpu_brand + 0x10],eax
       mov [cpu_brand + 0x14],ebx
       mov [cpu_brand + 0x18],ecx
       mov [cpu_brand + 0x1c],edx

       mov eax,0x80000004
       cpuid
       mov [cpu_brand + 0x20],eax
       mov [cpu_brand + 0x24],ebx
       mov [cpu_brand + 0x28],ecx
       mov [cpu_brand + 0x2c],edx

       ;显示处理器品牌信息
       mov ebx,cpu_brnd0 
       call flat_4gb_code_seg_sel:put_string
       mov ebx,cpu_brand
       call flat_4gb_code_seg_sel:put_string
       mov ebx,cpu_brnd1
       call flat_4gb_code_seg_sel:put_string

       ;以下开始安装为整个系统服务的调用门
       ;调用门（call-gate）用于在特权级的程序之间进行控制转移。本质上，它只是一个描述符，可以安装在 GDT 和 LDT 中。在调用门描述符中给出了例程所在的代码段选择子，有了段选择子再结合调用门中的偏移量就可以定位例程的入口地址。
       ;C-SALT 表的起始位置
       mov edi,salt
       ;C-SALT 表的条目数
       mov ecx,salt_items
  .b4:
       push ecx
       ;该条目入口点的 32 位偏移地址
       mov eax,[edi+256]
       ;该条目入口点的段选择子 
       mov bx,[edi+260]
       ;特权级 3 的调用门 (3 以上的特权级才允许访问)，0 个参数 (因为用寄存器传递参数，而没有用栈)
       mov cx,1_11_0_1100_000_00000B
       ;创建调用门描述符，并将其安装到 GDT 中
       call flat_4gb_code_seg_sel:make_gate_descriptor
       call flat_4gb_code_seg_sel:set_up_gdt_descriptor
       ;将返回的门描述符选择子回填，而 edi+256 处的 32 位偏移量将会被忽略
       mov [edi+260],cx
       ;指向下一个 C-SALT 条目
       add edi,salt_item_len 
       pop ecx
       loop .b4

       ;对门进行测试 
       mov ebx,message_1
       ;32 位间接绝对远调用，通过调用门显示信息 ，但是 salt_1+256 处的偏移量将被忽略，这里使用调用门描述符中的偏移量
       call far [salt_1+256]

       ;创建并初始化【程序管理器任务】的任务控制块 TCB
       ;首先设置内核任务的状态值为忙碌
       mov word [core_tcb+0x04],0xffff
       ;内核虚拟空间的分配从这里开始，因为前 1MB（0x80000000~0x800FFFFF）已经被内核用完了，所以内核地址空间新的内存分配从 0x80100000 开始
       mov dword [core_tcb+0x06],0x80100000
       ;登记 LDT 初始的界限到 TCB 中（未使用）
       mov word [core_tcb+0x0a],0xffff
       mov ecx,core_tcb
       ;将此 TCB 添加到 TCB 链的末尾
       call append_to_tcb_link

       ;为程序管理器的 TSS 分配内存空间，并将分配页的物理地址在页目录或页表中进行登记
       ;宏：在内核的虚拟地址空间分配内存
       alloc_core_linear

       ;在程序管理器的 TSS 中设置必要的项目
       ;反向链=0 
       mov word [ebx+0],0
       mov eax,cr3
       ;登记 CR3(PDBR)
       mov dword [ebx+28],eax
       ;没有 LDT。处理器允许没有 LDT 的任务
       mov word [ebx+96],0
       ;T=0
       mov word [ebx+100],0
       ;没有 I/O 位图。0 特权级事实上不需要
       mov word [ebx+102],103
       
       ;创建程序管理器的 TSS 描述符，并安装到 GDT 中 
       ;TSS 的起始线性地址
       mov eax,ebx
       ;段长度（界限）
       mov ebx,103
       ;TSS 描述符，特权级 0
       mov ecx,0x00408900
       call flat_4gb_code_seg_sel:make_seg_descriptor
       call flat_4gb_code_seg_sel:set_up_gdt_descriptor
       ;登记内核任务的 TSS 选择子到其 TCB
       mov [core_tcb+0x18],cx

       ;任务寄存器 TR 中的内容是任务存在的标志，该内容也决定了当前任务是谁。
       ;下面的指令为当前正在执行的 0 特权级任务 "程序管理器" 后补手续（TSS）。
       ;ltr 指令将 TSS 选择子加载到任务寄存器 tr 中
       ltr cx

       ;现在可认为"程序管理器"任务正执行中

       ;创建用户任务的任务控制块 TCB，内核为了访问和控制所有的任务，每个任务的 TCB 块都必须创建在内核的地址空间中
       ;宏：在内核的虚拟地址空间分配内存
       alloc_core_linear
       
       ;任务状态：空闲
       mov word [ebx+0x04],0
       ;用户任务局部空间的分配从 0 开始
       mov dword [ebx+0x06],0
       ;登记 LDT 初始的界限到 TCB 中
       mov word [ebx+0x0a],0xffff

       ;用户程序位于逻辑 50 扇区
       push dword 50
       ;压入任务控制块 TCB 起始线性地址
       push ebx
       call load_relocate_program
       mov ecx,ebx
       ;将此 TCB 添加到 TCB 链中
       call append_to_tcb_link

       ;创建用户任务的任务控制块
       ;宏：在内核的虚拟地址空间分配内存
       alloc_core_linear

       ;任务状态：空闲
       mov word [ebx+0x04],0
       ;用户任务局部空间的分配从 0 开始
       mov dword [ebx+0x06],0
       ;登记 LDT 初始的界限到 TCB 中
       mov word [ebx+0x0a],0xffff

       ;用户程序位于逻辑 100 扇区
       push dword 100
       ;压入任务控制块起始线性地址
       push ebx
       call load_relocate_program
       mov ecx,ebx
       ;将此 TCB 添加到 TCB 链中
       call append_to_tcb_link

  .core:
       mov ebx,core_msg0
       call flat_4gb_code_seg_sel:put_string
       
       ;这里可以编写回收已终止任务内存的代码
       jmp .core
            
core_code_end:

SECTION core_trail
core_end:
```


```armasm{.line-numbers}
       ;代码清单17-3
       ;文件名：c17_1.asm
       ;文件说明：用户程序 
       ;创建日期：2012-07-14 15:46   

       program_length   dd program_end          ;程序总长度#0x00
       entry_point      dd start                ;程序入口点#0x04
       salt_position    dd salt_begin           ;SALT表起始偏移量#0x08 
       salt_items       dd (salt_end-salt_begin)/256 ;SALT条目数#0x0C

       ;符号地址检索表
       salt_begin:                                     

       PrintString      db  '@PrintString'
                        times 256-($-PrintString) db 0
       TerminateProgram db  '@TerminateProgram'
                        times 256-($-TerminateProgram) db 0
       ReadDiskData     db  '@ReadDiskData'
                        times 256-($-ReadDiskData) db 0
       PrintDwordAsHex  db  '@PrintDwordAsHexString'
                        times 256-($-PrintDwordAsHex) db 0
       salt_end:
       message_0        db  '  User task A->;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;'
                        db  0x0d,0x0a,0

      [bits 32]

start:
          
       mov ebx,message_0
       call far [PrintString]
       jmp start
       ;退出，并将控制权返回到核心
       call far [TerminateProgram] 

program_end:
```


```armasm{.line-numbers}
         ;代码清单17-4
         ;文件名：c17_2.asm
         ;文件说明：用户程序 
         ;创建日期：2012-07-16 12:27   

       program_length   dd program_end          ;程序总长度#0x00
       entry_point      dd start                ;程序入口点#0x04
       salt_position    dd salt_begin           ;SALT表起始偏移量#0x08 
       salt_items       dd (salt_end-salt_begin)/256 ;SALT条目数#0x0C

       ;符号地址检索表
       salt_begin:

       PrintString      db  '@PrintString'
                        times 256-($-PrintString) db 0
       TerminateProgram db  '@TerminateProgram'
                        times 256-($-TerminateProgram) db 0
       ReadDiskData     db  '@ReadDiskData'
                        times 256-($-ReadDiskData) db 0
       PrintDwordAsHex  db  '@PrintDwordAsHexString'
                        times 256-($-PrintDwordAsHex) db 0
       salt_end:
       message_0        db  '  User task B->$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$$'
                        db  0x0d,0x0a,0

      [bits 32]

start:
          
       mov ebx,message_0
       call far [PrintString]
       jmp start  
       ;退出，并将控制权返回到核心
       call far [TerminateProgram] 
    
program_end:
```