# 任务和特权级保护

下面的内核代码的流程：

- 首先循环遍历 C-SALT 表开始安装为整个系统服务的调用门描述符到 GDT 中（DPL=0），同时将调用门描述符选择子回填到 C-SALT 表中（RPL=0）；
- 为马上要执行的任务创建任务控制块 TCB，并将其加入到 TCB 链表中；
- 为 LDT 申请内存空间，并将 LDT 的基地址和界限保存到 TCB 中；
- 将用户程序全部加载到内存中；
- 为用户程序头部段、数据段、代码段、堆栈段创建描述符并安装到 LDT 中，这些段描述符的 DPL 为 3，选择子的 RPL 为 3；
- 重定位 U-SALT，将用户程序头部 U-SALT 表格中的调用例程名称替换成对应调用门选择子和偏移量；
- 为用户程序创建 0、1、2 特权级的堆栈描述符，然后将其安装到 LDT 中，并且将这些堆栈描述符的基地址、段界限、选择子、堆栈指针 ESP 的值到 TCB 中；
- 将 LDT 系统段的描述符安装到 GDT 中，描述符的 DPL=0；
- 为用户程序创建任务状态段 TSS，然后登记基本的 TSS 表格内容；
- 在 GDT 中安装 TSS 描述符，并且将 TSS 描述符的相关信息写入到 TCB 中；
- 加载任务状态段 TSS 的选择子到 TR 寄存器中，加载 LDT 选择子到 LDTR 寄存器中；
- 从调用门返回到用户程序中进行执行（从高特权级 0 级返回到低特权级 3 级）

```armasm{.line-numbers}
       ;代码清单14-1
       ;文件名：c14_core.asm
       ;文件说明：保护模式微型核心程序 
       ;创建日期：2011-11-6 18:37

       ;只有具备能动性的代码段（不一定是当前代码段）才能发出请求，所以 RPL 是指【真正】发出访问请求的程序的特权级（但是真正发出访问请求的代码段和当前正在执行的代码段可能不是同一个，所以 CPL 不一定等于 RPL）
       ;由于内核程序请求访问自己的数据段、代码段等，请求者和当前正在执行的代码段是同一个，所以这里段选择子的 RPL=CPL=0
       ;内核代码段选择子
       core_code_seg_sel     equ  0x38
       ;内核数据段选择子
       core_data_seg_sel     equ  0x30
       ;系统公共例程代码段的选择子
       sys_routine_seg_sel   equ  0x28
       ;视频显示缓冲区的段选择子
       video_ram_seg_sel     equ  0x20
       ;内核堆栈段选择子
       core_stack_seg_sel    equ  0x18
       ;整个 0-4GB 内存的段的选择子
       mem_0_4_gb_seg_sel    equ  0x08    

       ;以下是系统核心的头部，用于加载核心程序 
       ;核心程序总长度 #00
       core_length      dd core_end       
       ;系统公用例程段偏移量 #04
       sys_routine_seg  dd section.sys_routine.start                
       ;核心数据段偏移量 #08
       core_data_seg    dd section.core_data.start                
       ;核心代码段偏移量 #0c
       core_code_seg    dd section.core_code.start
       ;核心代码段入口点 #10
       core_entry       dd start          
                        dw core_code_seg_sel

       [bits 32]

;系统公共例程代码段
SECTION sys_routine vstart=0                 

       ;字符串显示例程
       ;显示 0 终止的字符串并移动光标
put_string:                                  
       ;输入：ds:ebx=串地址
       push ecx
  .getc:
       mov cl,[ebx]
       or cl,cl
       jz .exit
       call put_char
       inc ebx
       jmp .getc

  .exit:
       pop ecx
       ;段间返回
       retf                               

;在当前光标处显示一个字符,并推进
;光标，仅用于段内调用 
;输入：cl=字符 ascii 码
put_char:
       pushad

       ;以下取当前光标位置
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;高字
       mov ah,al

       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       in al,dx                           ;低字
       mov bx,ax                          ;BX=代表光标位置的16位数

       cmp cl,0x0d                        ;回车符？
       jnz .put_0a
       mov ax,bx
       mov bl,80
       div bl
       mul bl
       mov bx,ax
       jmp .set_cursor

  .put_0a:
       cmp cl,0x0a                        ;换行符？
       jnz .put_other
       add bx,80
       jmp .roll_screen

  .put_other:                               ;正常显示字符
       push es
       mov eax,video_ram_seg_sel          ;0xb8000段的选择子
       mov es,eax
       shl bx,1
       mov [es:bx],cl
       pop es

       ;以下将光标位置推进一个字符
       shr bx,1
       inc bx

  .roll_screen:
       cmp bx,2000                        ;光标超出屏幕？滚屏
       jl .set_cursor

       push ds
       push es
       mov eax,video_ram_seg_sel
       mov ds,eax
       mov es,eax
       cld
       mov esi,0xa0                       ;小心！32位模式下movsb/w/d 
       mov edi,0x00                       ;使用的是esi/edi/ecx 
       mov ecx,1920
       rep movsd
       mov bx,3840                        ;清除屏幕最底一行
       mov ecx,80                         ;32位程序应该使用ECX
  .cls:
       mov word[es:bx],0x0720
       add bx,2
       loop .cls

       pop es
       pop ds

       mov bx,1920

  .set_cursor:
       mov dx,0x3d4
       mov al,0x0e
       out dx,al
       inc dx                             ;0x3d5
       mov al,bh
       out dx,al
       dec dx                             ;0x3d4
       mov al,0x0f
       out dx,al
       inc dx                             ;0x3d5
       mov al,bl
       out dx,al

       popad
       
       ret                                

;从硬盘读取一个逻辑扇区
;eax=逻辑扇区号
;ds:ebx=目标缓冲区地址
;返回：ebx=ebx+512
read_hard_disk_0:                           

         push eax 
         push ecx
         push edx
      
         push eax
         
         mov dx,0x1f2
         mov al,1
         out dx,al                          ;读取的扇区数

         inc dx                             ;0x1f3
         pop eax
         out dx,al                          ;LBA地址7~0

         inc dx                             ;0x1f4
         mov cl,8
         shr eax,cl
         out dx,al                          ;LBA地址15~8

         inc dx                             ;0x1f5
         shr eax,cl
         out dx,al                          ;LBA地址23~16

         inc dx                             ;0x1f6
         shr eax,cl
         or al,0xe0                         ;第一硬盘  LBA地址27~24
         out dx,al

         inc dx                             ;0x1f7
         mov al,0x20                        ;读命令
         out dx,al

  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                         ;不忙，且硬盘已准备好数据传输 

         mov ecx,256                        ;总共要读取的字数
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [ebx],ax
         add ebx,2
         loop .readw

         pop edx
         pop ecx
         pop eax
      
         retf                               ;段间返回 

;分配内存
;输入：ecx=希望分配的字节数
;输出：ecx=起始线性地址
allocate_memory:                             

       push ds
       push eax
       push ebx
       ;ds 寄存器保存内核数据段的选择子
       mov eax,core_data_seg_sel
       mov ds,eax
       ;[ram_alloc] 内存处保存的是本次分配内存的起始地址
       mov eax,[ram_alloc]
       ;加上 ecx，最后 eax 变为下一次分配内存时的起始地址
       add eax,ecx

       ;这里应当有检测可用内存数量的指令
       ;返回本次分配内存的起始地址
       mov ecx,[ram_alloc]

       ;在 32 位计算机中，分配的内存地址建议为 4 字节对齐的，以下代码将内存分配地址 4 字节对齐后写回到 ram_alloc 处
       mov ebx,eax
       ;能够被 4 整除的数的低 2 位均为 0，下面将 ebx 中的内存地址进行 4 字节对齐
       and ebx,0xfffffffc
       add ebx,4
       ;如果原先 eax 中的地址是 4 字节对齐的，那么就直接使用 eax 写回内存作为下一次分配的起始地址
       ;否则，就使用计算出来的 4 字节对齐的 ebx 作为下一次分配的起始地址
       test eax,0x00000003
       cmovnz eax,ebx
       mov [ram_alloc],eax

       pop ebx
       pop eax
       pop ds

       retf

;在 GDT 内安装一个新的描述符
;输入：edx:eax=描述符 
;输出：cx=描述符的选择子
set_up_gdt_descriptor:                      

       push eax
       push ebx
       push edx

       push ds
       push es

       ;切换到内核数据段
       mov ebx,core_data_seg_sel          
       mov ds,ebx
       ;以便开始处理 GDT，将 GDTR 寄存器的基地址和边界信息保存到指定内存位置
       ;将低 2 字节用于保存 GDT 的界限，高 4 字节用于保存 GDT 的 32 位物理地址     
       sgdt [pgdt]

       mov ebx,mem_0_4_gb_seg_sel
       mov es,ebx

       ;GDT 界限
       movzx ebx,word [pgdt]
       ;GDT 总字节数，也是 GDT 中下一个描述符偏移
       inc bx
       ;加上 GDT 的 32 位线性基地址，得到下一个调用门描述符的线性地址
       add ebx,[pgdt+2]
       ;将调用门描述符的低 32 位 eax 和高 32 位 edx 写入到 GDT 中
       mov [es:ebx],eax
       mov [es:ebx+4],edx
       ;增加一个描述符的大小 8 字节，更新 GDT 的界限值
       add word [pgdt],8
       ;将 GDT 的基址和界限保存到 GDTR 寄存器中，对 GDT 的更改生效
       lgdt [pgdt]

       ;得到 GDT 界限值
       mov ax,[pgdt]
       xor dx,dx
       mov bx,8
       ;除以 8，忽略余数，得到的商就是调用门描述符在 GDT 中的索引号
       div bx                             
       mov cx,ax
       ;将索引号移到正确位置
       shl cx,3                           

       pop es
       pop ds

       pop edx
       pop ebx
       pop eax

       retf

;构造存储器和系统的段描述符
;输入：eax=线性基地址
;      ebx=段界限
;      ecx=属性。各属性位都在原始位置，无关的位清零 
;返回：edx:eax=描述符
make_seg_descriptor:                        

       mov edx,eax
       shl eax,16
       ;描述符前 32 位 (EAX) 构造完毕
       or ax,bx                           
       ;清除基地址中无关的位
       and edx,0xffff0000                 
       rol edx,8
       ;装配基址的 31~24 和 23~16 (80486+)
       bswap edx                          

       xor bx,bx
       ;装配段界限的高 4 位
       or edx,ebx                         
       ;装配属性
       or edx,ecx                         

       retf

;构造门的描述符（调用门等）
;输入：eax=调用门代码在段内偏移地址
;      bx=调用门代码所在段的选择子 
;      cx=段类型及属性等（各属性位都在原始位置）
;返回：edx:eax=完整的描述符
make_gate_descriptor:                       

       push ebx
       push ecx

       mov edx,eax
       ;得到偏移地址高 16 位
       and edx,0xffff0000
       ;组装属性部分到 edx
       or dx,cx

       ;得到偏移地址低 16 位
       and eax,0x0000ffff
       shl ebx,16
       ;组装段选择子部分
       or eax,ebx

       pop ecx
       pop ebx

       retf                                   
                             
sys_routine_end:

;============================🥪内核核心数据段🥪==================================
SECTION core_data vstart=0      

       ;用于设置和修改 GDT
       pgdt             dw  0              
                        dd  0

       ;下次分配内存时的起始地址
       ram_alloc        dd  0x00100000    

       ;符号地址检索表 C-SALT
       salt:
       salt_1           db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        ;这里公共调用段选择子会被替换为调用门描述符选择子，下同
                        dw  sys_routine_seg_sel

       salt_2           db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  sys_routine_seg_sel

       salt_3           db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  sys_routine_seg_sel

       salt_4           db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  return_point
                        dw  core_code_seg_sel

       salt_item_len    equ $-salt_4
       salt_items       equ ($-salt)/salt_item_len

       message_1        db  '  If you seen this message,that means we '
                        db  'are now in protect mode,and the system '
                        db  'core is loaded,and the video display '
                        db  'routine works perfectly.',0x0d,0x0a,0

       message_2        db  '  System wide CALL-GATE mounted.',0x0d,0x0a,0
       
       message_3        db  0x0d,0x0a,'  Loading user program...',0
       
       do_status        db  'Done.',0x0d,0x0a,0
       
       message_6        db  0x0d,0x0a,0x0d,0x0a,0x0d,0x0a
                        db  '  User program terminated,control returned.',0

       bin_hex          db '0123456789ABCDEF'

       ;put_hex_dword 子过程用的查找表 
       ;内核用的缓冲区
       core_buf         times 2048 db 0         
       ;内核用来临时保存自己的栈指针
       esp_pointer      dd 0                   

       cpu_brnd0        db 0x0d,0x0a,'  ',0
       cpu_brand        times 52 db 0
       cpu_brnd1        db 0x0d,0x0a,0x0d,0x0a,0

       ;任务控制块链，指向第一个 TCB 控制块的指针
       tcb_chain        dd  0

core_data_end:

;===============================🧇内核程序段🧇==============================
SECTION core_code vstart=0

;在 LDT 内安装一个新的描述符
;输入：edx:eax=描述符
;     ebx=TCB 基地址
;输出：cx=描述符的选择子
fill_descriptor_in_ldt:                     

       push eax
       push edx
       push edi
       push ds

       mov ecx,mem_0_4_gb_seg_sel
       mov ds,ecx
       ;获得 LDT 基地址
       mov edi,[ebx+0x0c]
       
       xor ecx,ecx
       ;获得 LDT 界限
       mov cx,[ebx+0x0a]
       ;界限值加 1 即为 LDT 的总字节数，也是新描述符偏移地址
       ;这里有和 GDT 一样的问题，初始时 LDT 界限为 0xFFFF，因此只能使用 cx 而不是 ecx
       inc cx

       ;安装描述符
       mov [edi+ecx+0x00],eax
       mov [edi+ecx+0x04],edx

       add cx,8                           
       ;得到新的 LDT 界限值
       dec cx
       ;更新 LDT 界限值到 TCB
       mov [ebx+0x0a],cx                  

       ;下面的代码组装 LDT 描述符选择子
       ;对于描述符索引来说，假设现在 LDT 中段界限为 15（有 2 个描述符），那么 15/8 商为 1，可以作为这第 2 个描述符的索引
       mov ax,cx
       xor dx,dx
       mov cx,8
       div cx

       mov cx,ax
       ;左移 3 位，并且使 TI 位=1，指向 LDT，最后使 RPL=00
       shl cx,3
       or cx,0000_0000_0000_0100B

       pop ds
       pop edi
       pop edx
       pop eax

       ret
  
;加载并重定位用户程序
;输入: PUSH 逻辑扇区号
;      PUSH 任务控制块基地址
;输出：无
load_relocate_program:                      
       ;用于将 8 个 32 位寄存器（eax、ecx、edx、ebx、esp、ebp、esi、edi）的内容压入栈
       pushad

       push ds
       push es

       ;🍣为访问通过堆栈传递的参数做准备🍣
       mov ebp,esp
       mov ecx,mem_0_4_gb_seg_sel
       mov es,ecx
       ;从堆栈中取得 TCB 的基地址
       mov esi,[ebp+11*4]

       ;以下申请创建 LDT 所需要的内存
       ;允许安装 20 个 LDT 描述符（一个 LDT 描述符长 8 个字节）
       mov ecx,160
       call sys_routine_seg_sel:allocate_memory
       ;登记 LDT 基地址到 TCB 中
       mov [es:esi+0x0c],ecx
       ;登记 LDT 初始的界限到 TCB 中，界限值为 LDT 表的总字节数减 1，初始的字节数为 0，因此当前界限值为 0xFFFF
       mov word [es:esi+0x0a],0xffff

       ;以下开始加载用户程序
       mov eax,core_data_seg_sel
       ;切换 ds 到内核数据段
       mov ds,eax
       ;从堆栈中取出用户程序起始扇区号 50
       mov eax,[ebp+12*4]
       ;读取程序头部数据
       mov ebx,core_buf
       ;读取 eax 所指的扇区数据（1 个扇区）到 ds:ebx 内存区域中，即内核数据段的 core_buf 缓冲区
       call sys_routine_seg_sel:read_hard_disk_0

       ;以下判断整个程序有多大，首先获取用户程序尺寸，再对尺寸大小进行 512 字节对齐
       mov eax,[core_buf]
       mov ebx,eax
       ;使之 512 字节对齐（能被 512 整除的数低 9 位都为 0）
       and ebx,0xfffffe00
       add ebx,512
       ;程序的大小 eax 正好是 512 的倍数吗? 
       test eax,0x000001ff
       ;不是，则使用凑整的结果 ebx，否则，使用 eax 的结果
       cmovnz eax,ebx

       ;实际需要申请的内存数量
       mov ecx,eax
       call sys_routine_seg_sel:allocate_memory
       ;登记用户程序的加载基地址到 TCB 中
       mov [es:esi+0x06],ecx

       ;ebx -> 申请到的内存首地址
       mov ebx,ecx
       xor edx,edx
       mov ecx,512
       ;使用前面计算出来的用户程序大小 eax 除以 512，得到用户程序的总扇区数
       ;32 位的 div 指令会对 64 位的被除数 edx:eax 进行除法运算，商存储在 eax，余数存储在 edx
       div ecx
       ;总扇区数
       mov ecx,eax

       ;切换 ds 到 0-4GB 的段
       mov eax,mem_0_4_gb_seg_sel
       mov ds,eax
       ;从堆栈中取出用户程序起始扇区号
       mov eax,[ebp+12*4]
.b1:
       ;循环读取整个用户程序到申请到的内存首地址处（ebx）
       call sys_routine_seg_sel:read_hard_disk_0
       inc eax
       loop .b1

       ;获得用户程序在内存中的线性基地址
       mov edi,[es:esi+0x06]

       ;建立程序头部段描述符
       ;程序头部段的起始线性地址
       mov eax,edi
       ;程序头部段的段长度
       mov ebx,[edi+0x04]
       ;程序头部段的段界限
       dec ebx
       ;字节粒度的 32 位可读可写数据段描述符，特权级 3
       mov ecx,0x0040f200
       ;根据 eax、ebx 和 ecx 的值生成对应的用户程序头部段描述符
       ;eax=线性基地址，ebx=段界限，ecx=属性，最后生成的描述符保存在 edx:eax 中
       call sys_routine_seg_sel:make_seg_descriptor
       ;安装用户程序头部段描述符到 LDT 中
       ;保存 TCB 控制块的基地址 esi 到 ebx 中
       mov ebx,esi
       ;输入 edx:eax 是刚创建的段描述符，ebx 是 TCB 基地址，返回的描述符选择子保存在 cx 中
       call fill_descriptor_in_ldt
       ;设置选择子的特权级为 3
       or cx,0000_0000_0000_0011B
       ;登记用户程序头部段选择子到 TCB 控制块和用户程序头部 #0x04 偏移处
       mov [es:esi+0x44],cx
       mov [edi+0x04],cx

       ;建立程序代码段描述符
       mov eax,edi
       ;用户程序代码段的起始线性地址
       add eax,[edi+0x14]
       ;用户程序代码段的段长度
       mov ebx,[edi+0x18]
       ;用户程序代码段的段界限
       dec ebx
       ;字节粒度的 32 位代码段描述符，特权级 3（DPL 为 3）
       mov ecx,0x0040f800
       ;生成对应的用户程序代码段描述符
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置用户程序代码段选择子的特权级为 3（RPL 为 3）
       or cx,0000_0000_0000_0011B
       ;登记代码段选择子到用户程序头部 #0x14 偏移处
       mov [edi+0x14],cx

       ;生成用户程序数据段描述符，并安装到此任务的 LDT 中，最后登记数据段选择子到用户程序头部 #0x1c 偏移处 
       mov eax,edi
       ;数据段起始线性地址
       add eax,[edi+0x1c]
       ;数据段的段长度
       mov ebx,[edi+0x20]
       ;数据段的段界限
       dec ebx
       ;字节粒度的数据段描述符，特权级 DPL=3 
       mov ecx,0x0040f200
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置选择子的特权级 RPL=3
       or cx,0000_0000_0000_0011B
       mov [edi+0x1c],cx

       ;建立程序堆栈段描述符
       ;4KB 的倍率
       mov ecx,[edi+0x0c]
       mov ebx,0x000fffff
       ;得到段界限
       sub ebx,ecx
       mov eax,4096
       ;用 eax 寄存器中的值乘以另外一个 32 位的数，在 edx:eax 中得到 64 位的结果
       mul ecx
       ;准备为堆栈分配内存
       mov ecx,eax
       call sys_routine_seg_sel:allocate_memory
       ;得到堆栈的高端物理地址
       add eax,ecx
       ;字节粒度的堆栈段描述符，特权级 DPL=3 
       mov ecx,0x00c0f600
       call sys_routine_seg_sel:make_seg_descriptor
       ;TCB 的基地址
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置用户程序堆栈段选择子的特权级 RPL=3
       or cx,0000_0000_0000_0011B
       ;登记堆栈段选择子到头部
       mov [edi+0x08],cx

       ;🫔重定位 SALT🫔
       ;这里和前一章不同，头部段描述符已经安装，但还没有生效（还没有加载进局部描述符表寄存器 LDTR），故只能通过 4GB 段访问用户程序头部
       mov eax,mem_0_4_gb_seg_sel
       mov es,eax

       mov eax,core_data_seg_sel
       mov ds,eax

       ;以下外循环为用户程序中 U-SALT 条目，而内循环是内核程序 C-SALT 条目，每次读取一个 U-SALT 条目和 C-SALT 中的条目进行比对，如果比对通过，那么将 C-SALT 中调用例程的调用门选择子和偏移量写到用户程序 U-SALT 对应条目中

       cld
       ;用户程序 U-SALT 条目数 (通过访问 4GB 段取得) 
       mov ecx,[es:edi+0x24]
       ;用户程序内的 U-SALT 位于头部段内 0x28 偏移处，加上 edi 就是 U-SALT 在内存中的起始物理地址，es:edi 指向用户程序 U-SALT 表条目
       add edi,0x28
.b2:
       push ecx
       push edi

       ;内核程序中 C-SALT 条目的数量，也是内循环的循环次数
       mov ecx,salt_items
       ;ds:esi 指向内核程序的 C-SALT 表条目
       mov esi,salt
.b3:
       push edi
       push esi
       push ecx

       ;检索表中，每条目的比较次数
       mov ecx,64
       ;每次比较 4 字节 
       repe cmpsd
       jnz .b4
       ;若匹配，则 esi 恰好指向 C-SALT 表条目中调用例程的偏移地址
       mov eax,[esi]
       ;将用户程序 U-SALT 表对应的条目改写成偏移地址
       mov [es:edi-256],eax
       mov ax,[esi+4]
       ;以用户程序自己的特权级使用调用门故 RPL=3
       or ax,0000000000000011B
       ;回填调用门选择子到 U-SALT 中
       mov [es:edi-252],ax
.b4:

       pop ecx
       pop esi
       add esi,salt_item_len
       ;从头比较
       pop edi                             
       loop .b3

       pop edi
       add edi,256
       pop ecx
       loop .b2

       ;从堆栈中取得 TCB 的基地址
       mov esi,[ebp+11*4]                 

       ;🫕创建 0-2 特权级堆栈，每个特权级堆栈的大小为 4KB🫕
       ;创建 0 特权级堆栈
       mov ecx,4096
       ;为生成堆栈高端地址做准备
       mov eax,ecx
       
       mov [es:esi+0x1a],ecx
       ;登记 0 特权级堆栈尺寸到 TCB，由于尺寸是以 4KB 为单位，所以需要将先前保存的尺寸大小逻辑右移 12 位，除以 4096
       shr dword [es:esi+0x1a],12
       call sys_routine_seg_sel:allocate_memory
       ;堆栈必须使用高端地址为基地址
       add eax,ecx
       ;登记 0 特权级堆栈基地址到 TCB
       mov [es:esi+0x1e],eax
       ;0 特权级堆栈段界限
       mov ebx,0xffffe
       ;4KB 粒度，读写堆栈段，DPL 特权级 0，向下扩展
       mov ecx,0x00c09600
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       ;安装生成的 0 特权级堆栈描述符到 LDT 中
       call fill_descriptor_in_ldt
       ;设置选择子的 RPL 特权级为 0，由于上面过程返回的选择子 RPL 本身为 0，因此下面这条指令是作为注释存在的
       ;or cx,0000_0000_0000_0000
       ;登记 0 特权级堆栈选择子到 TCB
       mov [es:esi+0x22],cx
       ;登记 0 特权级堆栈初始 ESP 到 TCB
       mov dword [es:esi+0x24],0

       ;创建 1 特权级堆栈
       mov ecx,4096
       ;为生成堆栈高端地址做准备
       mov eax,ecx
       mov [es:esi+0x28],ecx
       ;登记 1 特权级堆栈尺寸到 TCB
       shr [es:esi+0x28],12
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx
       ;登记 1 特权级堆栈基地址到 TCB 控制块中
       mov [es:esi+0x2c],eax
       ;段界限
       mov ebx,0xffffe
       ;4KB 粒度，读写堆栈段，DPL 特权级 1，向下扩展
       mov ecx,0x00c0b600                 
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置 1 特权级堆栈段选择子的 RPL 特权级为 1
       or cx,0000_0000_0000_0001
       ;登记 1 特权级堆栈选择子到 TCB
       mov [es:esi+0x30],cx
       ;登记 1 特权级堆栈初始 ESP 到 TCB
       mov dword [es:esi+0x32],0

       ;创建 2 特权级堆栈
       mov ecx,4096
       mov eax,ecx
       mov [es:esi+0x36],ecx
       shr [es:esi+0x36],12
       call sys_routine_seg_sel:allocate_memory
       add eax,ecx
       ;登记 2 特权级堆栈基地址到 TCB
       mov [es:esi+0x3a],ecx
       mov ebx,0xffffe
       ;4KB 粒度，读写堆栈段，DPL 特权级 2，向下扩展
       mov ecx,0x00c0d600
       call sys_routine_seg_sel:make_seg_descriptor
       mov ebx,esi
       call fill_descriptor_in_ldt
       ;设置 2 特权级堆栈段选择子的 RPL 特权级为 2
       or cx,0000_0000_0000_0010
       ;登记 2 特权级堆栈选择子到 TCB
       mov [es:esi+0x3e],cx
       ;登记 2 特权级堆栈初始 ESP 到 TCB
       mov dword [es:esi+0x40],0

       ;🌮在 GDT 中登记 LDT 描述符🌮
       ;LDT 的起始线性/物理地址
       mov eax,[es:esi+0x0c]
       ;LDT 的段界限
       movzx ebx,word [es:esi+0x0a]
       ;LDT 描述符，特权级 DPL=0
       mov ecx,0x00408200
       call sys_routine_seg_sel:make_seg_descriptor
       ;安装 LDT 描述符到 GDT 表中
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;登记 LDT 选择子到 TCB 中，LDT 选择子的内容后续加载到 LDTR 中用来从 GDT 中选择 LDT 描述符
       mov [es:esi+0x10],cx

       ;🍱创建用户程序任务状态段 TSS🍱
       ;TSS 的基本尺寸为 104 字节
       mov ecx,104
       mov [es:esi+0x12],cx  
       ;登记 TSS 界限值到 TCB（TSS 字节数大小减 1）            
       dec word [es:esi+0x12] 
       call sys_routine_seg_sel:allocate_memory
       ;登记 TSS 基地址到 TCB
       mov [es:esi+0x14],ecx

       ;登记基本的 TSS 表格内容
       ;反向链=0
       mov word [es:ecx+0],0

       ;登记 0 特权级堆栈初始 ESP 和堆栈段选择子到 TSS 中
       mov edx,[es:esi+0x24]              
       mov [es:ecx+4],edx
       mov dx,[es:esi+0x22]
       mov [es:ecx+8],dx

       ;登记 1 特权级堆栈初始 ESP 和堆栈段选择子到 TSS 中
       mov edx,[es:esi+0x32]              
       mov [es:ecx+12],edx
       mov dx,[es:esi+0x30]
       mov [es:ecx+16],dx

       ;登记 2 特权级堆栈初始 ESP 和堆栈段选择子到 TSS 中
       mov edx,[es:esi+0x40]
       mov [es:ecx+20],edx
       mov dx,[es:esi+0x3e]
       mov [es:ecx+24],dx

       ;登记任务的 LDT 选择子到 TSS 中
       mov dx,[es:esi+0x10]               
       mov [es:ecx+96],dx

       ;登记任务的 I/O 许可位串起始地址到 TSS 中，这里登记的其实是 TSS 的界限值，说明此任务没有 I/O 许可位串
       mov dx,[es:esi+0x12]               
       mov [es:ecx+102],dx                 
       ;T=0
       mov word [es:ecx+100],0            

       ;🍣在 GDT 中登记 TSS 描述符🍣
       ;TSS 的起始线性地址
       mov eax,[es:esi+0x14]       
       ;TSS 的段界限   
       movzx ebx,word [es:esi+0x12]
       ;TSS 描述符，特权级 0
       mov ecx,0x00408900
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;登记 TSS 选择子到 TCB 控制块
       mov [es:esi+0x18],cx

       ;恢复到调用此过程前的 es 段
       pop es
       ;恢复到调用此过程前的 ds 段 
       pop ds

       popad
       ;丢弃调用本过程前压入的参数
       ret 8 
      
;在 tcb 链上追加任务控制块
;输入：ecx=tcb 线性基地址
append_to_tcb_link:                         

       push eax
       push edx
       push ds
       push es

       ;令 ds 指向内核数据段
       mov eax,core_data_seg_sel
       mov ds,eax
       ;令 es 指向数据内存 0..4GB 段
       mov eax,mem_0_4_gb_seg_sel
       mov es,eax
       ;当前 tcb 指针域（位于 tcb 控制块的第一个双字）清零，以指示这是最后一个 tcb
       mov dword [es:ecx+0x00],0
       ;tcb 表头指针，[tcb_chain] 处保存第一个 tcb 控制块的基地址
       mov eax,[tcb_chain]
       ;链表为空？
       or eax,eax
       jz .notcb 
       
       ;一直遍历链表，直到某个 tcb 控制块的指针域为 0，说明遍历到了链表中最后一个 tcb 控制块，最后将此 tcb 控制块的指针域设置为 ecx（尾插法）
.searc:
       mov edx,eax
       mov eax,[es:edx+0x00]
       or eax,eax               
       jnz .searc
       ;将新创建的 tcb 控制块的基地址写入到 tcb 链表的尾部
       mov [es:edx+0x00],ecx
       jmp .retpc

.notcb:       
       ;若为空表，直接令表头指针指向 tcb
       mov [tcb_chain],ecx                

.retpc:
       pop es
       pop ds
       pop edx
       pop eax

       ret

start:
       ;使 ds 指向核心数据段
       mov ecx,core_data_seg_sel
       mov ds,ecx

       mov ebx,message_1
       ;ds:ebx=字符串的起始地址
       call sys_routine_seg_sel:put_string

       ;显示处理器品牌信息 
       mov eax,0x80000002
       cpuid
       mov [cpu_brand + 0x00],eax
       mov [cpu_brand + 0x04],ebx
       mov [cpu_brand + 0x08],ecx
       mov [cpu_brand + 0x0c],edx

       mov eax,0x80000003
       cpuid
       mov [cpu_brand + 0x10],eax
       mov [cpu_brand + 0x14],ebx
       mov [cpu_brand + 0x18],ecx
       mov [cpu_brand + 0x1c],edx

       mov eax,0x80000004
       cpuid
       mov [cpu_brand + 0x20],eax
       mov [cpu_brand + 0x24],ebx
       mov [cpu_brand + 0x28],ecx
       mov [cpu_brand + 0x2c],edx

       ;显示处理器品牌信息
       mov ebx,cpu_brnd0
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brand
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brnd1
       call sys_routine_seg_sel:put_string

       ;以下循环遍历内核 C-SALT 表开始安装为整个系统服务的调用门描述符到 GDT 中，特权级之间的控制转移必须使用门
       ;C-SALT 表的起始位置
       mov edi,salt
       ;C-SALT 表的条目数量
       mov ecx,salt_items
.b3:
       push ecx
       ;该条目入口点的 32 位偏移地址
       mov eax,[edi+256]
       ;该条目入口点的段选择子 
       mov bx,[edi+260]
       ;特权级 3 的调用门 (3 以上的特权级才允许访问)，0 个参数 (因为用寄存器传递参数，而没有用栈)
       ;调用门描述符的 DPL 为 3，表示只有当前特权级 CPL 和调用门选择子 RPL <= 调用门描述符的 DPL 才允许访问此描述符，继而跳转到此例程
       mov cx,1_11_0_1100_000_00000B
       ;生成调用门描述符，并保存在 edx:eax
       call sys_routine_seg_sel:make_gate_descriptor
       ;🥓将生成的调用门描述符安装到 GDT 中，返回的描述符选择子保存在 cx 寄存器中
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;将返回的调用门描述符选择子（RPL=0）回填到 C-SALT 表中，即原先段选择子的部分
       mov [edi+260],cx
       ;指向下一个 C-SALT 条目
       add edi,salt_item_len
       pop ecx
       loop .b3

       ;对调用门进行测试
       mov ebx,message_2
       ;通过调用门显示信息 (偏移量将被忽略)
       call far [salt_1+256]

       mov ebx,message_3
       ;在内核中调用例程不需要通过调用门（因为不需要进行特权级转移） 
       call sys_routine_seg_sel:put_string

       ;创建任务控制块 TCB。这不是处理器的要求，而是我们自己为了方便而设立的
       mov ecx,0x46
       ;传入希望分配的字节数 ecx，返回分配 4 字节对齐的起始线性地址 ecx
       call sys_routine_seg_sel:allocate_memory
       ;将任务控制块追加到 TCB 链表，ecx 中保存的是本次为创建 TCB 控制块分配的内存首地址
       call append_to_tcb_link

       ;用户程序位于逻辑 50 扇区，压入立即数双字 0x00000032 到栈中
       push dword 50
       ;压入任务控制块起始线性地址
       push ecx
       ;跳转到 load_relocate_program 例程处执行，由于是 32 位相对近调用（被调用的目标过程位于当前代码段内，而非另外一个不同的代码段），所以会把 IP 原有的内容压入栈
       ;load_relocate_program 创建并加载用户程序，同时为用户程序中的数据段、代码段及不同特权级的栈段等创建私有的 LDT 描述符，并为整个 LDT 创建一个系统段，安装到 GDT 中；为用户程序创建 TSS 及其描述符，并把描述符安装到 GDT 中；为用户程序创建 TCB 并填充内容。
       call load_relocate_program

       mov ebx,do_status
       call sys_routine_seg_sel:put_string

       mov eax,mem_0_4_gb_seg_sel
       mov ds,eax

       ;加载任务状态段 TSS 的选择子到 TR 寄存器中
       ltr [ecx+0x18]
       ;加载 LDT 的选择子到 LDTR 寄存器中
       lldt [ecx+0x10]

       ;加载用户程序头部段选择子
       mov eax,[ecx+0x44]
       ;切换到用户程序头部段
       mov ds,eax

       ;以下假装是从调用门返回。模仿处理器压入返回参数
       ;调用前的堆栈段选择子 
       push dword [0x08]
       ;调用前的 esp
       push dword 0

       ;调用前的用户程序头部代码段选择子，特权级为 3
       push dword [0x14]
       ;调用前的 eip，指向用户程序入口点地址 start
       push dword [0x10]

       retf

return_point:
       ;用户程序返回点
       ;因为 c14.asm 是以 JMP 的方式使用调用门 @TerminateProgram，回到这里时，特权级为3，会导致异常
       mov eax,core_data_seg_sel
       mov ds,eax

       mov ebx,message_6
       call sys_routine_seg_sel:put_string

       hlt
            
core_code_end:

SECTION core_trail

core_end:
```