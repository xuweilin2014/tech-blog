# 分页机制和动态页面分配

内核程序的代码如下：

```armasm{.line-numbers}
        ;代码清单16-1
        ;文件名：c16_core.asm
        ;文件说明：保护模式微型核心程序 
        ;创建日期：2012-06-20 00:05

        ;以下常量定义部分。内核的大部分内容都应当固定
        core_code_seg_sel     equ  0x38    ;内核代码段选择子
        core_data_seg_sel     equ  0x30    ;内核数据段选择子 
        sys_routine_seg_sel   equ  0x28    ;系统公共例程代码段的选择子 
        video_ram_seg_sel     equ  0x20    ;视频显示缓冲区的段选择子
        core_stack_seg_sel    equ  0x18    ;内核堆栈段选择子
        mem_0_4_gb_seg_sel    equ  0x08    ;整个0-4GB内存的段的选择子

        ;以下是系统核心的头部，用于加载核心程序
        ;核心程序总长度 #00 
        core_length      dd core_end
        ;系统公用例程段位置 #04
        sys_routine_seg  dd section.sys_routine.start
        ;核心数据段位置 #08
        core_data_seg   dd section.core_data.start
        ;核心代码段位置 #0c
        core_code_seg   dd section.core_code.start
        ;核心代码段入口点 #10
        core_entry      dd start          
                        dw core_code_seg_sel

        [bits 32]

;============================🍸内核公共例程代码段🍸===============================

SECTION sys_routine vstart=0
;字符串显示例程
put_string:                                 ;显示 0 终止的字符串并移动光标 
                                            ;输入：DS:EBX=串地址
         push ecx
  .getc:
         mov cl,[ebx]
         or cl,cl
         jz .exit
         call put_char
         inc ebx
         jmp .getc

  .exit:
         pop ecx
         retf                               ;段间返回

put_char:                                   ;在当前光标处显示一个字符,并推进
                                            ;光标。仅用于段内调用 
                                            ;输入：CL=字符ASCII码 
         pushad

         ;以下取当前光标位置
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         inc dx                             ;0x3d5
         in al,dx                           ;高字
         mov ah,al

         dec dx                             ;0x3d4
         mov al,0x0f
         out dx,al
         inc dx                             ;0x3d5
         in al,dx                           ;低字
         mov bx,ax                          ;BX=代表光标位置的16位数

         cmp cl,0x0d                        ;回车符？
         jnz .put_0a
         mov ax,bx
         mov bl,80
         div bl
         mul bl
         mov bx,ax
         jmp .set_cursor

  .put_0a:
         cmp cl,0x0a                        ;换行符？
         jnz .put_other
         add bx,80
         jmp .roll_screen

  .put_other:                               ;正常显示字符
         push es
         mov eax,video_ram_seg_sel          ;0x800b8000段的选择子
         mov es,eax
         shl bx,1
         mov [es:bx],cl
         pop es

         ;以下将光标位置推进一个字符
         shr bx,1
         inc bx

  .roll_screen:
         cmp bx,2000                        ;光标超出屏幕？滚屏
         jl .set_cursor

         push ds
         push es
         mov eax,video_ram_seg_sel
         mov ds,eax
         mov es,eax
         cld
         mov esi,0xa0                       ;小心！32位模式下movsb/w/d 
         mov edi,0x00                       ;使用的是esi/edi/ecx 
         mov ecx,1920
         rep movsd
         mov bx,3840                        ;清除屏幕最底一行
         mov ecx,80                         ;32位程序应该使用ECX
  .cls:
         mov word[es:bx],0x0720
         add bx,2
         loop .cls

         pop es
         pop ds

         mov bx,1920

  .set_cursor:
         mov dx,0x3d4
         mov al,0x0e
         out dx,al
         inc dx                             ;0x3d5
         mov al,bh
         out dx,al
         dec dx                             ;0x3d4
         mov al,0x0f
         out dx,al
         inc dx                             ;0x3d5
         mov al,bl
         out dx,al

         popad
         
         ret                                

;从硬盘读取一个逻辑扇区
;EAX=逻辑扇区号
;DS:EBX=目标缓冲区地址
;返回：EBX=EBX+512
read_hard_disk_0:                           

         push eax 
         push ecx
         push edx
      
         push eax
         
         mov dx,0x1f2
         mov al,1
         ;读取的扇区数
         out dx,al

         inc dx                             ;0x1f3
         pop eax
         out dx,al                          ;LBA地址7~0

         inc dx                             ;0x1f4
         mov cl,8
         shr eax,cl
         out dx,al                          ;LBA地址15~8

         inc dx                             ;0x1f5
         shr eax,cl
         out dx,al                          ;LBA地址23~16

         inc dx                             ;0x1f6
         shr eax,cl
         or al,0xe0                         ;第一硬盘  LBA地址27~24
         out dx,al

         inc dx                             ;0x1f7
         mov al,0x20                        ;读命令
         out dx,al

  .waits:
         in al,dx
         and al,0x88
         cmp al,0x08
         jnz .waits                         ;不忙，且硬盘已准备好数据传输 

         mov ecx,256                        ;总共要读取的字数
         mov dx,0x1f0
  .readw:
         in ax,dx
         mov [ebx],ax
         add ebx,2
         loop .readw

         pop edx
         pop ecx
         pop eax
      
         retf                               ;段间返回 
      
set_up_gdt_descriptor:                      ;在GDT内安装一个新的描述符
                                            ;输入：EDX:EAX=描述符 
                                            ;输出：CX=描述符的选择子
        push eax
        push ebx
        push edx

        push ds
        push es

        mov ebx,core_data_seg_sel          ;切换到核心数据段
        mov ds,ebx

        sgdt [pgdt]                        ;以便开始处理GDT

        mov ebx,mem_0_4_gb_seg_sel
        mov es,ebx

        movzx ebx,word [pgdt]              ;GDT界限
        inc bx                             ;GDT总字节数，也是下一个描述符偏移
        add ebx,[pgdt+2]                   ;下一个描述符的线性地址

        mov [es:ebx],eax
        mov [es:ebx+4],edx

        add word [pgdt],8                  ;增加一个描述符的大小

        lgdt [pgdt]                        ;对GDT的更改生效

        mov ax,[pgdt]                      ;得到GDT界限值
        xor dx,dx
        mov bx,8
        div bx                             ;除以8，去掉余数
        mov cx,ax
        shl cx,3                           ;将索引号移到正确位置

        pop es
        pop ds

        pop edx
        pop ebx
        pop eax

        retf

make_seg_descriptor:                        ;构造存储器和系统的段描述符
                                            ;输入：EAX=线性基地址
                                            ;      EBX=段界限
                                            ;      ECX=属性。各属性位都在原始
                                            ;          位置，无关的位清零 
                                            ;返回：EDX:EAX=描述符
         mov edx,eax
         shl eax,16
         or ax,bx                           ;描述符前32位(EAX)构造完毕

         and edx,0xffff0000                 ;清除基地址中无关的位
         rol edx,8
         bswap edx                          ;装配基址的31~24和23~16  (80486+)

         xor bx,bx
         or edx,ebx                         ;装配段界限的高4位

         or edx,ecx                         ;装配属性

         retf

make_gate_descriptor:                       ;构造门的描述符（调用门等）
                                            ;输入：EAX=门代码在段内偏移地址
                                            ;       BX=门代码所在段的选择子 
                                            ;       CX=段类型及属性等（各属
                                            ;          性位都在原始位置）
                                            ;返回：EDX:EAX=完整的描述符
        push ebx
        push ecx
    
        mov edx,eax
        and edx,0xffff0000                 ;得到偏移地址高16位 
        or dx,cx                           ;组装属性部分到EDX
    
        and eax,0x0000ffff                 ;得到偏移地址低16位 
        shl ebx,16                          
        or eax,ebx                         ;组装段选择子部分
    
        pop ecx
        pop ebx
    
        retf                                   

;分配一个 4KB 的页
;输入：无
;输出：EAX=页的物理地址                             
allocate_a_4k_page:                         

        push ebx
        push ecx
        push edx
        push ds
        
        mov eax,core_data_seg_sel
        mov ds,eax
        
        xor eax,eax
  .b1:
        ;先将 eax 清零，表示要从 page_bit_map 指明的第 1 个 bit 开始搜索，将索引值为 eax 的比特传送到 CF 标志位，并且将该索引处的比特置 1
        bts [page_bit_map],eax
        ;jmp if not Carry，当 CF=1 时（发生了进位），继续执行；当 CF=0 时（没有进位），不继续执行，CF=0，说明找到了空闲物理页
        jnc .b2
        inc eax
        cmp eax,page_map_len*8
        jl .b1

        mov ebx,message_3
        call sys_routine_seg_sel:put_string
        ;没有可以分配的页，停机
        hlt
         
  .b2:
        ;乘以 4096（0x1000）
        shl eax,12                          
        
        pop ds
        pop edx
        pop ecx
        pop ebx
        
        ret

;分配一个页，并安装在当前活动的层级分页结构中
;输入：EBX=页的线性地址
alloc_inst_a_page:                          

        push eax
        push ebx
        push esi
        push ds
        
        mov eax,mem_0_4_gb_seg_sel
        mov ds,eax
        
        ;检查该线性地址所对应的页表是否存在
        mov esi,ebx
        and esi,0xffc00000
        ;得到页目录索引，乘以 4
        shr esi,20
        ;页目录自身的线性地址+表内偏移，这里又利用了前面页目录索引 FFC 处保存页目录 PDT 的起始物理地址
        or esi,0xfffff000

        ;页目录项的 P 位为 1 表示页表或者页存在于内存中，这可用于检查该线性地址是否已经有对应的页表，注意 [esi] 此时保存的是页表的物理地址
        test dword [esi],0x00000001
        jnz .b1
        
        ;分配一个页做为【页表】，并将此页的物理地址保存到 eax 中
        call allocate_a_4k_page
        ;设置这个页表的属性，RW=1 可读可写，US=1 所有特权级程序都可以访问，P=1 页表位于内存中
        or eax,0x00000007
        ;在页目录中登记该页表物理地址
        mov [esi],eax

  .b1:
        ;在分配了一个页表，并将其物理地址写入到页目录项中后，接下来需要再分配一个物理页，并将其物理地址写入页表项中。这里在处理时，将页表项在页表中的位置，当成了数据在页中的位置
        ;开始访问该线性地址所对应的页表
        mov esi,ebx
        shr esi,10
        ;或者 0xfffff000，因高 10 位是零
        and esi,0x003ff000
        ;得到该页表的线性地址
        or esi,0xffc00000
        
        ;得到该线性地址在页表内的对应条目（页表项） 
        and ebx,0x003ff000
        ;相当于右移 12 位，再乘以 4
        shr ebx,10
        ;页表项的线性地址，线性地址高 10 位为 0x3FF，中间 10 位是页表对应页目录项在页目录中的偏移，低 12 位是页对应页表项在页表中的偏移
        or esi,ebx
        ;分配一个页，这才是要安装的页
        call allocate_a_4k_page
        or eax,0x00000007
        mov [esi],eax
        
        pop ds
        pop esi
        pop ebx
        pop eax
        
        retf  

;创建新页目录，并复制当前页目录内容
;输入：无
;输出：EAX=新页目录的物理地址 
create_copy_cur_pdir:                       

        push ds
        push es
        push esi
        push edi
        push ebx
        push ecx
        
        mov ebx,mem_0_4_gb_seg_sel
        mov ds,ebx
        mov es,ebx
        ;分配一个 4KB 的物理页作为用户程序页目录，并且将页的物理地址保存到 eax 中
        call allocate_a_4k_page
        mov ebx,eax
        ;将页物理地址的低 12 位修改成属性，US=1（特权级别为 3），RW=1（可读可写），P=1（页位于物理内存中）
        or ebx,0x00000007
        mov [0xfffffff8],ebx
        mov [0xffffeffc],ebx
        
        ;ESI->当前页目录的线性地址
        mov esi,0xfffff000
        ;EDI->新页目录的线性地址
        mov edi,0xffffe000
        ;ECX=要复制的目录项数
        mov ecx,1024
        cld
        repe movsd 
        
        pop ecx
        pop ebx
        pop edi
        pop esi
        pop es
        pop ds
        
        retf
         
;终止当前任务，注意，执行此例程时，当前任务仍在运行中。此例程其实也是当前任务的一部分 
terminate_current_task:                     
        mov eax,core_data_seg_sel
        mov ds,eax

        pushfd
        pop edx

        test dx,0100_0000_0000_0000B       ;测试NT位
        jnz .b1                            ;当前任务是嵌套的，到.b1执行iretd 
        jmp far [program_man_tss]          ;程序管理器任务 
  .b1: 
        iretd

sys_routine_end:

;=========================🧀系统核心的数据段🧀=================================
SECTION core_data vstart=0

        ;用于设置和修改 GDT
        pgdt            dw  0              
                        dd  0

        ;page_bit_map 
        page_bit_map    db  0xff,0xff,0x3f,0x00,0x00,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0xff,0xff,0xff,0xff,0xff,0xff,0xff,0xff
                        db  0x55,0x55,0x55,0x55,0x55,0x55,0x55,0x55
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
                        db  0x00,0x00,0x00,0x00,0x00,0x00,0x00,0x00
        page_map_len    equ $-page_bit_map

        ;符号地址检索表
        salt:
        salt_1          db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        dw  sys_routine_seg_sel

        salt_2          db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  sys_routine_seg_sel

        salt_3          db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  sys_routine_seg_sel

        salt_4          db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  terminate_current_task
                        dw  sys_routine_seg_sel

        salt_item_len   equ $-salt_4
        salt_items      equ ($-salt)/salt_item_len

        message_0       db  '  Working in system core,protect mode.'
                        db  0x0d,0x0a,0
        message_1       db  '  Paging is enabled.System core is mapped to'
                        db  ' address 0x80000000.',0x0d,0x0a,0
        message_2       db  0x0d,0x0a
                        db  '  System wide CALL-GATE mounted.',0x0d,0x0a,0
        message_3       db  '********No more pages********',0
        message_4       db  0x0d,0x0a,'  Task switching...@_@',0x0d,0x0a,0
        message_5       db  0x0d,0x0a,'  Processor HALT.',0
        bin_hex         db '0123456789ABCDEF'
        ;内核用的缓冲区
        core_buf        times 512 db 0          
        cpu_brnd0       db 0x0d,0x0a,'  ',0
        cpu_brand       times 52 db 0
        cpu_brnd1       db 0x0d,0x0a,0x0d,0x0a,0
        ;任务控制块链
        tcb_chain       dd  0

        ;内核信息
        ;内核空间中下一个可分配的线性地址，由于内核主体部分占据 0x80000000~0x800FFFFF，所以初始可分配线性地址从 0x80100000 开始
        ;注意，用户地址空间中下一个可以分配的线性地址在 TCB 中保存
        core_next_laddr dd  0x80100000
        ;程序管理器的 TSS 描述符选择子        
        program_man_tss dd  0              
                        dw  0

core_data_end:
               
;==============================🧆系统核心代码段🧆=================================
SECTION core_code vstart=0

;在 LDT 内安装一个新的描述符
;输入：edx:eax=描述符
;         ebx=TCB 基地址
;输出：    cx=描述符的选择子
fill_descriptor_in_ldt:                     

        push eax
        push edx
        push edi
        push ds

        mov ecx,mem_0_4_gb_seg_sel
        mov ds,ecx
        ;获得 LDT 基地址
        mov edi,[ebx+0x0c]
        
        xor ecx,ecx
        ;获得 LDT 界限
        mov cx,[ebx+0x0a]
        ;LDT 的总字节数，即新描述符偏移地址
        inc cx
        
        mov [edi+ecx+0x00],eax
        ;安装描述符
        mov [edi+ecx+0x04],edx

        add cx,8
        ;得到新的 LDT 界限值                            
        dec cx
        ;更新 LDT 界限值到 TCB
        mov [ebx+0x0a],cx

        mov ax,cx
        xor dx,dx
        mov cx,8
        div cx
        
        mov cx,ax
        ;左移 3 位，并且使 TI 位=1，指向 LDT，最后使 RPL=00
        shl cx,3
        or cx,0000_0000_0000_0100B 

        pop ds
        pop edi
        pop edx
        pop eax
    
        ret
      
;加载并重定位用户程序
;输入: PUSH 逻辑扇区号
;      PUSH 任务控制块基地址
;输出：无
load_relocate_program:                      

        pushad
    
        push ds
        push es
        ;为访问通过堆栈传递的参数做准备
        mov ebp,esp                        

        mov ecx,mem_0_4_gb_seg_sel
        mov es,ecx
    
        ;清空当前内核页目录的前半部分（对应低 2GB 的局部地址空间），这里还有另外一个需注意的事项，每次调用 load_relocate_program 都会创建一个用户任务，然后在内核页目录的 0~2GB 范围会增加目录项，然后将该页目录 PDT 复制一份给用户程序。所以每次调用 load_relocate_program 都需要将 0~2GB 范围的页目录清零
        ;注意，一个页目录对应 1024 个页表，每个页表对应 1024 个 4KB 物理页，所以一个页目录对应 4GB 内存
        ;其实此时内核页目录的前半部分只有 0x000 索引处一个页目录项，后面由于内核映射到地址空间高端，在 0x800 处创建一个页目录项，指向同样的页表，所以 0x000 索引处的页目录项可以被清零
        mov ebx,0xfffff000
        xor esi,esi
.b1:
        mov dword [es:ebx+esi*4],0x00000000
        inc esi
        cmp esi,512
        jl .b1
        
        ;以下开始分配内存并加载用户程序
        mov eax,core_data_seg_sel
        ;切换 ds 到内核数据段
        mov ds,eax
        ;从堆栈中取出用户程序起始扇区号，默认段寄存器为 ss
        mov eax,[ebp+12*4]
        ;读取程序头部数据，core_buf 是内核数据段一个缓冲区，长 512 字节（1 个扇区）
        mov ebx,core_buf
        ;eax=逻辑扇区号，ds:ebx=目标缓冲区地址，从逻辑扇区号处读取一个扇区到目标缓冲区
        call sys_routine_seg_sel:read_hard_disk_0

        ;以下判断整个程序有多大
        ;获取用户程序尺寸
        mov eax,[core_buf]
        mov ebx,eax
        ;使之 4KB 对齐
        and ebx,0xfffff000
        add ebx,0x1000     
        ;程序的大小正好是 4KB 的倍数吗?
        test eax,0x00000fff
        ;不是。使用凑整的结果
        cmovnz eax,ebx

        mov ecx,eax
        ;程序占用的总 4KB 页数
        shr ecx,12
        ;切换 ds 段寄存器到 0-4GB 的段
        mov eax,mem_0_4_gb_seg_sel
        mov ds,eax
        ;起始扇区号
        mov eax,[ebp+12*4]
        ;从堆栈中取得 TCB 的基地址->esi
        mov esi,[ebp+11*4]
.b2:
        ;从 TCB 中取得【用户任务】可用的线性地址，在此线性地址处分配一个页，并安装在当前活动的层级分页结构中
        ;首先在【用户任务】虚拟地址空间 ebx 处分配一个 4KB 的页，然后根据 ebx 在页目录中创建页目录项（从线性地址 0x00000000 开始），并且创建页表（之前将 PDT 中 0~2GB 范围的页目录项清零）和物理页
        mov ebx,[es:esi+0x06]
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page
        ;分页机制下，是先登记再使用，前面对页目录和页表进行了登记，现在将用户程序写入到内存中，这里根据线性地址写入用户程序，然后线性地址被转换成真实物理地址，然后用户程序被写入真正物理内存中
        push ecx
        mov ecx,8
.b3:
        ;read_hard_disk_0 只读取一个扇区 512 字节的内容到 ds:ebx 目标缓冲区中，需要循环读取 8 次才能读完一个页
        call sys_routine_seg_sel:read_hard_disk_0
        inc eax
        loop .b3

        pop ecx
        loop .b2

        ;在内核地址空间内创建用户任务的 TSS
        mov eax,core_data_seg_sel
        ;切换 ds 到内核数据段
        mov ds,eax
        ;用户任务的 TSS 必须在全局空间上分配
        mov ebx,[core_next_laddr] 
        call sys_routine_seg_sel:alloc_inst_a_page
        add dword [core_next_laddr],4096
        ;在用户程序 TCB 中填写用户 TSS 的线性地址
        mov [es:esi+0x14],ebx
        ;在用户程序 TCB 中填写用户 TSS 的界限值
        mov word [es:esi+0x12],103
        
        ;在用户任务的局部地址空间内创建 LDT
        ;从 TCB 中取得可用的线性地址
        mov ebx,[es:esi+0x06]
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page
        ;填写 LDT 线性地址到 TCB 中 
        mov [es:esi+0x0c],ebx

        ;建立程序代码段描述符
        mov eax,0x00000000
        mov ebx,0x000fffff
        ;4KB 粒度的代码段描述符，特权级 3，寻址范围为 0~4GB
        mov ecx,0x00c0f800
        ;创建程序代码段的描述符，并安装到 LDT 中
        call sys_routine_seg_sel:make_seg_descriptor
        ;TCB 的基地址
        mov ebx,esi
        call fill_descriptor_in_ldt
        ;设置选择子的特权级为 3
        or cx,0000_0000_0000_0011B
        ;从 TCB 中获取 TSS 的线性地址
        mov ebx,[es:esi+0x14]
        ;填写 TSS 的 CS 域
        mov [es:ebx+76],cx 

        ;建立程序数据段描述符
        mov eax,0x00000000
        mov ebx,0x000fffff                 
        ;4KB 粒度的数据段描述符，特权级 3
        mov ecx,0x00c0f200
        ;生成成都数据段描述符，并安装到 LDT 中
        call sys_routine_seg_sel:make_seg_descriptor
        mov ebx,esi
        call fill_descriptor_in_ldt
        ;设置选择子的特权级为 3
        or cx,0000_0000_0000_0011B
        
        ;从 TCB 中获取 TSS 的线性地址
        mov ebx,[es:esi+0x14]
        ;在平坦模型下，段寄存器 ds，es，fs 和 gs 都指向同一个 4GB 的数据段
        ;填写 TSS 的 DS 域
        mov [es:ebx+84],cx
        ;填写 TSS 的 ES 域
        mov [es:ebx+72],cx
        ;填写 TSS 的 FS 域
        mov [es:ebx+88],cx
        ;填写 TSS 的 GS 域
        mov [es:ebx+92],cx
        
        ;将数据段作为用户任务的 3 特权级固有堆栈
        ;从 TCB 中取得可用的线性地址
        mov ebx,[es:esi+0x06]
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page
        ;从 TCB 中获取 TSS 的线性地址
        mov ebx,[es:esi+0x14]
        ;填写 TSS 的 SS 域
        mov [es:ebx+80],cx
        ;堆栈的高端线性地址 
        mov edx,[es:esi+0x06]
        ;填写 TSS 的 ESP 域
        mov [es:ebx+56],edx

        ;在用户任务的局部地址空间内创建 0 特权级堆栈
        mov ebx,[es:esi+0x06]
        ;从 TCB 中取得可用的线性地址
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page

        mov eax,0x00000000
        mov ebx,0x000fffff
        mov ecx,0x00c09200
        ;4KB 粒度的堆栈段描述符，特权级 0
        call sys_routine_seg_sel:make_seg_descriptor
        mov ebx,esi
        call fill_descriptor_in_ldt
        ;设置选择子的特权级为 0
        or cx,0000_0000_0000_0000B
        ;从 TCB 中获取 TSS 的线性地址，填写 TSS 的 SS0 域，堆栈的高端线性地址填写 TSS 的 ESP0 域
        mov ebx,[es:esi+0x14]
        mov [es:ebx+8],cx
        mov edx,[es:esi+0x06]
        mov [es:ebx+4],edx

        ;在用户任务的局部地址空间内创建 1 特权级堆栈
        ;从 TCB 中取得可用的线性地址
        mov ebx,[es:esi+0x06]
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page

        mov eax,0x00000000
        mov ebx,0x000fffff
        mov ecx,0x00c0b200
        ;4KB 粒度的堆栈段描述符，特权级 1
        call sys_routine_seg_sel:make_seg_descriptor
        mov ebx,esi
        call fill_descriptor_in_ldt
        ;设置选择子的特权级为 1
        or cx,0000_0000_0000_0001B
        ;从 TCB 中获取 TSS 的线性地址，填写 TSS 的 SS1 域，堆栈的高端线性地址填写 TSS 的 ESP1 域
        mov ebx,[es:esi+0x14]
        mov [es:ebx+16],cx
        mov edx,[es:esi+0x06]
        mov [es:ebx+12],edx

        ;在用户任务的局部地址空间内创建 2 特权级堆栈
        ;从 TCB 中取得可用的线性地址
        mov ebx,[es:esi+0x06]
        add dword [es:esi+0x06],0x1000
        call sys_routine_seg_sel:alloc_inst_a_page

        mov eax,0x00000000
        mov ebx,0x000fffff
        mov ecx,0x00c0d200
        ;4KB 粒度的堆栈段描述符，特权级 2
        call sys_routine_seg_sel:make_seg_descriptor
        mov ebx,esi
        call fill_descriptor_in_ldt
        ;设置选择子的特权级为 2
        or cx,0000_0000_0000_0010B
        ;从 TCB 中获取 TSS 的线性地址，填写 TSS 的 SS2 域，堆栈的高端线性地址填写 TSS 的 ESP2 域
        mov ebx,[es:esi+0x14]
        mov [es:ebx+24],cx
        mov edx,[es:esi+0x06]
        mov [es:ebx+20],edx

        ;重定位 SALT
        ;访问任务的 4GB 虚拟地址空间时用
        mov eax,mem_0_4_gb_seg_sel 
        mov es,eax
        mov eax,core_data_seg_sel
        mov ds,eax
        cld
        ;由于将用户程序加载到线性地址 0x00000000 处，所以可以直接通过 es+偏移量的方式来获取 U-SALT 条目数和偏移量
        ;U-SALT 条目数
        mov ecx,[es:0x0c]
        ;U-SALT 在 4GB 空间内的偏移，es:edi
        mov edi,[es:0x08]
.b4:
        push ecx
        push edi
        ;K-SALT 条目数
        mov ecx,salt_items
        ;K-SALT 在内核数据段内的偏移，ds:esi
        mov esi,salt
.b5:
        push edi
        push esi
        push ecx
        ;检索表中，每条目的比较次数
        mov ecx,64
        ;每次比较 4 字节
        repe cmpsd
        jnz .b6
        ;若匹配，则 esi 恰好指向其后的地址，即内核例程的偏移地址
        mov eax,[esi]
        ;将用户程序 U-SALT 表中字符串改写成偏移地址
        mov [es:edi-256],eax
        ;获取内核例程的调用门描述符选择子
        mov ax,[esi+4]
        ;以用户程序自己的特权级使用调用门故 RPL=3
        or ax,0000000000000011B
        ;回填调用门选择子
        mov [es:edi-252],ax
.b6:
        pop ecx
        pop esi
        ;接着比较 K-SALT 表中下一个条目
        add esi,salt_item_len
        ;从头比较
        pop edi
        loop .b5
    
        pop edi
        ;接着比较 U-SALT 表中下一个条目
        add edi,256
        pop ecx
        loop .b4

        ;在 GDT 中登记 LDT 描述符
        ;从堆栈中取得 TCB 的基地址
        mov esi,[ebp+11*4]
        ;分别从 TCB 中获取 LDT 线性地址和段界限
        mov eax,[es:esi+0x0c]
        movzx ebx,word [es:esi+0x0a]
        ;生成 LDT 描述符，特权级 0，并将其安装到 GDT 中
        mov ecx,0x00408200
        call sys_routine_seg_sel:make_seg_descriptor
        call sys_routine_seg_sel:set_up_gdt_descriptor
        ;登记 LDT 选择子到 TCB 中
        mov [es:esi+0x10],cx

        ;从用户任务的 TCB 中获取用户程序 TSS 的线性地址
        mov ebx,[es:esi+0x14]
        ;填写 TSS 的 LDT 域，写入 LDT 选择子
        mov [es:ebx+96],cx
        ;反向链=0
        mov word [es:ebx+0],0
        ;从 TCB 中获取 TSS 段长度（界限）
        mov dx,[es:esi+0x12]
        ;填写 TSS 的 I/O 位图偏移域
        mov [es:ebx+102],dx 
        ;T=0
        mov word [es:ebx+100],0
        ;从任务的 4GB 地址空间获取入口点
        mov eax,[es:0x04]  
        ;TSS 的 EIP 域填写的是用户程序入口点，从内核任务切换到用户任务使用的是 TSS 来恢复现场
        mov [es:ebx+32],eax

        pushfd
        pop edx
        ;填写 TSS 的 EFLAGS 域
        mov [es:ebx+36],edx

        ;在 GDT 中登记 TSS 描述符
        ;从 TCB 中获取 TSS 的起始线性地址和段长度（界限），并生成 TSS 描述符，然后安装到 GDT 中
        mov eax,[es:esi+0x14]
        movzx ebx,word [es:esi+0x12]
        mov ecx,0x00408900
        call sys_routine_seg_sel:make_seg_descriptor
        call sys_routine_seg_sel:set_up_gdt_descriptor
        ;登记 TSS 选择子到 TCB
        mov [es:esi+0x18],cx

        ;创建用户任务的页目录
        ;注意！页的分配和使用是由页位图决定的，可以不占用线性地址空间，将当前内核使用的页目录复制一份，并且将用户程序 PDT 的最后一个页目录项修改为指向当前 PDT 首地址
        call sys_routine_seg_sel:create_copy_cur_pdir
        ;从 TCB 中获取 TSS 的线性地址
        mov ebx,[es:esi+0x14]
        ;将给用户程序新分配的页目录基址填写 TSS 的 CR3(PDBR) 域
        mov dword [es:ebx+28],eax
        ;恢复到调用此过程前的 es 段  
        pop es
        ;恢复到调用此过程前的 ds 段
        pop ds

        popad
        ;丢弃调用本过程前压入的参数
        ret 8 
      
;在 TCB 链上追加任务控制块
;输入：ECX=TCB 线性基地址
append_to_tcb_link:                         

        push eax
        push edx
        push ds
        push es
        
        ;令 DS 指向内核数据段
        mov eax,core_data_seg_sel          
        mov ds,eax
        ;令 ES 指向 0..4GB 段
        mov eax,mem_0_4_gb_seg_sel
        mov es,eax
        ;当前 TCB 指针域清零，以指示这是最后一个 TCB
        mov dword [es: ecx+0x00],0         
        ;TCB 表头指针                 
        mov eax,[tcb_chain]
        ;链表为空？
        or eax,eax
        jz .notcb 
         
.searc:
        mov edx,eax
        mov eax,[es: edx+0x00]
        or eax,eax               
        jnz .searc
        
        mov [es: edx+0x00],ecx
        jmp .retpc
         
.notcb:       
        ;若为空表，直接令表头指针指向 TCB
        mov [tcb_chain],ecx
         
.retpc:
        pop es
        pop ds
        pop edx
        pop eax
        
        ret
         
start:
        ;令 ds 指向内核数据段
        mov ecx,core_data_seg_sel           
        mov ds,ecx
        ;令 es 指向内存 0...4GB 数据段
        mov ecx,mem_0_4_gb_seg_sel          
        mov es,ecx

        mov ebx,message_0                    
        call sys_routine_seg_sel:put_string
                                        
        ;显示处理器品牌信息 
        mov eax,0x80000002
        cpuid
        mov [cpu_brand + 0x00],eax
        mov [cpu_brand + 0x04],ebx
        mov [cpu_brand + 0x08],ecx
        mov [cpu_brand + 0x0c],edx
    
        mov eax,0x80000003
        cpuid
        mov [cpu_brand + 0x10],eax
        mov [cpu_brand + 0x14],ebx
        mov [cpu_brand + 0x18],ecx
        mov [cpu_brand + 0x1c],edx

        mov eax,0x80000004
        cpuid
        mov [cpu_brand + 0x20],eax
        mov [cpu_brand + 0x24],ebx
        mov [cpu_brand + 0x28],ecx
        mov [cpu_brand + 0x2c],edx

        ;显示处理器品牌信息
        mov ebx,cpu_brnd0                  
        call sys_routine_seg_sel:put_string
        mov ebx,cpu_brand
        call sys_routine_seg_sel:put_string
        mov ebx,cpu_brnd1
        call sys_routine_seg_sel:put_string

        ;准备打开分页机制
        ;创建系统内核的页目录 PDT，并将页目录清零，页目录中有 1024 个目录项，全部清零，主要是为了将所有目录项的 P 位设置为 0，表明该页表不在内存中
        mov ecx,1024
        ;页目录的物理地址，此时还没有进行分页，线性地址=物理地址
        mov ebx,0x00020000
        xor esi,esi
.b1:
        ;页目录表项清零
        mov dword [es:ebx+esi],0x00000000   
        add esi,4
        loop .b1
        
        ;在页目录最后一个目录项内创建指向页目录自己起始物理地址 0x00020003 的目录项，页目录项和页表项中只保存页物理地址的高 20 位，低 12 位默认全部为 0
        ;接下来会将内核各段映射到地址空间中的高位（2~4GB），因此需要为内核段在 PDT 中创建一个新的目录项，对应线性地址范围 0x80000000~0x800FFFFF
        mov dword [es:ebx+4092],0x00020003

        ;修改第 1 个页目录项的内容，使其指向页表基址，页表的物理地址为 0x00021000，此页表在内存中（P=1），并且可读可写（RW=1）
        mov dword [es:ebx+0],0x00021003

        ;创建与上面那个目录项相对应的页表，此页表有 256 个页表项，表示线性地址范围 0x00000000~0x000FFFFF，同时也对应内存物理地址范围 0x00000000~0x000FFFFF
        ;第 1 个页表的物理地址
        mov ebx,0x00021000
        ;起始页的物理地址
        xor eax,eax
        xor esi,esi
.b2:
        mov edx,eax
        ;设置页表项的属性，可读可写，页位于内存中
        or edx,0x00000003
        ;登记页的物理地址到页表中
        mov [es:ebx+esi*4],edx
        ;下一个相邻页的物理地址，增加 4KB
        add eax,0x1000 
        inc esi
        ;仅低端 1MB 内存对应的页才是有效的
        cmp esi,256 
        jl .b2
.b3:
        ;当前页表第 256~1024 页表项的值全部设置为 0（即全部置为无效）
        mov dword [es:ebx+esi*4],0x00000000
        inc esi
        cmp esi,1024
        jl .b3

        ;下面将页目录 PDT 的基地址赋值给 CR3 寄存器，即页目录表基址寄存器 PDBR，PCD=PWT=0
        mov eax,0x00020000
        mov cr3,eax
        ;将 CR0 控制寄存器的最高位 31 位设置为 1，表示开启分页功能
        mov eax,cr0
        or eax,0x80000000
        mov cr0,eax

        ;在页目录内创建与线性地址 0x80000000 对应的目录项，目录项的值为 0x00021003，使得两个目录项都映射到内存中相同的物理页，此目录项对应的线性地址范围为 0x80000000~0x800FFFFF
        ;当线性地址的高 20 位为 0xFFFFF 时，访问的就是页目录表自己，页目录被作为页表，也被作为物理页
        ;由于线性地址 0x80000000 实际访问的是（或者说实际对应的是）页目录表中 0x200*4=0x800 偏移处的页目录项，因此将线性地址 esi 的值右移 22 位就得到线性地址的高 10 位，再将它左移 2 位，相当于乘以 4
        mov ebx,0xfffff000
        mov esi,0x80000000
        shr esi,22
        shl esi,2
        ;写入目录项（页表的物理地址和属性），此段部件发出的目标单元线性地址为 0xFFFFF800
        mov dword [es:ebx+esi],0x00021003

        ;只是增加了目录项没有用，还需要将内核代码段映射到 0x80000000 以上，由于内核各段的地址实际上是在 GDT 的段描述符中指明的，所以需要将 GDT 中的段描述符映射到线性地址 0x80000000 以上，也包括 GDT 起始地址本身
        ;将 GDT 中的基地址和界限保存到数据段 pgdt 标号处，并将基地址移动到 ebx 寄存器
        sgdt [pgdt]
        mov ebx,[pgdt+2]
        ;ebx 提供了 GDT 的基地址，0x10、0x18 等提供了描述符在 GDT 中的偏移量，加 4 就是每个描述符的高 32 位
        or dword [es:ebx+0x10+4],0x80000000
        or dword [es:ebx+0x18+4],0x80000000
        or dword [es:ebx+0x20+4],0x80000000
        or dword [es:ebx+0x28+4],0x80000000
        or dword [es:ebx+0x30+4],0x80000000
        or dword [es:ebx+0x38+4],0x80000000
        ;GDTR 也用的是线性地址，将 GDT 的基地址也映射到内存的高端，即加上 0x80000000
        add dword [pgdt+2],0x80000000

        lgdt [pgdt]
        ;刷新段寄存器 CS，从 GDT 中获取新的段描述符更新段寄存器的缓存，启用高端线性地址，实际就是跳转到新映射的内核代码段执行
        jmp core_code_seg_sel:flush

flush:
        ;使用段选择子重新设置段寄存器 ss 和 ds 的值，导致处理器访问 GDT，并刷新 ss 和 ds 段寄存器描述符高速缓存的内容
        mov eax,core_stack_seg_sel
        mov ss,eax
        mov eax,core_data_seg_sel
        mov ds,eax
        
        mov ebx,message_1
        call sys_routine_seg_sel:put_string

        ;以下开始安装为整个系统服务的调用门描述符到 GDT 中。特权级之间的控制转移必须使用门
        ;K-SALT 表的起始位置
        mov edi,salt
        ;K-SALT 表的条目数量
        mov ecx,salt_items
.b4:
        push ecx   
        ;该条目入口点的 32 位偏移地址
        mov eax,[edi+256]
        ;该条目入口点的段选择子 
        mov bx,[edi+260]
        ;特权级 3 的调用门 (3 以上的特权级才允许访问)，0 个参数 (因为用寄存器传递参数，而没有用栈)
        ;当前特权级 CPL 和请求特权级 RPL 高于或者和调用门描述符特权级 DPL 相同才能访问此调用门
        mov cx,1_11_0_1100_000_00000B

        ;生成调用门描述符，并且安装到 GDT 中
        call sys_routine_seg_sel:make_gate_descriptor
        call sys_routine_seg_sel:set_up_gdt_descriptor
        ;将返回的门描述符选择子回填
        mov [edi+260],cx
        ;指向下一个 K-SALT 条目
        add edi,salt_item_len
        pop ecx
        loop .b4

        ;对门进行测试
        mov ebx,message_2
        ;通过门显示信息 (偏移量将被忽略)
        call far [salt_1+256]

        ;为程序管理器的 TSS 分配一个页大小的内存空间，页的起始线性地址位于 core_next_laddr
        mov ebx,[core_next_laddr]
        ;为 TSS 创建一个物理页，并将此物理页的【物理地址】写入到页表中；如果没有页表，需要创建页表，并将页表【物理地址】写入到页目录中
        call sys_routine_seg_sel:alloc_inst_a_page
        add dword [core_next_laddr],4096

        ;在程序管理器的 TSS 中设置必要的项目
        ;反向链=0
        mov word [es:ebx+0],0

        mov eax,cr3
        ;登记 CR3 (PDBR)，即登记内核所使用的页目录基址到 TSS 中
        mov dword [es:ebx+28],eax
        ;没有 LDT。处理器允许没有 LDT 的任务
        mov word [es:ebx+96],0
        ;T=0
        mov word [es:ebx+100],0
        ;没有 I/O 位图。0 特权级事实上不需要，所以可以对其赋值大于等于 TSS 界限的值（界限为 103）
        mov word [es:ebx+102],103
        
        ;创建程序管理器的 TSS 描述符，并安装到 GDT 中
        ;TSS 的起始线性地址
        mov eax,ebx
        ;段长度（界限）
        mov ebx,103
        ;TSS 描述符，DPL 特权级 0，P=1，TYPE=1001 表示 TSS 描述符
        mov ecx,0x00408900
        ;生成 TSS 描述符并安装到 GDT 中
        call sys_routine_seg_sel:make_seg_descriptor
        call sys_routine_seg_sel:set_up_gdt_descriptor
        ;保存程序管理器的 TSS 描述符选择子 
        mov [program_man_tss+4],cx

        ;任务寄存器 TR 中的内容是任务存在的标志，该内容也决定了当前任务是谁。
        ;下面的指令为当前正在执行的 0 特权级任务"程序管理器"后补手续（TSS）。
        ltr cx

        ;现在可认为"程序管理器"任务正执行中
        ;创建【用户任务】的 TCB，为 TCB 创建一个页，并将页的物理地址写入到页表项中，同样如果 TCB 线性地址没有对应的页表，也需要先分配内存创建页表，并将页表的物理地址写入到页目录项中
        mov ebx,[core_next_laddr]
        call sys_routine_seg_sel:alloc_inst_a_page
        add dword [core_next_laddr],4096
        
        ;用户任务局部空间的分配从 0 开始
        mov dword [es:ebx+0x06],0
        ;登记 LDT 初始的界限到 TCB 中
        mov word [es:ebx+0x0a],0xffff
        mov ecx,ebx
        ;将此 TCB 添加到 TCB 链中，但是该 TCB 链的作用在后面进行抢占式任务切换时才能显现出来
        call append_to_tcb_link 
        ;用户程序位于逻辑 50 扇区
        push dword 50
        ;压入任务控制块起始线性地址
        push ecx

        call load_relocate_program         

        mov ebx,message_4
        call sys_routine_seg_sel:put_string
        ;执行任务切换
        call far [es:ecx+0x14]

        mov ebx,message_5
        call sys_routine_seg_sel:put_string
        hlt
            
core_code_end:

SECTION core_trail
core_end:
```

用户程序的代码如下所示：

```armasm{.line-numbers}
        ;代码清单16-2
        ;文件名：c16.asm
        ;文件说明：用户程序 
        ;创建日期：2012-05-25 13:53   

        ;程序总长度 #0x00
        program_length     dd program_end
        ;程序入口点 #0x04
        entry_point        dd start
        ;SALT 表起始偏移量 #0x08
        ;由于用户程序看作是一整个段，并且用户程序被加载到虚拟地址空间起始处，所以代表的是在 4GB 空间中的偏移
        salt_position      dd salt_begin 
        ;SALT 条目数 #0x0C
        salt_items         dd (salt_end-salt_begin)/256

        ;符号地址检索表
        salt_begin:                                     
        PrintString        db  '@PrintString'
                           times 256-($-PrintString) db 0
        TerminateProgram   db  '@TerminateProgram'
                           times 256-($-TerminateProgram) db 0
        ;保留一个空白区，以演示分页
        reserved           times 3*4096 db 0
        ReadDiskData       db  '@ReadDiskData'
                           times 256-($-ReadDiskData) db 0
        PrintDwordAsHex    db  '@PrintDwordAsHexString'
                           times 256-($-PrintDwordAsHex) db 0
        salt_end:
        ;要显示的数据信息
        message_0          db  0x0d,0x0a,
                           db  '  ............User task is running with '
                           db  'paging enabled!............',0x0d,0x0a,0
        space              db  0x20,0x20,0
         
        [bits 32]
start:
        mov ebx,message_0
        call far [PrintString]
        
        xor esi,esi
        mov ecx,88
  .b1:
        mov ebx,space
        call far [PrintString] 
        mov edx,[esi*4]
        call far [PrintDwordAsHex]
        
        inc esi
        loop .b1 
        ;退出，并将控制权返回到核心
        call far [TerminateProgram] 
    
program_end:
```