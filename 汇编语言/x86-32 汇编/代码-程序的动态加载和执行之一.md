# 程序的动态加载和执行之一

## 一、MBR 主引导程序代码

MBR 主引导程序首先将 0~4GB 数据段描述符、MBR 代码段描述符、内核堆栈段描述符、文本模式显存段描述符这 5 个描述符安装到 GDT 表中。然后将整个内核程序读取到内存中（起始地址为 0x00040000），从而获取内核程序内部的分段结构，包括内核公用例程段、内核数据段、内核代码段，并分别为其创建段描述符，安装到 GDT 中，有了这些段描述符作为基础，MBR 接下来跳转到 内核程序。

```armasm{.line-numbers}
       ;代码清单13-1
       ;文件名：c13_mbr.asm
       ;文件说明：硬盘主引导扇区代码 
       ;创建日期：2011-10-28 22:35         
       
       ;设置堆栈段和栈指针
       ;常数，内核加载的起始内存地址
       core_base_address equ 0x00040000   
       ;常数，内核的起始逻辑扇区号 
       core_start_sector equ 0x00000001  
       
       ;初始化栈的初始地址为 0x7C00，cs 寄存器的值为 0
       mov ax,cs
       mov ss,ax
       mov sp,0x7c00

       ;计算 GDT 所在的逻辑段地址
       ;GDT 的 32 位物理地址，即 0x7e00
       mov eax,[cs:pgdt+0x7c00+0x02] 
       xor edx,edx
       mov ebx,16
       ;分解成 16 位逻辑地址，商（逻辑段地址）在 eax 中，余数（偏移地址）在 edx 中
       div ebx

       ;令 ds 指向 GDT 所在逻辑段地址以进行后续操作
       mov ds,eax
       ;ebx 指向 GDT 段的偏移地址
       mov ebx,edx

       ;跳过 0# 号描述符的槽位，以下为 MBR 在 GDT 中为内核安装 4 个描述符
       ;创建 1# 描述符，这是一个数据段，对应 0~4GB 的线性地址空间
       ;基地址为 0，段界限为 0xFFFFF
       mov dword [ebx+0x08],0x0000ffff
       ;粒度为 4KB，存储器段描述符（读写权限）
       mov dword [ebx+0x0c],0x00cf9200     

       ;创建 2# 描述符，这是即主引导程序（MBR）代码的描述符
       ;基地址为 0x00007c00，界限 0x1FF
       mov dword [ebx+0x10],0x7c0001ff
       ;粒度为 1 个字节，代码段描述符
       mov dword [ebx+0x14],0x00409800  

       ;创建 3# 描述符，即为内核创建一个堆栈段的描述符
       ;基地址为 0x00007C00，界限 0xFFFFE 
       mov dword [ebx+0x18],0x7c00fffe
       ;粒度为 4KB，此内核堆栈的区域范围为 0x6C00~0x7BFF
       mov dword [ebx+0x1c],0x00cf9600
       
       ;创建 4# 描述符，即建立保护模式下的显示缓冲区描述符
       ;基地址为 0x000B8000，界限 0x07FFF 
       mov dword [ebx+0x20],0x80007fff
       ;粒度为字节 
       mov dword [ebx+0x24],0x0040920b
       
       ;初始化描述符表寄存器 GDTR
       ;描述符表的界限为 39（从 0 字节开始计算偏移量）
       mov word [cs:pgdt+0x7c00],39
       lgdt [cs:pgdt+0x7c00]

       ;南桥芯片内的端口，在进入保护模式之前，一定要打开 A20 线路，从而可以访问更高的地址区间
       in al,0x92                          
       or al,0000_0010B
       ;打开 A20
       out 0x92,al                        
       ;保护模式和实模式下的中断处理机制不同，因此这里禁止可屏蔽中断的处理
       cli                                

       mov eax,cr0
       or eax,1
       ;设置 PE 位，正式进入保护模式
       mov cr0,eax

       ;16 位的描述符选择子:32 位偏移
       ;清流水线并串行化处理器
       jmp dword 0x0010:flush
       [bits 32]

flush:                                  
       ;加载数据段 (0..4GB) 选择子，可以访问所有的物理内存地址
       mov eax,0x0008
       mov ds,eax

       ;加载堆栈段选择子
       mov eax,0x0018
       mov ss,eax
       ;堆栈指针 esp 初始化为 0
       xor esp,esp
       
       ;以下加载内核程序
       ;获取内核程序在物理内存的起始地址 0x00040000
       mov edi,core_base_address
       ;获取内核程序在磁盘上的起始逻辑扇区号 0x00000001
       mov eax,core_start_sector
       mov ebx,edi
       ;以下读取程序的起始部分（一个扇区），读取 eax 指明的扇区到 ds:ebx 指定的内存区域
       call read_hard_disk_0

       ;以下判断内核程序有多大
       ;在内核程序的第 1~4 个字节是内核程序尺寸
       mov eax,[edi]
       xor edx,edx
       ;512 字节每扇区
       mov ecx,512    
       ;扇区数量在 eax 中，余数（不到一个扇区的字节数）在 edx 中
       div ecx

       or edx,edx
       ;如果 edx 不为 0，且 eax 不为 0，说明应该读取的扇区总数为 eax + 1，但是已经读取了程序头的一个扇区，因此还应读取 eax 个扇区
       ;如果 edx 不为 0，且 eax 为 0，说明应该读取的扇区总数为 1，之前已经读取，直接跳到 setup
       jnz @1
       ;如果 edx 为 0，且 eax 不为 0，那么应该读取的扇区总数为 eax，已经读了一个扇区，还应该读取 eax - 1 个扇区
       dec eax
@1:
       ;考虑实际长度 ≤512 个字节的情况
       or eax,eax
       ;EAX=0 ? 
       jz setup

       ;读取剩余的扇区
       ;32 位模式下的 LOOP 使用 ECX
       mov ecx,eax
       mov eax,core_start_sector
       ;从下一个逻辑扇区接着读
       inc eax
@2:
       call read_hard_disk_0
       inc eax
       ;循环读，直到读完整个内核
       loop @2

setup:
       ;不可以在代码段内寻址 pgdt，但可以通过 4GB 的数据段来访问
       ;esi 为 GDT 的物理基址
       mov esi,[0x7c00+pgdt+0x02]

       ;内核程序按照 系统公用例程段 -> 核心数据段 -> 核心代码段 进行排列
       ;建立公用例程段描述符
       ;内核公用例程代码段起始汇编地址
       mov eax,[edi+0x04]                 
       ;内核数据段汇编地址
       mov ebx,[edi+0x08]                 
       sub ebx,eax
       ;内核公用例程段界限
       dec ebx                
       ;内核公用例程段基地址 
       add eax,edi
       ;字节粒度的代码段描述符
       mov ecx,0x00409800
       ;eax=线性基地址、ebx=段界限、ecx=属性、返回 edx:eax=完整的描述符
       call make_gdt_descriptor
       mov [esi+0x28],eax
       mov [esi+0x2c],edx

       ;建立核心数据段描述符
       ;核心数据段起始汇编地址
       mov eax,[edi+0x08]                 
       ;核心代码段汇编地址
       mov ebx,[edi+0x0c]                  
       sub ebx,eax
       ;核心数据段界限
       dec ebx                            
       ;核心数据段基地址
       add eax,edi                        
       ;字节粒度的数据段描述符
       mov ecx,0x00409200                  
       call make_gdt_descriptor
       mov [esi+0x30],eax
       mov [esi+0x34],edx 

       ;建立核心代码段描述符
       ;核心代码段起始汇编地址
       mov eax,[edi+0x0c]                 
       ;程序总长度
       mov ebx,[edi+0x00]                 
       sub ebx,eax
       ;核心代码段界限
       dec ebx                            
       ;核心代码段基地址
       add eax,edi                        
       ;字节粒度的代码段描述符
       mov ecx,0x00409800                 
       call make_gdt_descriptor
       mov [esi+0x38],eax
       mov [esi+0x3c],edx

       ;描述符表的界限
       mov word [0x7c00+pgdt],63
       ;将 GDT 信息写到 GDTR 寄存器中                                   
       lgdt [0x7c00+pgdt]
       ;跳转到内核程序中开始执行
       jmp far [edi+0x10]
       

;从硬盘读取一个逻辑扇区
;eax=逻辑扇区号
;ds:ebx=目标缓冲区地址
;返回：ebx=ebx+512
read_hard_disk_0:                        

       push eax 
       push ecx
       push edx

       ;al 寄存器为 eax 的最低 8 位，因此下面的 mov al,1 会改变 eax 寄存器的值
       ;需要先 push 保存 eax 寄存器的值
       push eax
       ;设置要从磁盘中读取的逻辑扇区数量，这个数值要写入 0x1f2 端口
       mov dx,0x1f2
       mov al,1
       ;设置读取的扇区数
       out dx,al                       

       ;设置起始 LBA 扇区号，此扇区号保存在 eax 寄存器中
       ;需要将 LBA 扇区号分成 4 段，分别写入端口 0x1f3、0x1f4、0x1f5 和 0x1f6 端口
       inc dx                        
       pop eax
       ;LBA 地址 7~0
       out dx,al                       

       ;将 LBA 地址 15~8 号写到 0x1f4 端口中
       inc dx                          
       mov cl,8
       shr eax,cl
       out dx,al

       ;将 LBA 地址 23~16 号写到 0x1f5 端口中
       inc dx                          
       shr eax,cl
       out dx,al                       

       ;0x1f6
       inc dx                          
       shr eax,cl
       ;0x1f6 端口的低 4 位用于存放逻辑扇区号的 24～27 位
       ;第 4 位用于指示硬盘号，0 表示主盘，1 表示从盘。高 3 位是“111”，表示 LBA 模式
       or al,0xe0                     
       out dx,al

       ;向端口 0x1f7 写入 0x20，请求硬盘读
       inc dx                          
       mov al,0x20
       out dx,al

.waits:
       ;等待读写操作完成。端口 0x1f7 既是命令端口，又是状态端口。
       ;在通过这个端口发送读写命令之后，硬盘就开始读取数据，它将 0x1f7 端口的第 7 位置 1，表明自己很忙
       ;一旦硬盘系统准备就绪，它再将此位清零，同时将第 3 位置 1，意思是准备好了，请求主机发送或者接收数据
       in al,dx
       and al,0x88
       cmp al,0x08
       ;不忙，且硬盘已准备好数据传输
       jnz .waits

       ;0x1f0 是硬盘接口的数据端口，而且还是一个 16 位端口。一旦硬盘准备就绪，就可以连续从这个端口写入或者读取数据
       ;总共要读取的字数
       mov ecx,256
       mov dx,0x1f0
.readw:
       in ax,dx
       mov [ebx],ax
       add ebx,2
       loop .readw

       pop edx
       pop ecx
       pop eax

       ret

;构造描述符
;输入：EAX=线性基地址
;      EBX=段界限
;      ECX=属性（各属性位都在原始位置，其它没用到的位置0） 
;返回：EDX:EAX=完整的描述符
make_gdt_descriptor:   

       mov edx,eax
       ;将 eax 中低 16 位的线性基地址左移 16 位，并且左移过程中右边空出的位用 0 填充
       shl eax,16                     
       ;描述符前 32 位 (EAX) 构造完毕
       or ax,bx                        

       ;清除基地址中无关的位
       and edx,0xffff0000              
       rol edx,8
       ;装配基址的 31~24 和 23~16 (80486+)
       ;0x0123456789abcdef ==> 0xefcdab8967452301
       ;0x01234567 ==> 0x67452301
       bswap edx                       

       and ebx 0x000f0000
       ;装配段界限的高 4 位
       or edx,ebx                      

       ;装配属性
       or edx,ecx                       
       ret

                        ;GDT 的界限值    
       pgdt             dw 0
                        ;GDT 的物理地址    
                        dd 0x00007e00      
                            
       times 510-($-$$) db 0
                        db 0x55,0xaa
```

## 二、内核程序代码

内核程序首先显示 CPU 品牌信息，接着跳转到 load_relocate_program 调用过程，首先调用 allocate_memory 确定分配的内存地址，然后将用户程序全部从磁盘读取到内存中，并且根据读取的用户程序段结构（头部段、代码段、数据段、堆栈段），生成段描述符并安装到 GDT 中，最后把描述符选择子保存到用户程序头部。接着，将用户程序头部的调用例程符号名替换为内核中的入口地址，这个过程被称为**符号地址重定位**，最后跳转到用户程序开始执行。

```armasm{.line-numbers}
       ;代码清单13-2
       ;文件名：c13_core.asm
       ;文件说明：保护模式微型核心程序 
       ;创建日期：2011-10-26 12:11

       ;以下常量定义部分。内核的大部分内容都应当固定
       ;内核代码段选择子（序号为 7） 
       core_code_seg_sel     equ  0x38    
       ;内核数据段选择子（序号为 6）
       core_data_seg_sel     equ  0x30
       ;系统公共例程代码段的选择子（序号为 5） 
       sys_routine_seg_sel   equ  0x28
       ;视频显示缓冲区的段选择子（序号为 4） 
       video_ram_seg_sel     equ  0x20
       ;内核堆栈段选择子（序号为 3）
       core_stack_seg_sel    equ  0x18
       ;整个 0-4GB 内存的段的选择子（序号为 1）
       mem_0_4_gb_seg_sel    equ  0x08

       ;以下是系统核心的头部，用于加载核心程序
       ;核心程序总长度，位于偏移地址 0x00 处 
       core_length      dd core_end
       ;系统公用例程段位置，位于偏移地址 0x04 处
       sys_routine_seg  dd section.sys_routine.start
       ;核心数据段位置，位于偏移地址 0x08 处
       core_data_seg    dd section.core_data.start
       ;核心代码段位置，位于偏移地址 0x0C 处
       core_code_seg    dd section.core_code.start
       ;核心代码段入口点，位于偏移地址 0x10 处，高地址为代码段地址，即代码段的段选择子
       core_entry       dd start
                        dw core_code_seg_sel

       [bits 32]

;=====================🍳系统公共例程代码段🍳=================================

SECTION sys_routine vstart=0                 

;字符串显示例程
;显示 0 终止的字符串并移动光标
;输入：DS:EBX=串地址
put_string:                                  
                                   
       push ecx
.getc:
       ;输入：ds:ebx=串地址，ds 指向数据段的段地址，ebx 初始值指向数据段中字符串
       mov cl,[ebx]
       ;cl=0，如果 cl=0 的话，那么说明整个要显示字符串的结束
       or cl,cl
       ;如果显示到字符串的结尾，返回主程序
       jz .exit
       call put_char
       ;下一个字符
       inc ebx
       jmp .getc

.exit:
       pop ecx
       ;段间返回，RETF 指令不仅恢复指令指针（IP/EIP），还恢复代码段寄存器（CS）。
       retf                               

;在当前光标处显示一个字符，并推进光标，仅用于段内调用 
;输入：CL=字符 ASCII 码 
put_char:                                   
       ;作用是把通用寄存器压栈，寄存器的入栈顺序依次是：EAX,ECX,EDX,EBX,ESP(初始值),EBP,ESI,EDI.
       pushad

       ;以下两段代码获取当前光标位置，即光标寄存器的高 8 位和低 8 位分别保存到 ah 和 al 中
       ;索引寄存器的端口号是 0x3d4，可以向它写入一个值，用来指定内部的某个寄存器。比如，两个 8 位的光标寄存器，其索引值分别是 14 (0x0e) 和 15 (0x0f)，分别用于提供光标位置的高 8 位和低 8 位；指定寄存器后，需要读写数据，可以通过 0x3d5 来进行
       mov dx,0x3d4
       mov al,0x0e
       ;索引寄存器的端口号是 0x3d4，将 0x0e 的值写入此索引寄存器（al -> dx）
       ;指定想要访问光标寄存器（高 8 位）
       out dx,al
       inc dx                             
       ;高字
       in al,dx                           
       mov ah,al

       ;取当前光标位置的低 8 位到寄存器 al 中
       dec dx                             
       mov al,0x0f
       out dx,al
       ;0x3d5
       inc dx                             
       in al,dx                           
       ;bx = 代表光标位置的 16 位数
       mov bx,ax                          

       ;判断要显示的字符 cl 是否为回车符？
       cmp cl,0x0d     
       ;如果不是，看看是不是换行等字符                   
       jnz .put_0a
       ;如果是回车，就将当前光标转移到当前行行首
       mov ax,bx
       mov bl,80
       ;每行有 80 个字符，用当前光标的位置除以 80，商 al 寄存器中就是当前光标所在行号
       div bl
       ;将 al 中的内容乘以 bl 中的 80，会在结果 ax 寄存器中得到当前行行首的光标值
       ;字节乘法 mul，则 al×REG8/MEM8，乘积存于 ax 中
       mul bl
       mov bx,ax
       jmp .set_cursor

  .put_0a:
       ;判断要显示的字符 cl 是否为换行符？
       cmp cl,0x0a
       ;不是，那就正常显示字符
       jnz .put_other
       ;如果是换行符，就将当前的光标移动到下一行
       ;如果当前行在屏幕中是最后一行，就需要进行滚屏 
       add bx,80
       jmp .roll_screen

  .put_other:                               
       ;正常显示字符，在内存中，0xB8000～0xBFFFF 是留给显卡的，由显卡来显示文本
       ;使用 es 作为显存寄存器，段地址为 0xb800，偏移地址为 0x0000~0x7FFF
       push es
       ;0xb8000 段的选择子
       mov eax,video_ram_seg_sel
       mov es,eax
       ;由于一个字符在显存中对应两个字节，所以可以将光标位置乘以 2 来得到该位置在显存中的偏移地址
       shl bx,1
       mov [es:bx],cl
       pop es

       ;以下将光标位置推进一个字符
       shr bx,1
       inc bx

  .roll_screen:
       ;光标超出屏幕？滚屏
       cmp bx,2000
       jl .set_cursor

       push ds
       push es
       ;0xb8000 段的选择子
       mov eax,video_ram_seg_sel
       mov ds,eax
       mov es,eax
       ;从 ds:esi 传送到的目的地址 es:edi，并且 cld 表示要进行正向传输，即从低地址传送到高地址
       cld
       ;将屏幕上第 2~25 行的内容整体往上提 1 行，最后用黑底白字的空白字符填充第 25 行
       mov esi,0xa0
       mov edi,0x00
       mov ecx,1920
       ;将屏幕上第 2~25 行的内容整体往上提 1 行，最后用黑底白字的空白字符填充第 25 行
       rep movsw
       ;清除屏幕最底一行
       mov bx,3840
       ;32 位程序应该使用 ECX
       mov ecx,80                         
  .cls:
       ;0x0720 中的 0x20 表示空白字符（SPACE），0x07 表示黑底白字
       mov word [es:bx],0x0720
       add bx,2
       loop .cls

       pop es
       pop ds
       ;将光标移到至最后一行的开头处
       mov bx,1920

  .set_cursor:
       ;不管是回车、换行，还是显示可打印字符，上面都给出了光标位置的新数值
       ;下面就是将新的光标位置在屏幕上显示出来
       mov dx,0x3d4
       mov al,0x0e
       ;al -> dx，指定要访问光标寄存器（高 8 位）
       out dx,al
       ;需要往光标寄存器写数据
       inc dx                             
       mov al,bh
       ;将光标位置的高 8 位写入
       out dx,al
       dec dx                             
       mov al,0x0f
       ;al -> dx，指定要访问光标寄存器（低 8 位）
       out dx,al
       inc dx                             
       mov al,bl
       ;将光标位置的低 8 位写入
       out dx,al

       popad
       ret                                

;从硬盘读取一个逻辑扇区
;EAX=逻辑扇区号
;DS:EBX=目标缓冲区地址
;返回：EBX=EBX+512
read_hard_disk_0:                           
       push eax 
       push ecx
       push edx

       push eax
       
       mov dx,0x1f2
       mov al,1
       ;读取的扇区数
       out dx,al                          

       inc dx                             
       pop eax
       out dx,al

       inc dx
       mov cl,8
       shr eax,cl
       out dx,al

       inc dx   
       shr eax,cl
       out dx,al

       inc dx
       shr eax,cl
       or al,0xe0
       out dx,al

       inc dx
       mov al,0x20
       out dx,al

  .waits:
       in al,dx
       and al,0x88
       cmp al,0x08
       jnz .waits                          

       mov ecx,256
       mov dx,0x1f0
  .readw:
       in ax,dx
       mov [ebx],ax
       add ebx,2
       loop .readw

       pop edx
       pop ecx
       pop eax

       retf                               ;段间返回 

      
;分配内存
;输入：ECX=希望分配的字节数
;输出：ECX=起始线性地址 
allocate_memory:                            
       push ds
       push eax
       push ebx

       ;将内核数据段选择子写入 eax 中
       mov eax,core_data_seg_sel
       mov ds,eax

       ;获取可用于内存分配的初始内存地址
       mov eax,[ram_alloc]
       ;将 eax + 分配的字节数来计算下一次分配时的起始地址
       add eax,ecx                        
       ;在 ecx 中得到本次分配到的起始内存地址，并返回给调用者
       mov ecx,[ram_alloc]

       ;在 32 位计算机中，分配的内存地址建议为 4 字节对齐的
       mov ebx,eax
       ;能够被 4 整除的数的低 2 位均为 0，下面将 ebx 中的内存地址进行 4 字节对齐
       and ebx,0xfffffffc
       add ebx,4         
       ;如果原先 eax 中的地址是 4 字节对齐的，那么就直接使用 eax 写回内存作为下一次分配的起始地址
       ;否则，就使用计算出来的 4 字节对齐的 ebx 作为下一次分配的起始地址
       test eax,0x00000003                
       cmovnz eax,ebx                    
       mov [ram_alloc],eax               
                                           
       pop ebx
       pop eax
       pop ds

       retf

;在 gdt 内安装一个新的描述符
;输入：edx:eax=描述符 
;输出：cx=描述符的选择子
set_up_gdt_descriptor:  

       push eax
       push ebx
       push edx
       push ds
       push es

       ;切换到核心数据段
       mov ebx,core_data_seg_sel
       mov ds,ebx
       ;sgdt m 指令，其中 m 是一个 6 字节的内存区域的首地址
       ;以便开始处理 GDT，将 GDTR 寄存器的基地址和边界信息保存到指定内存位置
       ;将低 2 字节用于保存 GDT 的界限，高 4 字节用于保存 GDT 的 32 位物理地址
       sgdt [pgdt]

       mov ebx,mem_0_4_gb_seg_sel
       mov es,ebx

       ;下面的工作是计算描述符的安装地址：先得到描述符表的界限值，将它加 1，得到描述符表的总字节数，这实际上也是新描述符在 GDT 内的偏移量。
       ;然后用 GDT 的线性地址加上这个偏移量，就是用于安装新描述符的线性地址。

       ;GDT 界限
       ;movzx 用于将一个较小的数据值零扩展到更大的寄存器中。其含义是 "Move with Zero-Extend"，即将源操作数加载到目标寄存器时，用 0 填充高位。
       movzx ebx,word [pgdt]
       ;GDT 总字节数，也是下一个描述符偏移 
       inc bx
       ;下一个描述符的线性地址
       add ebx,[pgdt+2]

       mov [es:ebx],eax
       mov [es:ebx+4],edx

       ;增加一个描述符的大小
       ;在机器刚加电或处理器复位后，GDT 的基地址被默认地设置为 0，而界限值被设置成 0xFFFF
       add word [pgdt],8                     
       ;对 GDT 的更改生效
       lgdt [pgdt]                         
       ;得到 GDT 最新的界限值
       mov ax,[pgdt]                      
       xor dx,dx
       mov bx,8
       ;除以 8，去掉余数，此时商 ax 就是新安装的段描述符的索引号
       div bx                             
       mov cx,ax
       ;将索引号移到正确位置，此时 RPL=0，TI=0（表示在 GDT 中）
       shl cx,3

       pop es
       pop ds

       pop edx
       pop ebx
       pop eax

       retf 

;构造存储器和系统的段描述符
;输入：eax=线性基地址
;      ebx=段界限
;      ecx=属性，各属性位都在原始位置，无关的位清零 
;返回：edx:eax=描述符
make_seg_descriptor:    

       mov edx,eax
       shl eax,16
       ;描述符前 32 位 eax 构造完毕
       or ax,bx                           

       ;清除基地址中无关的位
       and edx,0xffff0000                 
       rol edx,8
       ;装配基址的 31~24 和 23~16 (80486+)
       bswap edx                          

       xor bx,bx
       ;装配段界限的高 4 位
       or edx,ebx                         
       ;装配属性
       or edx,ecx                         

       retf

;========================🍕内核数据段🍕======================================

SECTION core_data vstart=0                

       ;用于设置和修改 GDT
       ;这里的低 2 字节保存 GDT 的界限，而高 4 字节保存 GDT 的 32 位物理地址
       pgdt             dw  0
                        dd  0

       ;下次分配内存时的起始地址
       ram_alloc        dd  0x00100000    

       ;符号地址检索表
       salt:
       salt_1           db  '@PrintString'
                        times 256-($-salt_1) db 0
                        dd  put_string
                        dw  sys_routine_seg_sel

       salt_2           db  '@ReadDiskData'
                        times 256-($-salt_2) db 0
                        dd  read_hard_disk_0
                        dw  sys_routine_seg_sel

       salt_3           db  '@PrintDwordAsHexString'
                        times 256-($-salt_3) db 0
                        dd  put_hex_dword
                        dw  sys_routine_seg_sel

       salt_4           db  '@TerminateProgram'
                        times 256-($-salt_4) db 0
                        dd  return_point
                        dw  core_code_seg_sel

       salt_item_len    equ $-salt_4
       salt_items       equ ($-salt)/salt_item_len

       message_1        db  '  If you seen this message,that means we '
                        db  'are now in protect mode,and the system '
                        db  'core is loaded,and the video display '
                        db  'routine works perfectly.',0x0d,0x0a,0

       message_5        db  '  Loading user program...',0
       
       do_status        db  'Done.',0x0d,0x0a,0
       
       message_6        db  0x0d,0x0a,0x0d,0x0a,0x0d,0x0a
                        db  '  User program terminated,control returned.',0

       bin_hex          db '0123456789ABCDEF'
       ;put_hex_dword子过程用的查找表
       ;内核用的缓冲区 
       core_buf         times 2048 db 0         
       ;内核用来临时保存自己的栈指针
       esp_pointer      dd 0                   
       cpu_brnd0        db 0x0d,0x0a,'  ',0
       cpu_brand        times 52 db 0
       cpu_brnd1        db 0x0d,0x0a,0x0d,0x0a,0


;========================🍉内核代码段🍉======================================
SECTION core_code vstart=0

;加载并重定位用户程序
;输入：ESI=起始逻辑扇区号
;返回：AX=指向用户程序头部的选择子 
load_relocate_program:

       push ebx
       push ecx
       push edx
       push esi
       push edi

       push ds
       push es

       ;将内核的数据段选择子保存到 ds 寄存器中
       mov eax,core_data_seg_sel
       ;切换 ds 到内核数据段
       mov ds,eax

       ;读取程序头部数据
       mov eax,esi    
       ;core_buf 是在内核数据段中定义的缓冲区，大小为 2048 个字节
       mov ebx,core_buf
       ;从 eax 开始的逻辑扇区号读取一个扇区的数据到 ds:ebx 处的内存缓冲区区域
       ;这样可以把用户程序头部数据读取到前面内核定义的缓冲区中
       call sys_routine_seg_sel:read_hard_disk_0

       ;以下判断整个程序有多大
       ;首先获取用户程序整体尺寸
       mov eax,[core_buf]
       mov ebx,eax
       ;所有能被 512 整除的数，最低端的 9 个比特都为 0
       ;and 指令使程序尺寸 512 字节对齐（能够被 512 整除） 
       and ebx,0xfffffe00  
       ;如果用户程序大小不为 512 的整数倍，那么下面指令会对程序大小凑整，正好为用户程序所占用的扇区数
       ;如果用户程序大小为 512 的整数倍，那么下面指令会将用户程序占用的扇区数多增加 1      
       add ebx,512
       ;test 指令是一条逻辑指令，用于执行按位与操作，并根据结果更新处理器的状态标志，test 指令不改变任何操作数的值
       ;程序的大小正好是 512 的倍数吗?
       test eax,0x000001ff                
       ;不是，就使用凑整的结果 ebx
       ;是，就使用原来程序的大小 eax
       cmovnz eax,ebx

       ;实际需要申请的内存字节大小（前面计算的程序占用的扇区数 * 512）
       mov ecx,eax
       call sys_routine_seg_sel:allocate_memory
       ;ebx -> 申请到的内存首地址
       mov ebx,ecx
       ;保存为该内存段分配的起始地址
       push ebx
       xor edx,edx
       mov ecx,512
       ;32 位的 div 指令会对 64 位的被除数 edx:eax 进行除法运算，商存储在 eax，余数存储在 edx
       div ecx
       ;总扇区数
       mov ecx,eax                         

       ;切换 ds 到 0-4GB 的数据段
       mov eax,mem_0_4_gb_seg_sel         
       mov ds,eax

       ;起始扇区号
       mov eax,esi                         
  .b1: 
       ;从 eax 指定的逻辑扇区号读取一个扇区数据到内存 ds:ebx 的区域
       ;ebx 就是 allocate_memory 函数返回的内存起始地址
       call sys_routine_seg_sel:read_hard_disk_0
       inc eax
       ;循环读，直到读完整个用户程序
       loop .b1                           

       ;建立用户程序头部段描述符
       ;前面将申请到的用户程序在内存首地址 ebx 压入栈，现在将用户程序头部起始线性地址弹出到 edi 寄存器中
       pop edi
       mov eax,edi
       ;获取用户程序头部段长度
       mov ebx,[edi+0x04]
       ;获取用户程序头部段的段界限
       dec ebx
       mov ecx,0x00409200
       ;生成字节粒度的数据段描述符
       call sys_routine_seg_sel:make_seg_descriptor
       ;将生成的用户程序头部段描述符安装到 GDT 中
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;将此描述符选择子保存到用户程序头部 0x04 偏移处
       mov [edi+0x04],cx

       ;建立用户程序代码段描述符
       mov eax,edi
       ;代码起始线性地址
       add eax,[edi+0x14]
       ;代码段长度
       mov ebx,[edi+0x18]
       ;代码段界限
       dec ebx                            
       mov ecx,0x00409800
       ;eax=线性基地址，ebx=段界限，ecx=属性
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;将用户程序代码段描述符的选择子保存到用户程序头部 0x14 偏移处
       mov [edi+0x14],cx

       ;建立用户程序数据段描述符
       mov eax,edi
       ;数据段起始线性地址
       add eax,[edi+0x1c]
       ;段长度
       mov ebx,[edi+0x20]
       ;段界限
       dec ebx                            
       ;字节粒度的数据段描述符
       mov ecx,0x00409200
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       ;将用户程序数据段描述符选择子保存到 0x1c 偏移处
       mov [edi+0x1c],cx

       ;建立程序堆栈段描述符
       ;4KB 的倍率 
       mov ecx,[edi+0x0c]
       mov ebx,0x000fffff
       ;得到段界限
       sub ebx,ecx
       mov eax,4096
       ;用 eax 寄存器中的值乘以另外一个 32 位的数，在 edx:eax 中得到 64 位的结果
       mul dword [edi+0x0c]
       ;准备为堆栈分配内存
       mov ecx,eax
       call sys_routine_seg_sel:allocate_memory
       ;得到堆栈的高端物理地址
       add eax,ecx
       ;4KB 粒度的堆栈段描述符
       mov ecx,0x00c09600
       call sys_routine_seg_sel:make_seg_descriptor
       call sys_routine_seg_sel:set_up_gdt_descriptor
       mov [edi+0x08],cx

       ;重定位 SALT
       mov eax,[edi+0x04]
       ;es 为用户程序头部段的描述符选择子
       mov es,eax
       ;ds 为内核程序数据段的描述符选择子
       mov eax,core_data_seg_sel
       mov ds,eax

       ;cld 将 EFLAGS 寄存器中的方向标志 DF 清零，地址按递增的方向进行比较
       cld

       ;用户程序的 U-SALT 条目数，也是外循环的循环次数
       mov ecx,[es:0x24]
       ;用户程序内的 U-SALT 位于头部内 0x28 处，es:edi 指向用户程序 U-SALT 表条目
       mov edi,0x28
  .b2:
       push ecx
       push edi

       ;内核程序中 C-SALT 条目的数量，也是内循环的循环次数
       mov ecx,salt_items
       ;ds:esi 指向内核程序的 C-SALT 条目
       ;es:edi 指向用户程序的 U-SALT 条目
       mov esi,salt
  .b3:
       push edi
       push esi
       push ecx

       ;检索表中，每条目的比较次数
       mov ecx,64
       ;每次比较 4 字节，repe 若相等则重复
       repe cmpsd
       jnz .b4
       ;若匹配，esi 恰好指向内核程序中符号名后的偏移地址
       mov eax,[esi]                      
       ;对用户程序的 salt 表条目依次写入 4 字节的偏移地址和 2 字节的段选择子 
       mov [es:edi-256],eax
       mov ax,[esi+4]
       mov [es:edi-252],ax
  .b4:
       pop ecx
       pop esi
       ;esi 加上 salt_item_len，指向内核 salt 表中下一个条目
       add esi,salt_item_len
       ;从头比较
       pop edi
       loop .b3

       pop edi
       ;edi 加上 256，指向用户程序 salt 表中下一个条目
       add edi,256
       pop ecx
       loop .b2

       ;将用户程序头部的选择子保存到 ax 寄存器中
       mov ax,[es:0x04]
       ;恢复到调用此过程前的 es 段
       pop es                             
       ;恢复到调用此过程前的 ds 段
       pop ds                             

       pop edi
       pop esi
       pop edx
       pop ecx
       pop ebx

       ret
      
start:
       ;使 ds 指向核心数据段
       mov ecx,core_data_seg_sel
       mov ds,ecx

       ;调用 put_string 例程来在屏幕上显示字符串 message_1 信息
       mov ebx,message_1
       ;调用 put_string 例程显示 0 终止的字符串并移动光标，输入字符串地址由 DS:EBX 给出
       call sys_routine_seg_sel:put_string
                                   
       ;显示处理器品牌信息 
       mov eax,0x80000002
       cpuid
       mov [cpu_brand + 0x00],eax
       mov [cpu_brand + 0x04],ebx
       mov [cpu_brand + 0x08],ecx
       mov [cpu_brand + 0x0c],edx

       mov eax,0x80000003
       cpuid
       mov [cpu_brand + 0x10],eax
       mov [cpu_brand + 0x14],ebx
       mov [cpu_brand + 0x18],ecx
       mov [cpu_brand + 0x1c],edx

       mov eax,0x80000004
       cpuid
       mov [cpu_brand + 0x20],eax
       mov [cpu_brand + 0x24],ebx
       mov [cpu_brand + 0x28],ecx
       mov [cpu_brand + 0x2c],edx

       ;从处理器返回的数据都是现成的 ASCII 码，先在屏幕上留出空行再显示处理器品牌信息，然后再留空，以突出要显示的内容
       mov ebx,cpu_brnd0
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brand
       call sys_routine_seg_sel:put_string
       mov ebx,cpu_brnd1
       call sys_routine_seg_sel:put_string

       ;显示信息，要开始加载用户程序
       mov ebx,message_5
       call sys_routine_seg_sel:put_string
       ;用户程序位于逻辑 50 扇区
       mov esi,50                           
       call load_relocate_program

       mov ebx,do_status
       call sys_routine_seg_sel:put_string
       ;临时保存堆栈指针
       mov [esp_pointer],esp               
       ;在 ds 段寄存器中保存用户程序头部段描述子
       mov ds,ax
       ;控制权交给用户程序（入口点）
       ;堆栈可能切换
       jmp far [0x10]                      
                                            

return_point:              
       ;用户程序返回点                  
       ;使 ds 指向核心数据段
       mov eax,core_data_seg_sel           
       mov ds,eax
       ;切换回内核自己的堆栈
       mov eax,core_stack_seg_sel          
       mov ss,eax 
       mov esp,[esp_pointer]

       mov ebx,message_6
       call sys_routine_seg_sel:put_string

       ;这里可以放置清除用户程序各种描述符的指令
       ;也可以加载并启动其它程序
       hlt
            
SECTION core_trail

core_end:
```

## 三、用户程序代码

用户程序将磁盘中的文件读取并显示在屏幕上，最后调用 TerminateProgram 例程返回内核程序。

```armasm{.line-numbers}
       ;代码清单13-3
       ;文件名：c13.asm
       ;文件说明：用户程序 
       ;创建日期：2011-10-30 15:19   
         
SECTION header vstart=0

       ;程序总长度 #0x00
       program_length   dd program_end
       ;程序头部的长度 #0x04，保存用户程序头部段描述子
       head_len         dd header_end
       ;用于接收堆栈段选择子 #0x08，保存堆栈段描述子
       stack_seg        dd 0
       ;用户程序建议的堆栈大小（以 4KB 位单位）#0x0c
       stack_len        dd 1
                                  
       ;程序入口地址 #0x10           
       prgentry         dd start                
       ;代码段位置 #0x14
       ;当内核完成用户程序的加载和重定位后，会把该代码段的段选择子回填到这里（仅占用最低 16 位）
       code_seg         dd section.code.start   
       ;代码段长度 #0x18
       code_len         dd code_end

       ;数据段位置 #0x1c，保存数据段描述子
       data_seg         dd section.data.start   
       ;数据段长度 #0x20
       data_len         dd data_end             
       
       ;符号地址检索表的条目数 #0x24
       salt_items       dd (header_end-salt)/256 
       
       ;#0x28
       ;用户程序在偏移地址 0x28 处构造一个表格，填写用户程序需要用到的符号名
       ;内核在加载用户程序时，会将符号名称替换为相应的内存地址，这就是过程的重定位
       salt:
       PrintString      db  '@PrintString'
                        times 256-($-PrintString) db 0
       TerminateProgram db  '@TerminateProgram'
                        times 256-($-TerminateProgram) db 0
       ReadDiskData     db  '@ReadDiskData'
                        times 256-($-ReadDiskData) db 0
                 
       header_end:

SECTION data vstart=0    
                         
       buffer            times 1024 db  0         

       message_1         db  0x0d,0x0a,0x0d,0x0a
                         db  '**********User program is runing**********'
                         db  0x0d,0x0a,0
       message_2         db  '  Disk data:',0x0d,0x0a,0

data_end:

      [bits 32]

SECTION code vstart=0
start:
       ;在内核程序跳转到用户程序之前，就将用户程序头部段的选择子保存到 ds 寄存器中
       mov eax,ds
       mov fs,eax

       ;切换到用户程序自己的栈
       mov eax,[stack_seg]
       mov ss,eax
       mov esp,0
       ;切换到用户程序自己的数据段，现在 ds 保存的是用户数据段而不是头部段的选择子
       mov eax,[data_seg]
       mov ds,eax

       mov ebx,message_1
       ;调用内核中的 PrintString 方法，显示 message_1 字符串
       ;这里调用分两步执行，第 1 步就是根据 fs:PrintString 找到用户程序头部段中 PrintString 标号处
       ;第 2 步就是根据 PrintString 标号处的内核调用例程段选择子和偏移地址，执行 32 位间接绝对远调用，跳转到内核的打印例程执行
       call far [fs:PrintString]
       ;读取硬盘逻辑扇区号 100 中的数据到缓冲区中
       mov eax,100
       ;缓冲区 buffer 偏移地址
       mov ebx,buffer
       ;执行内核的 read_hard_disk_0 例程，从 eax 磁盘号读取数据到 buffer 缓冲区
       call far [fs:ReadDiskData]

       mov ebx,message_2
       call far [fs:PrintString]

       mov ebx,buffer
       call far [fs:PrintString]
       ;将控制权返回到系统
       jmp far [fs:TerminateProgram]

code_end:

SECTION trail
program_end:
```